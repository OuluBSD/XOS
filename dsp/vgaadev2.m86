	.TITLE	vgaadev2 - Loadable driver for basic VGA display

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.PROC	80486
	.INCLUD	XOSINC:\xmac\xos.par
	.INCLUD	XOSINC:\xmac\xosx.par
	.INCLUD	XOSINC:\xmac\xoserr.par
	.INCLUD	XOSINC:\xmac\xostrm.par
	.INCLUD	XOSINC:\xmac\xosxtrm.par
	.INCLUD	XOSINC:\xmac\xosdos.par
	.INCLUD	XOSINC:\xmac\pcat.par
	.INCLUD	XOSINC:\xmac\xoslke.par
	.INCLUD	XOSINC:\xmac\xosxlke.par

	LKECONT

	CODE

	.SBTTL	vgadspmode - Function to get and set display mode

;Function to get and set display mode
;	c{EAX} = Display mode bits
;	c{EBX} = Address of data block
;	c{ESI} = Offset of TDB
;	CALL	vgadspmode
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EAX} = Current display mode
;  Stack frame offsets are defined in XOSXTRM.PAR so they will also be
;    available to the extension driver

4$:	LEAVE
	RET

vgadspmode::
	ENTER	dm_SIZE
	CALL	xosconChkBiosMd##	;Convert BIOS mode requests
	JC	4$			;If error
	MOVL	adm_bits[EBP], EAX
	MOVL	adm_dataadr+0[EBP], EBX ;Store address of caller's data block
	MOVL	adm_dataadr+4[EBP], ES
	CMPB	AL, #0			;Changing the mode?
	JE	rtnmode			;No - just return the current values
	PUSHL	ESI			;Yes - copy new data values
	PUSHL	EDI
	MOVL	ESI, EDX
	LEAL	EDI, adm_dv[EBP]
	PUSHL	DS
	POPL	ES
	CLD
	MOVL	ECX, #5
	IFFAULT	knlRtnAdrErLv##
	RMOVSL	[EDI], [ESI]
	POPL	EDI
	POPL	ESI
	TESTB	tdb_stsdsp[ESI], #TSD$ACTDSP ;Is this the active display?
	JE	8$			;No
	MOVL	EAX, tdb_dhdb[ESI]	;Yes - is the session menu on screen?
	TESTB	dhdb_keysts[EAX], #~KS$SYSSHFT
	JE	8$			;No - go on
	MOVL	EAX, #0x05		;Yes - tell the screen symbiont about
	CALL	ssdialog		;  this
8$:	CMPB	adm_bits[EBP], #1	;Text mode?
	JNE	vgagraphic		;No - graphics mode
					;Yes - fall into vgatext on next page
.PAGE
;Here for mode 1 - In mode 1 we must determine the table which most closely
;  matches the parameters specified by the user.  The only parameters which
;  are considered are the character rows and character columns values.  Only
;  12 combinations are supported:
;	Cols  Rows  Horiz  Vert  Font
;	 80    25    720    400  9x16
;	 80    30    720    480  9x16
;	 80    50    720    400  9x8
;	 80    60    720    480  9x8
;	100    25    800    400  8x16
;	100    30    800    480  8x16
;	100    50    800    400  8x8
;	100    60    800    480  8x8
;	132    25   1056    400  8x16
;	132    30   1056    480  8x16
;	132    50   1056    400  8x8
;	132    60   1056    480  8x8
;  The 80 column configurations are supported on all VGAs.  The others are only
;  available on VGAs which can generate the necessary clock frequencies.

;  The configuration is selected based on the column and row values specified.
;  The lowest resolution which is at least equal to both the specified
;  column and row values is used.  If a column value is not given 80 is used.
;  If a row value is not given, 25 is used.

vgatext:CMPB	tdb_mode[ESI], #DM_TEXT	;Are we in text mode now?
	JBE	6$			;Yes - go on
	PUSHL	ESI			;No - switch from graphic mode to text
	PUSHL	#DM_TEXT		;  mode (BIOS mode 3)
	PUSHL	#0
	PUSHL	#0
	CALL	vgasendmode#
	TESTL	EAX, EAX		;If this fails, there isn't much we
	JNS	6$			;  do!
	CRASH	CCGM			;[Can't Clear Graphic Mode]

6$:	CMPL	adm_dv+dm_columns[EBP], #0 ;Columns value given?
	JNE	8$			;Yes
	MOVB	adm_dv+dm_columns[EBP], #80t ;No - use default
8$:	CMPL	adm_dv+dm_rows[EBP], #0 ;Rows value given?
	JNE	10$			;Yes
	MOVB	adm_dv+dm_rows[EBP], #25t ;No - use default
10$:	CMPL	adm_dv+dm_columns[EBP], #80t ;Can we use 80 columns?
	JA	14$			;No
	MOVL	EBX, #vgatable0#
	CMPL	adm_dv+dm_rows[EBP], #25t ;Yes - can we use 25 rows?
	JBE	havetbl			;Yes
	MOVL	EBX, #vgatable1#	;No
	CMPL	adm_dv+dm_rows[EBP], #30t ;Can we use 30 rows?
	JBE	havetbl			;Yes
	MOVL	EBX, #vgatable2#	;No
	CMPL	adm_dv+dm_rows[EBP], #50t ;Can we use 50 rows?
	JBE	havetbl			;Yes
	MOVL	EBX, #vgatable3#	;No - use 60 rows
	JMP	havetbl

;Here if need more than 80 columns

14$:	CMPL	adm_dv+dm_columns[EBP], #100t ;Can we use 100 columns?
	JA	16$			;No
	MOVL	EBX, #vgatable4#
	CMPL	adm_dv+dm_rows[EBP], #25t ;Yes - can we use 25 rows?
	JBE	havetbl			;Yes
	MOVL	EBX, #vgatable5#	;No
	CMPL	adm_dv+dm_rows[EBP], #30t ;Can we use 30 rows?
	JBE	havetbl			;Yes
	MOVL	EBX, #vgatable6#	;No
	CMPL	adm_dv+dm_rows[EBP], #50t ;Can we use 50 rows?
	JBE	havetbl			;Yes
	MOVL	EBX, #vgatable7#	;No - use 60 rows
	JMP	havetbl

;Here if need more than 100 columns

16$:	MOVL	EBX, #vgatable8#
	CMPL	adm_dv+dm_rows[EBP], #25t ;Yes - can we use 25 rows?
	JBE	havetbl			;Yes
	MOVL	EBX, #vgatable9#	;No
	CMPL	adm_dv+dm_rows[EBP], #30t ;Can we use 30 rows?
	JBE	havetbl			;Yes
	MOVL	EBX, #vgatable10#	;No
	CMPL	adm_dv+dm_rows[EBP], #50t ;Can we use 50 rows?
	JBE	havetbl			;Yes
	MOVL	EBX, #vgatable11#	;No - use 60 rows
	JMP	havetbl
.PAGE
;Here with the table for the requested mode and configuration

havetbl:MOVL	tdb_table[ESI], EBX	;Store offset of new table
	MOVL	EAX, adm_bits[EBP]
	MOVZBL	ECX, tdb_mode[ESI]	;Get current display mode
	MOVB	tdb_mode[ESI], AL	;Store new mode
	CLRL	EAX
	MOVW	tdb_sattrval[ESI], AX	;Clear saved attribute values
	MOVB	tdb_dsppage[ESI], AL	;Set to page 0
	MOVL	EAX, tbl_pgsize[EBX]	;Get page size
	MOVL	tdb_pgsize[ESI], EAX
	MOVL	EAX, tbl_horiz[EBX]
	MOVL	tdb_horiz[ESI], EAX
	MOVL	EAX, tbl_vert[EBX]
	MOVL	tdb_vert[ESI], EAX
	MOVL	EAX, tbl_columns[EBX]
	MOVL	tdb_columns[ESI], EAX
	MOVL	EAX, tbl_rows[EBX]
	MOVL	tdb_rows[ESI], EAX
	MOVZBL	EAX, tbl_font[EBX]
	MOVB	AL, vgafontsize#[EAX*2]
	MOVB	tdb_charch[ESI], AL
	MOVW	AX, tbl_crtregs+0Ah[EBX]
	XCHGB	AL, AH
	MOVW	tdb_dcurmode[ESI], AX
	MOVB	AH, tbl_crtregs+07h[EBX]
	SHRB	AH, #1
	MOVB	AL, AH
	SHRB	AH, #3
	ANDB	AL, #0x01
	ANDB	AH, #0x02
	ORB	AL, AH
	MOVB	tdb_dvertend+1[ESI], AL
	MOVB	AL, tbl_crtregs+12h[EBX]
	MOVB	tdb_dvertend+0[ESI], AL
	MOVB	AL, tbl_seqregs+3h[EBX]
	MOVB	tdb_dfontsel[ESI], AL
	MOVL	tdb_dstart[ESI], #0
	PUSHL	EDI			;Free up another register
	LEAL	EDI, tdb_dacdata[ESI]	;Initialize the palette data
	PUSHL	DS
	POPL	ES
	PUSHL	ESI
	MOVL	ESI, #colcol#
	MOVL	ECX, #64t*3/4		;Get number of values to copy
	CLD
	RMOVSL	[EDI], [ESI]		;Copy the DAC values to the TDB
	POPL	ESI
	POPL	EDI
	TESTB	tdb_stsdsp[ESI], #TSD$ACTDSP ;Is this the active display?
	JE	22$			;No
	CALL	vgasetdhs2		;Yes - set up state of the display
22$:	BTL	adm_bits[EBP], #DM%NOCLEAR ;Are we clearing the screen?
	JC	23$			;No
	CLRL	EAX			;Yes - reset all cursor positions
	MOVL	tdb_curpos+0[ESI], EAX
	MOVL	tdb_curpos+4[ESI], EAX
	MOVL	tdb_curpos+8[ESI], EAX
	MOVL	tdb_curpos+12t[ESI], EAX
23$:	CMPB	knlCrashed##, #0	;Have we crashed?
	JNE	28$			;Yes
	CALL	xosconDosChk##		;No - is this the terminal for a DOS
					;  process?
	JE	28$			;No
	MOVL	bda_vcurpos+0, EAX	;Yes - also reset cursor position data
	MOVL	bda_vcurpos+4, EAX	;  on page 0
	MOVL	bda_vcurpos+8, EAX
	MOVL	bda_vcurpos+12t, EAX
	MOVB	bda_vdsppage, AL	;Reset display page number
	MOVW	bda_vvidstart, AX	;Reset display page offset
	MOVB	AL, tdb_rows[ESI]	;Store number of rows on screen
	MOVB	bda_vrows, AL
	MOVZBL	EAX, tdb_charch[ESI]	;Store scan rows per character
	MOVW	bda_vpoints, AX
28$:	CMPB	tdb_dcurmode+1[ESI], #0 ;Have cursor ending value?
	JE	30$			;No
	MOVB	AL, #118t		;Yes
30$:	MOVW	tdb_curmode[ESI], AX
	MOVW	AX, tdb_battrval[ESI]	;Initialize text mode attributes
	MOVW	tdb_tattrval[ESI], AX
	MOVB	tdb_scntop[ESI], #1	;Initialize scrolling region to entire
	MOVB	AL, tdb_rows[ESI]	;  screen
	MOVB	tdb_scnbtm[ESI], AL
	BTL	adm_bits[EBP], #DM%NOCLEAR ;Want to clear the buffer?
	JC	34$			;No
	CLRL	EDX			;Yes
32$:	MOVL	EAX, #' '		;Get value for clearing text screen
	MOVB	AH, tdb_tattrfil[ESI]
	PUSHL	EDX
	CALL	vgaclrbfrpage#
	POPL	EDX
	INCL	EDX
	CMPL	EDX, #16t
	JB	32$
	CALL	xosconClrPgChng##
34$:	CALL	vgawrtchargen#
	MOVL	EDX, #16t
	MOVL	EAX, #0xFE
	CALL	vgaclrbfrpage#
	MOVL	EDX, #17t
	MOVL	EAX, #0xFF
	CALL	vgaclrbfrpage#
	MOVL	EDX, #18t
36$:	CLRL	EAX
	PUSHL	EDX
	CALL	vgaclrbfrpage#
	POPL	EDX
	INCL	EDX
	CMPL	EDX, #32t
	JB	36$
	CALL	vgarestore#
	TESTB	tdb_stsdsp[ESI], #TSD$ACTDSP ;Is this the active display?
	JNE	40$			;Yes - go on
	MOVL	EAX, #0x02		;Assume cleared everything
	BTL	adm_bits[EBP], #DM%NOCLEAR ;Right?
	JNC	38$			;Yes
	INCL	EAX			;No - just clear the character generator
38$:	CALL	ssdialog
40$:	CALL	vgaenable		;And enable display
	JMP	rtnmode
.PAGE
;Here when setting to a graphics mode

vgagraphic:
	PUSHL	ESI			;Address of TDB
	MOVZBL	EAX, adm_bits[EBP]	;New mode
	PUSHL	EAX
	PUSHL	adm_dv+dm_horiz[EBP]	;Desired new X resolution
	PUSHL	adm_dv+dm_vert[EBP]  	;Desired new Y resolution
	CALL	vgasendmode#		;Send message to the graphics symbiont
	TESTL	EAX, EAX
	JS	modedn			;If error
	CLRL	EAX
	MOVW	tdb_sattrval[ESI], AX	;Clear saved attribute values
	MOVB	tdb_dsppage[ESI], AL	;Set page 0
	MOVL	tdb_columns[ESI], EAX	;No character values
	MOVL	tdb_rows[ESI], EAX
	MOVL	EAX, #4t		;Tell the screen symbiont about this
	CALL	vgassstatus#
					;Fall into rtnmode on next page
.PAGE
;Here to return current values

rtnmode:MOVZBL	EAX, tdb_mode[ESI]
	BTL	adm_bits[EBP], #DM%RTNDATA ;Want data values returned?
	JNC	4$			;No
	MOVL	EBX, adm_dataadr[EBP]	;Yes - get address of his data block
	MOVL	EDX, tdb_disptype[ESI]
	IFFAULT	knlRtnAdrErLv##
	MOVL	dm_disptype[EBX], EDX
	MOVB	DL, tdb_dsppage[ESI]
	IFFAULT	knlRtnAdrErLv##
	MOVB	dm_actpage[EBX], DL
	MOVL	EDX, tdb_pgsize[ESI]
	IFFAULT	knlRtnAdrErLv##
	MOVL	dm_pagesize[EBX], EDX
	MOVL	EDX, tdb_columns[ESI]
	IFFAULT	knlRtnAdrErLv##
	MOVL	dm_columns[EBX], EDX
	MOVZBL	EDX, tdb_rows[ESI]
	IFFAULT	knlRtnAdrErLv##
	MOVL	dm_rows[EBX], EDX
	MOVL	EDX, tdb_horiz[ESI]
	IFFAULT	knlRtnAdrErLv##
	MOVL	dm_horiz[EBX], EDX
;;;;;;	IFFAULT	knlRtnAdrErLv##
;;;;;;	MOVL	dm_span[EBX], EDX
	MOVL	EDX, tdb_vert[ESI]
	IFFAULT	knlRtnAdrErLv##
	MOVL	dm_vert[EBX], EDX
4$:	TESTB	adm_bits+1[EBP], #DM$BIOSMODE>8 ;Want BIOS value?
	JE	6$			;No
	CALL	xosconGetBiosMd##	;Yes - get equivalent BIOS mode
6$:	MOVL	EDX, tdb_dhdb[ESI]	;Yes - is this the current screen?
	CMPL	ESI, dhdb_curtdb[EDX]
	JNE	10$
	ORL	EAX, #DM$ACTIVE		;Yes - indicate that
10$:	ANDL	EAX, #~{DM$BIOSMODE|DM$RTNDATA}
modedn:	LEAVE
	RET

;Data array for vgaclrbfr

	DATA

clrbytes::.BLKL	65t

	CODE
.PAGE
	.SBTTL	ssdialog - Subroutine to do dialog with the screen symbiont

;Subroutine to do dialog with the screen symbiont
;	c{EAX} = Data for for the MT_SSSTATUS message
;	CALL	ssdialog

ssdialog:
	CALL	vgassstatus#		;Tell the screen symbiont
	TESTL	EAX, EAX
	JS	ret004			;Don't wait if error sending message
	TOFORK
	LEAL	EAX, 1[ESI]
	PUSHL	EAX
	PUSHL	#-1
	PUSHL	#-1
	PUSHL	#THDS_DW2		;Set up to wait using the TDB address
	CALL	sysSchSetUpToWait##	;  / 64 as the wait index
	SCHEDULE			;Call the scheduler (this is an
					;  uninterruptable wait!)
ret004:	RET				;Continue after the screen symbiont
					;  has updated the state of its virtual
					;  buffers
.PAGE
	.SBTTL	vgasetdhs - Subroutine to set up state of the display hardware

;Subroutine to set up state of the display hardware
;	c{ESI} = Offset of TDB
;	CALL	vgasetdhs
;  The display has been blanked upon return by clearing bit 5 in the attribute
;    controller register 0

vgasetdhs::
	MOVL	EBX, tdb_table[ESI]
vgasetdhs2:				;Enter here if EBX already set up
	CMPB	tdb_mode[ESI], #DM_TEXT	;Text mode?
	JNE	ret004			;No - finished now
	PUSHL	EDI
	PUSHF
	CLI
	MOVL	EDX, tdb_bioreg[ESI]	;Get port for the CRT controller index
	ADDL	EDX, #P_EGACCRTINDX-P_EGABASE
	MOVB	AL, #0x03		;Make sure can change vertical retrace
	OUTB	[DX]			;  registers
	IOPAUSE
	INCL	EDX
	INB	[DX]
	IOPAUSE
	ORB	AL, #0x80
	OUTB	[DX]
	IOPAUSE
	DECL	EDX
	MOVB	AL, #0x11		;Make sure CRT controller is unprotected
	OUTB	[DX]
	IOPAUSE
	INCL	EDX
	INB	[DX]
	IOPAUSE
	ANDB	AL, #0x7F
	OUTB	[DX]
	ADDL	EDX, #P_EGACSTS1-P_EGACCRTDATA
	INB	[DX]			;Read input status 1 register to reset
	IOPAUSE				;  attribute port to the index register
	MOVL	EDX, tdb_bioreg[ESI]	;Point to attribute controller port
	MOVB	AL, #0			;Disable video
	OUTB	[DX]

$$$$a::	NOP

;Now program the sequencer registers

	ADDL	EDX, #P_EGASEQINDX-P_EGAATTRIB
	MOVB	AH, #0			;Start with register 0
	LEAL	EDI, tbl_seqregs[EBX]	;Point to the sequencer data
	MOVB	AL, tdb_dfontsel[ESI]
	MOVB	3[EDI], AL
	MOVL	ECX, #5
6$:	MOVB	AL, AH			;Set sequencer index register
	OUTB	[DX]
	IOPAUSE
	INCL	EDX			;Point to data register
	MOVB	AL, [EDI]		;Get data byte
	OUTB	[DX]			;Output it
	DECL	EDX			;Point back to the index register
	INCB	AH			;Bump register number
	INCL	EDI			;Bump pointer
	LOOP	ECX, 6$
	MOVL	EDX, tdb_bioreg[ESI]	;Point to miscellaneous output register
	ADDL	EDX, #P_EGAMISC-P_EGABASE
	MOVB	AL, tbl_miscoutput[EBX] ;Get value
	OUTB	[DX]			;Set it
	IOPAUSE				     ;Point back to the sequencer
	ADDL	EDX, #P_EGASEQINDX-P_EGAMISC ;  registers
	MOVB	AL, #0			;Access register 0
	OUTB	[DX]
	IOPAUSE
	MOVB	AL, #3			;Enable the sequencer
	INCL	EDX
	OUTB	[DX]
	IOPAUSE

;Now program the graphics chips

	MOVL	EDX, tdb_bioreg[ESI]	    ;Point to graphics controller
	ADDL	EDX, #P_EGAGCINDX-P_EGABASE ;  index register
	MOVB	CL, #9
	MOVB	AH, #0
	LEAL	EDI, tbl_gcregs[EBX]
10$:	MOVB	AL, AH			;Set graphics controller index register
	OUTB	[DX]
	IOPAUSE
	INCL	EDX			;Point to data register
	MOVB	AL, [EDI]		;Get data byte
	OUTB	[DX]			;Output it
	DECL	EDX
	INCB	AH			;Bump register number
	INCL	EDI
	LOOP	ECX, 10$

;Now program the CRT controller registers

	MOVL	EDX, tdb_bioreg[ESI]	;Get port for the CRT controller index
	ADDL	EDX, #P_EGACCRTINDX-P_EGABASE
	LEAL	EDI, tbl_crtregs[EBX]
	MOVL	EAX, tdb_dstart[ESI]	;Use current value for the start
	XCHGB	AL, AH			;  display position
	MOVW	0x0C[EDI], AX
	CMPB	tdb_mode[ESI], #DM_TEXT ;Text mode?
	JNE	12$			;No
	MOVB	AL, tdb_charch[ESI]	;Yes
	DECB	AL
	ANDB	0x09[EDI], #0xE0
	ORB	0x09[EDI], AL
	MOVW	AX, tdb_dcurmode[ESI]	;Use current value for the cursor
	XCHGB	AL, AH			;  mode (start and end values)
	MOVW	0Ah[EDI], AX
	MOVW	AX, tdb_dcurpos[ESI]	;Use current value for the cursor
	XCHGB	AL, AH			;  position
	MOVW	0Eh[EDI], AX
	MOVW	AX, tdb_dvertend[ESI]
	MOVB	12h[EDI], AL
	ANDB	07h[EDI], #0xDD
	ADDB	AH, AH
	MOVB	AL, AH
	SHLB	AL, #3t
	ANDB	AH, #0x02
	ANDB	AL, #0x20
	ORB	AL, AH
	ORB	07h[EDI], AL
12$:	MOVB	AH, #0
	MOVB	CL, #25t
14$:	MOVB	AL, AH			;Set CRT controller index register
	OUTB	[DX]
	IOPAUSE
	INCL	EDX			;Point to data register
	MOVB	AL, [EDI]		;Get data byte
	OUTB	[DX]			;Output it
	DECL	EDX			;Point back to the index register
	INCB	AH			;Bump register number
	INCL	EDI
	LOOP	ECX, 14$

;Now program the attribute chip

	MOVL	EDX, tdb_bioreg[ESI]
	ADDL	EDX, #P_EGACSTS1-P_EGABASE
	INB	[DX]			;Read input status 1 register to reset
	IOPAUSE				;  attribute port to the index register
	MOVL	EDX, tdb_bioreg[ESI]	;Point to the attribute controller port
	MOVB	AH, #0
	LEAL	EDI, tbl_atrregs[EBX]
	MOVB	CL, #21t
16$:	MOVB	AL, AH			;Set attribute controller index register
	OUTB	[DX]
	IOPAUSE
	MOVB	AL, [EDI]		;Get data byte
	OUTB	[DX]			;Output it
	INCB	AH			;Bump register number
	INCL	EDI
	LOOP	ECX, 16$
	POPF

;Finally, set up the DAC registers

	ADDL	EDX, #P_EGADACPMASK-P_EGABASE ;Point to pixel mask register
	MOVB	AL, #0xFF		;Make sure it has the right value
	OUTB	[DX]
	IOPAUSE
	INCL	EDX
	INCL	EDX
	MOVB	AL, #0
	OUTB	[DX]
	INCL	EDX
	PUSHL	ESI
	ADDL	ESI, #tdb_dacdata
	MOVL	ECX, #256t*3
	CLD
18$:	LODSB	[ESI]			;Get DAC register value
	OUTB	[DX]
	LOOP	ECX, 18$
	POPL	ESI
	POPL	EDI

	CALL	vgaenable	;;;;; TEMP!!!!

	RET
.PAGE
	.SBTTL	vgaenable - Subroutine to enable the display

;Subroutine to enable the display after setting the mode or clearing the
;  display buffer
;	c{ESI} = Offset of TDB for terminal
;	CALL	vgaenable

vgaenable::
	CMPB	tdb_mode[ESI], #DM_TEXT ;Graphic mode?
	JNE	ret006			;Yes - nothing to do here
	MOVL	EDX, tdb_bioreg[ESI]	;No - point to status 1 register
	ADDL	EDX, #P_EGACSTS1-P_EGABASE
	INB	[DX]			;Read status 1 register to reset
	IOPAUSE				;  attribute port to the index register
	ADDL	EDX, #P_EGAATTRIB-P_EGACSTS1 ;Point to attribute controller
					     ;  port
	MOVB	AL, #0x20		;Set the index value to enable the
	OUTB	[DX]			;  display
	ADDL	EDX, #P_EGASEQINDX-P_EGAATTRIB
	MOVB	AL, #1
	OUTB	[DX]
	IOPAUSE
	INCL	EDX
	MOVL	EAX, tdb_table[ESI]
	MOVB	AL, tbl_seqregs+1[EAX]
	ANDB	AL, #0x01
	OUTB	[DX]
ret006:	RET
.PAGE

	.SBTTL	vgacurtype - Function to set cursor type (INT 10, function 1)

;Function to set cursor type (INT 10, function 1)
;	c(CL) = Ending scan line for cursor (0 - 1Fh)
;	c(CH) = Starting scan line for cursor (0 - 1Fh), bit 5 set to indicate
;		  invisible cursor, bit 7 set to indicate no cursor emulation
;		Both c(CL) and c(CH) = 0FFh means don't change type
;	CALL	vgacurtype
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c(AL) = Previous ending line for cursor
;	  c(AH) = Previous starting scan line for cursor
;	  High 16 bits of EAX are 0

vgacurtype::
	MOVZWL	EAX, tdb_curmode[ESI]	;Get previous value to return
	CMPW	CX, #0xFFFFFFFF		;Want to change value?
	JE	ret006			;No - just return value
	PUSHL	EAX			;Yes
	ANDL	ECX, #0xBF1F		;Remove possible junk
	MOVW	tdb_curmode[ESI], CX
	ANDB	CH, #~0x20
	BTZL	ECX, #15t		;Want to do cursor emulation?
	JC	8$			;No
	CMPB	CL, #7			;Yes - check ending value
	JBE	2$			;If OK
	MOVB	CL, #7			;Too big - use maximum
2$:	CMPB	CL, #4			;Need to adjust it?
	JBE	4$			;No
	SUBB	CL, #8			;Yes
	ADDB	CL, tdb_charch[ESI]
4$:	CMPB	CH, #7			;Check starting value
	JBE	6$			;If OK
	MOVB	CH, #7			;Too big - use maximum
6$:	CMPB	CH, #4			;Need to adjust it?
	JBE	8$			;No
	SUBB	CH, #8			;Yes
	ADDB	CH, tdb_charch[ESI]
8$:	MOVB	AL, tdb_curmode+1[ESI]
	ANDB	AL, #0x20
	ORB	CH, AL			;Put back the cursor off bit
	MOVW	tdb_dcurmode[ESI], CX
	POPL	EAX			;Restore previous value
	TESTB	tdb_stsdsp[ESI], #TSD$ACTDSP ;Is this the active display?
	JE	12$			;No - nothing more needed here
	PUSHL	EAX
	MOVL	EDX, tdb_bioreg[ESI]	;Get port for the CRT controller index
	ADDL	EDX, #P_EGACCRTINDX-P_EGABASE
	MOVB	AL, #10t		;Point to cursor position register
	OUTB	[DX]			;  in CRTC
	MOVB	AL, CH			;Get cursor start value
	INCL	EDX
	OUTB	[DX]
	MOVB	AL, #11t
	DECL	EDX
	OUTB	[DX]
	MOVB	AL, CL
	INCL	EDX
	OUTB	[DX]
	POPL	EAX			;Restore previous value
12$:	CLC
	RET
.PAGE
	.SBTTL	vgacurpos - Func. to set and get cursor pos. (INT 10, func. 02)

;Function to set and get cursor position (INT 10, function 02)
;	c{EBX} = Page number
;	c{ECX} = Character number
;	c{EDX} = Line number
;	CALL	vgacurpos
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c(AL) = Previous character number
;	  c(AH) = Previous line number

vgacurpos::

;;;;	PUSHL	EDX
;;;;	CALL	xosconDosUpdCP##	;Update cursor position for DOS
;;;;	POPL	EDX
;;;;	JC	20$			;If error
;;;;	SETNE	AL
;;;;	PUSHL	EAX			;Remember if DOS process

	MOVZWL	EAX, tdb_curpos[ESI+EBX*2]
	CMPL	ECX, #-1		;Want to change character position
	JE	8$			;No
	TESTL	ECX, ECX		;Yes - relative?
	JNS	6$			;No
	ADDB	tdb_curpos+0[ESI+EBX*2], CL ;Yes
	JMP	8$

6$:	MOVB	tdb_curpos+0[ESI+EBX*2], CL
8$:	CMPL	EDX, #-1		;Want to change line position?
	JE	12$			;No
	TESTL	EDX, EDX		;Yes - relative?
	JNS	10$			;No
	ADDB	tdb_curpos+1[ESI+EBX*2], DL ;Yes
	JMP	12$

10$:	MOVB	tdb_curpos+1[ESI+EBX*2], DL
12$:
;;;;	POPL	EDX
;;;;	CMPB	DL, #0			;Is this terminal for a DOS process?
;;;;	JE	14$			;No
;;;;	MOVW	CX, tdb_curpos[ESI+EBX*2] ;Yes - update cursor position on
;;;;	MOVW	+bda_vcurpos[EBX*2], CX	  ;  real mode page 0

14$:	CMPB	tdb_dsppage[ESI], BL	;Is this for the current page?
	JNE	16$			;No
	PUSHL	EAX			;Yes
	CALL	vgadspcur#		;Set value into the hardware
	POPL	EAX
16$:	CLC
20$:	RET
.PAGE
;Function to select display position
;	c{EBX} = Display start value
;	CALL	vgadsppage
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EAX} = Previous display page number
;	  High 16 bits of EAX are 0

vgadsppos::
	MOVL	EAX, tdb_dstart[ESI]	;Get current start offset
	CMPL	EBX, #-1		;Want to change it?
	JE	6$			;No
	PUSHL	EAX			;Yes - save current value
	CMPB	tdb_mode[ESI], #DM_TEXT ;Graphic mode?
	JBE	10$			;No

;;; NEED LIMIT CHECK HERE!!!

	TESTB	tdb_stsdsp[ESI], #TSD$ACTDSP ;Is this the active display?
	JE	4$			;No - nothing needed here
	PUSHL	EBX
	PUSHL	ESI
	PUSHL	EBX
	CALL	vgasenddsppos#		;Yes - send the request to the graphic
	POPL	EBX			;  support symbiont
	TESTL	EAX, EAX
	JS	8$
	MOVL	tdb_dstart[ESI], EBX	;Store new value
4$:	POPL	EAX
6$:	RET

;Here if error

8$:	POPL	EDX
	RET

;Here for text mode

10$:	SHRL	EBX, #1
	MOVL	tdb_dstart[ESI], EBX
	TESTB	tdb_stsdsp[ESI], #TSD$ACTDSP ;Is this the active display?
	JE	12$			;No
	MOVL	EDX, tdb_bioreg[ESI]	;Yes - point to register
	ADDL	EDX, #P_EGACCRTINDX-P_EGABASE
	MOVB	AL, #0Ch		;Update the display start registers
	OUTB	[DX]
	IOPAUSE
	INCL	EDX
	MOVB	AL, BH
	OUTB	[DX]
	IOPAUSE
	DECL	EDX
	MOVB	AL, #0x0D
	OUTB	[DX]
	IOPAUSE
	INCL	EDX
	MOVB	AL, BL
	OUTB	[DX]
	CALL	vgadspcur#		;Display cursor at correct position
12$:	CALL	xosconDosChk##		;Is this terminal for a DOS process?
	JE	14$			;No
	MOVZBL	EDX, tdb_dsppage[ESI]	  ;Yes - update cursor position in
	MOVW	AX, tdb_curpos[ESI+EDX*2] ;  real mode page 0
	MOVW	bda_vcurpos[EDX*2], AX
	MOVB	bda_vdsppage, DL
14$:	POPL	EAX			;Restore previous page number
	CLC
	RET



.PAGE
	.SBTTL	vgagetatrchr - Func. to get attrib. and char. (INT 10, func. 08)

;Function to get attributes and character at cursor (INT 10, function 08)
;	c{EBX} = Display page
;	CALL	vgagetatrchr
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c(AL) = Character
;	  c(AH) = Attribute byte
;	  High 16 bits of EAX are 0

vgagetatrchr::
	CMPB	tdb_mode[ESI], #DM_TEXT ;Text mode?
	JNE	gferr
	CALL	xosconDosUpdCP##	;Yes - update cursor for DOS
	JC	2$			;If error
	MOVL	ECX, EBX		;Calculate offset of start of page
	IMULL	ECX, tdb_pgsize[ESI]
	MOVZBL	EAX, tdb_curpos+1[ESI+EBX*2] ;Calculate offset on screen for
	IMULL	EAX, tdb_columns[ESI]
	ADDL	EAX, EAX
	ADDL	ECX, EAX
	MOVZBL	EAX, tdb_curpos+0[ESI+EBX*2] ;Add in position in line
	ADDL	EAX, EAX
	ADDL	ECX, EAX
	ADDL	ECX, tdb_vtaddr[ESI]	;Add in offset of screen
	MOVZWL	EAX, [ECX]		;Get character and attribute from screen
2$:	RET				;Finished

4$:	MOVL	EAX, #ER_VALUE
	STC
	RET

gferr:	MOVL	EAX, #ER_FUNCM
	STC
	RET
.PAGE
	.SBTTL	vgasetchr - Function to set character (INT 10, function 0A)

;Function to set character only at cursor (INT 10, function 0A)
;	c(AL)  = ASCII character
;	c(AH)  = Color (graphics modes only)
;	c{EBX} = Display page
;	c{ECX} = Count
;	CALL	vgasetchr
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EAX} = 0

vgasetchr::
	CMPB	tdb_mode[ESI], #DM_TEXT ;Text mode?
	JNE	gferr
	PUSHL	#vgachr#		;Save offset of routine to call
	JMP	2$

	.SBTTL	vgasetatrchr - Function to set attr. & char. (INT 10, func. 09)

;Function to set attributes and character at cursor (INT 10, function 09)
;	c(AL)  = ASCII character
;	c(AH)  = Attribute byte
;	c{EBX} = Display page
;	c{ECX} = Repeat count
;	CALL	vgasetatrchr
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EAX} = 0

vgasetatrchr::
	CMPB	tdb_mode[ESI], #DM_TEXT ;Text mode?
	JNE	gferr
	PUSHL	#vgachratr#		;Save offset of routine to call
2$:	PUSHL	EAX			;Save character and attributes
	CALL	xosconDosUpdCP##	;Update cursor position for DOS
	JC	10$
	MOVW	DX, tdb_curpos[ESI+EBX*2] ;Get current position
6$:	MOVL	EAX, [ESP]		;Get character and attributes
	PUSHL	ECX
	PUSHL	EDX
	CALLI	12t[ESP]		;Put character and attribute on screen
	POPL	EDX
	POPL	ECX
	INCL	EDX			;Advance position
	LOOP	ECX, 6$			;Continue if more to do
	POPL	EAX			;Discard stack data
	POPL	EAX
	CLRL	EAX
	RET

;Here if have illegal display page number

10$:	POPL	EDX
	POPL	EDX
	RET

	DATA

yyyyy::	.BLKB	0x30
	.LONG	0

	END
