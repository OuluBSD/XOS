	.TITLE	scsisup - SCSI device support functions

;*--------------------------------------------------------------------------*
;* scsisup.m86
;*
;* Written by: John Goltz
;*
;* Edit History:
;* 10/9/06(jrg) - First version
;*
;*--------------------------------------------------------------------------*

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

; This LKE provides some support functions for devices that use the SCSI
;   command set. This includes RBC devices.

	.PROC	80486
	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\XOSX.PAR
	.INCLUD	XOSINC:\XMAC\XOSTIME.PAR
	.INCLUD	XOSINC:\XMAC\XOSERR.PAR
	.INCLUD	XOSINC:\XMAC\XOSLKE.PAR
	.INCLUD	XOSINC:\XMAC\XOSXLKE.PAR

MAJV   =!1t
MINV   =!0t
EDITNUM=!0t

	LKEHEAD	SCSISUP, MAJV, MINV, EDITNUM, LKETYPE_SUPPORT

	.EXPORT	xosscsMapError

	ONCE

	MOVL	EBX, lkei_pctop-4[ESP]
	MOVL	[EBX], #codetop
	CLRL	EAX
	RET	lkei_ADJ

	CODE

	.SBTTL	xosscsMapError - Subroutine to map SCSI error to knl error code

;Subroutine to map SCSI error information to an knl error code
;	long xosscsMapError(
;	    uchar *sdata,	// Address of sense data response (must contain
;				//   at least 8 bytes)
;	    long  *pinfo);	// Address to receive information value (may
;				//   be null)
;  Value returned is the extended XOS error code

mperr_sdata=!8t
mperr_pinfo=!4t

xosscsMapError:
	MOVL	EBX, mperr_sdata[ESP]
	CMPB	[EBX], #70h		;Really sense data?
	JNE	6$			;No
	MOVB	AL, ES:2.B[EBX]		;Yes - get the sense key value
	TESTB	AL, #0xE0
	JE	8$
	TESTB	AL, #0x80		;Filemark reported?
	JNE	filemrk			;Yes
	TESTB	AL, #0x40		;End of media reported?
	JNE	eom			;Yes
	MOVL	EAX, #ER_INCBL		;No - must be incorrect length
	JMP	10$

;Here if have filemark or setmark indication

filemrk:CMPB	7[EBX], #6		;Have an additional sense code?
	JB	2$			;No
	CMPB	12t[EBX], #0		;Yes - additional sense code should be 0
	JNE	2$
	CMPB	13t[EBX], #3		;Does the qualifier indicate setmark?
	JE	4$			;Yes
2$:	MOVL	EAX, #ER_EOF		;No - report file mark (EOF)
	JMP	10$

4$:	MOVL	EAX, #ER_EOS
	JMP	10$

;Here if have end of media indication

eom:	MOVL	EAX, #ER_EOM
	JMP	10$

6$:	MOVL	EAX, #ER_ERROR
	RET

;Here if don't have one of the special bits set

8$:	ANDL	EAX, #0Fh.B
	MOVL	EAX, errcode[EAX*4]	;Get the XOS error code
10$:	MOVL	ECX, mperr_pinfo[ESP]
	TESTL	ECX, ECX
	JE	12$
	MOVL	EDX, 3.B[EBX]		;Get the information value
	XCHGB	DL, DH			;Fix up the byte order
	RORL	EDX, #16t
	XCHGB	DL, DH
	MOVL	[ECX], EDX
12$:	ANDL	EAX, #0xF0000FFF	;Clear the extended error code fields
	CMPB	7[EBX], #6
	JB	14$
	MOVZBL	EDX, 12t[EBX]		;Get the additional sense code
	SHLL	EDX, #12t
	ORL	EAX, EDX
	CMPB	8[EBX], #7
	JB	14$
	MOVZBL	EDX, 13t[EBX]		;Get the additional sense code
	SHLL	EDX, #20		;  qualifier
	ORL	EAX, EDX
14$:	RET	8t

	DATA

errcode:.LONG	ER_NOSEN	;0 - NO SENSE
	.LONG	0		;1 - RECOVERED ERROR
	.LONG	ER_NTRDY	;2 - NOT READY
	.LONG	ER_DATER	;3 - MEDIUM ERROR
	.LONG	ER_DEVER	;4 - HARDWARE ERROR
	.LONG	ER_ICMDV	;5 - ILLEGAL REQUEST
	.LONG	ER_MDCHG	;6 - UNIT ATTENTION
	.LONG	ER_WPRER	;7 - DATA PROTECT
	.LONG	ER_BLANK	;8 - BLANK CHECK
	.LONG	ER_ERROR	;9 - VENDOR-SPECIFIC
	.LONG	ER_ERROR	;A - COPY ABORTED
	.LONG	ER_DVDER	;B - ABORTED COMMAND
	.LONG	ER_ERROR	;C - EQUAL
	.LONG	ER_DEVFL	;D - VOLUME OVERFLOW
	.LONG	ER_ERROR	;E - MISCOMPARE
	.LONG	ER_ERROR	;F - RESERVED

	CODE

	LKEEND
