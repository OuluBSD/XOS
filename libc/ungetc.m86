	.TITLE	ungetc - Function to put back input character
	.SBTTL	STDC run-time library for XOS

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\XOSERR.PAR
	.INCLUD	XOSINC:\XMAC\STDIO.PAR

	.STDSEGS

	.PSECT	_TEXT_p

	.SBTTL	ungetc - Function to put back input character

;Function to put back input character
;	int   chr;		/* Character to put back */
;	FILE *stream;		/* Stream pointer */
;	(int)val = ungetc(chr, stream);
;  Value returned is character put back (0 filled to 32 bits) if OK or -1 if
;  error (errno is set)

	.ENTRY	ungetc
ungetc:	MOVL	EDX, 8t[ESP]		;Get offset of the IOB
	BTL	iob_flag[EDX], #IOB%READ ;Open for read?
	JNC	2$			;No - fail
	BTL	iob_flag[EDX], #IOB%IONBF ;Yes - unbuffered device?
	JNC	8$			;No
	CMPW	iob_ungetc[EDX], #0xFFFF ;Yes - has something already been put
					 ;  back?
	JNE	4$			;Yes - fail
	MOVZBL	EAX, 4t[ESP]		;No - put this character back
	MOVW	iob_ungetc[EDX], AX
	RET				;Finished

;Here if not open for input

2$:	MOVL	EAX, #ER_NOIN
	JMP	6$

;Here if can't put the character back

4$:	MOVL	EAX, #ER_FUNCM
6$:	JMP	$errorrtn#

;Here for buffered device

8$:	MOVL	EAX, iob_count[EDX]
	TESTL	EAX, EAX
	JS	4$
	CMPL	EAX, iob_bsize[EDX]
	JAE	4$
	INCL	iob_count[EDX]
	DECL	iob_pnt[EDX]
	MOVL	EDX, iob_pnt[EDX]
	MOVZBL	EAX, 4t[ESP]
	MOVB	[EDX], AL
	RET

	.END
