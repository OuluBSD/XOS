	.TITLE	getkey

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.PROC	80386
	.INCLUD xosinc:\xmac\stdio.par
	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD XOSINC:\XMAC\XOSERR.PAR
	.INCLUD	XOSINC:\XMAC\XOSTRM.PAR
	.INCLUD XOSINC:\XMAC\CLIBX.PAR
	.INCLUD XOSINC:\XMAC\PROCARG.PAR
	.INCLUD	VID.PAR

	.STDSEGS

	.PSECT	_TEXT_p

	.SBTTL	getkey - Subroutine to get character from the keyboard

;Subroutine to get character from the keyboard
;	CALL	getkey
;	c(AL)  = Character typed (ASCII) or 0 if special function
;	c{EBX} = Special function dispatch index (only if c(A) = 0)
;	c{ECX} = Data for function

getkey::CALL	getkb			;Get charcter
	CMPB	AL, #0			;Non-ASCII key?
	JE	12$			;Yes
	MOVZBL	EAX, AL
	CMPB	AL, #0x80		;Maybe
	JE	12$			;Yes
	CMPB	litflg#, #0		;No - should it be literal?
	JNE	rmvlit#			;Yes
	ORB	AL, AL			;ALT key pressed?
	JNS	6$			;No - go on
	CMPB	AL, #'1'+0x80		;Yes - ALT-digit?
	JB	2$			;No
	CMPB	AL, #'9'+0x80		;Maybe
	JA	2$			;No
	MOVL	ECX, EAX		;Yes - this is a change window function
	ANDL	ECX, #7
	MOVB	AL, #0
	MOVL	EBX, #VS_CWN
2$:	RET

;Here if alt key not pressed

6$:	CMPB	AL, #' '		;Is character a control character?
	JB	8$			;Yes - map the character
	CMPB	AL, #DEL		;No - is character rubout?
	JNE	ret001			;No - return the character
	MOVB	AL, #0x20		;Yes - get index for this
8$:	MOVZBL	EBX, AL			;Get the function for the character
	MOVB	BL, cintrl#[EBX]
	ORB	BL, BL			;Really a function?
	JNS	10$			;No - just normal character
	MOVB	AL, #0
10$:	RET

;Here if have a non-ASCII key

12$:	PUSHL	EAX
	CALL	rmvlit#			;Clear literal character state
	POPL	EAX
	MOVL	ECX, EAX		;Get the shift bits
	SHRL	ECX, #16t
	MOVB	AL, AH			;Get the scan code
	MOVB	AH, CL			;Merge left and right bits
	SHRB	CL, #4
	ORB	AH, CL
	ANDB	AH, #0x07
	MOVL	ECX, #ESCLEN#		;Get the length of the table
	PUSHL	CS
	POPL	ES
	PUSHL	EDI
	MOVL	EDI, #esctbl#		;Get offset of the escape table
	CLD
	RNSCASW	[EDI]			;Search the table
	JNE	16$			;If not found
	SUBL	EDI, #esctbl#+2
	MOVZBL	EBX, escdat#[EDI]	;Get the function
14$:	POPL	EDI
	MOVB	AL, #0			;Indicate have function index
ret001:	RET

;Here if not found

16$:	MOVL	EBX, #VS_NOP		;No operation
	JMP	14$
.PAGE
	.SBTTL	getkb = Subroutine to get a character from keybord

;Subroutine to get a character from the keybord
;	CALL	getkb
;	c(AL)	= Character

getkb::	PUSHL	#5			;Get character
	PUSHL	#kbbufr#
	PUSHL	#4
	CALL	svcIoInBlock##
	MOVL	EAX, kbbufr#
	RET

fmt001:	.ASCIZ	"### char: %08.8X"{LF}
xxxx:	.ASCIZ	"X"
