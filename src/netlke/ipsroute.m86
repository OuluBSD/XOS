	.TITLE	ipsroute - Internet routing routines for XOS

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.PROC	80486
	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\XOSX.PAR
	.INCLUD	XOSINC:\XMAC\XOSTIME.PAR
	.INCLUD	XOSINC:\XMAC\XOSERR.PAR
	.INCLUD	XOSINC:\XMAC\XOSNET.PAR
	.INCLUD	XOSINC:\XMAC\XOSXNET.PAR
	.INCLUD	XOSINC:\XMAC\XOSXSNAP.PAR
	.INCLUD	XOSINC:\XMAC\XOSXARP.PAR
	.INCLUD	XOSINC:\XMAC\XOSXIPS.PAR
	.INCLUD	XOSINC:\XMAC\XOSLKE.PAR
	.INCLUD	XOSINC:\XMAC\XOSXLKE.PAR

;This module contains routines which implement Internet routing for hosts

	LKECONT

	.EXPORT	xosipsRtChkAddr
	.EXPORT	xosipsRtRmvOne

	.SBTTL	xosipsRtChkAddr - Subroutine to check if IP address is in the cache

;Subroutine to check if an IP address is in the routing cache.
;	c{EAX} = IP address
;	c{EDI} = Offset of PDB
;	c{ESI} = Offset of SDB
;	CALL	xosipsRtChkAddr
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal

xosipsRtChkAddr:
	MOVB	CL, #1
	JMP	geta2

	.SBTTL	rtgetaddr - Subroutine to get hardware address given IP address


;Subroutine to get hardware address given IP address.  This subroutine is
;  called via the npb_getaddr dispatch by snapsendpkt.  If it can find the
;  address immediately, it fills it in and returns with EBX unchanged.  If
;  it must use ARP to get the address, it links the packet to be sent to the
;  send list for the ARP request and returns EBX pointing to the initial ARP
;  packet to send.  Note that the sequence needed to find an address (especially
;  a remote address) is quite complex, but it must all be implemented as a
;  state machine at device fork level.  This is necessary because we must be
;  able to resolve addresses of packets (such as ICMP echo requests) which are
;  processed completely at device fork level.
;	c{EBX} = Offset of packet buffer
;	c{EDI} = Offset of PDB
;	c{ESI} = Offset of SDB
;	CALL	rtgetaddr
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EDX,EAX} = Hardware address
;	  c{ECX}     = Offset of routing table entry

$$$=!0
FRM rtg_ipa    , 4t		;IP address
FRM rtg_pkt    , 4t		;Offset of packet
FRM rtg_foreign, 4t
FRM rtg_test   , 1t
FRM            , 3t
rtg_SIZE=!$$$

rtgetaddr::
	MOVB	CL, #0
	MOVZBL	EAX, npb_npofs[EBX]	;Get offset of the IP header
	MOVL	EAX, ip_dstipaddr[EBX+EAX] ;Get the destination IP address
geta2:	TESTL	EAX, #0FF000000h	;Invalid IP address
	JE	6$			;Yes - fail!
	CMPL	EAX, #0FFFFFFFFh	;No - limited broadcase address?
	JE	8$			;Yes
	ENTER	rtg_SIZE, 0		;No - set up our stack frame
	MOVL	rtg_ipa[EBP], EAX	;Save IP address
	MOVL	rtg_pkt[EBP], EBX	;Also save offset of the packet buffer
	MOVB	rtg_test[EBP], CL
	CMPB	AL, #IPA_LOCAL		;Is this the local echo address?
	JE	10$			;Yes
	CMPL	pdb_ipaddr[EDI], EAX	;No - is it our own address?
	JE	10$			;Yes
	MOVL	EAX, rtg_ipa[EBP]
	CALL	rtsearch
	JNE	getanirt		;If not found
geta7:	CMPB	rt_type[ECX], #RTT_LPENDING ;Is this a pending local entry?
	JE	getapend		;Yes - go handle that
	CMPB	rt_type[ECX], #RTT_FPENDING ;Is this a pending foreign entry?
	JE	getapend		;Yes - go handle that
	CMPB	rt_type[ECX], #RTT_DPENDING ;Is this a pending default entry?
	JE	getapend		;Yes - almost like a pending entry!
	MOVL	EAX, knlSchTtl##	;No - update time-stamp value
	MOVL	rt_timestamp[ECX], EAX
	CMPB	rt_type[ECX], #RTT_FOREIGN ;Is this a foreign entry?
	JNE	4$			;No - go on
	MOVL	ECX, rt_router[ECX]	;Yes - point to associated router entry
4$:	MOVL	EAX, rt_hwaddr+0[ECX]	;Get hardware address
	MOVL	EDX, rt_hwaddr+4[ECX]
	MOVL	EBX, rtg_pkt[EBP]	;Restore offset of packet
	LEAVE				;Thats all - return with normal packet
	CLC				;  to send (this is the normal path
	RET				;  for a routing cache hit)

;Here if have illegal IP address

6$:	MOVL	EAX, #ER_NILAD
	STC
	RET

;Here if sending to the IP limited broadcast address

8$:	MOVL	EDX, EAX
	JMP	14$

;Here if sending to own IP address

10$:	CMPB	rtg_test[EBP], #0
	JNE	12$
	MOVL	EDX, sdb_idb[ESI]
	MOVL	EAX, idb_hwaddr+0[EDX]
	MOVL	EDX, idb_hwaddr+4[EDX]
	MOVL	EBX, rtg_pkt[EBP]	;Restore offset of packet
	ORB	npb_sts[EBX], #NPS$SELF ;Indicate sending packet to self
12$:	LEAVE
14$:	CLRL	ECX
	RET
.PAGE
;Here if found a pending ARP routing table entry of some kind - we get here
;  for RTT_LPENDING, RTT_FPENDING, and RTT_DPENDING entries.  In all cases we
;  link the packet to send to the hold list for the routing table entry and
;  return 0 in EBX to indicate that there is nothing to send now.

getapend:
	CMPB	rtg_test[EBP], #0	;Just testing?
	JNE	getarerr		;Yes - fail
	MOVL	EBX, rtg_pkt[EBP]	;No - get offset of the packet
	TESTL	EBX, EBX
	JE	6$
	MOVL	EAX, rt_holdtail[ECX]	;Link this packet to the tail of the
	TESTL	EAX, EAX		;  packet hold list
	JE	2$
	MOVL	npb_nextsnd[EAX], EBX
	CLRL	EAX
	JMP	4$

2$:	MOVL	rt_holdhead[ECX], EBX
4$:	MOVL	rt_holdtail[ECX], EBX
	MOVL	npb_nextsnd[EBX], EAX
	MOVL	npb_holdrt[EBX], ECX
	CLRL	EBX			;Return 0 to indicate no output needed
	LEAVE				;  now
6$:	RET

;Here if have internal error with the routing table - This should not happen
;  but may be possible with certain illegal responses from the network. Also
;  get here if rtchkaddr is called and we do not have the address in the cache.

getarerr:
	LEAVE
	MOVL	EAX, #ER_NRTER		;Report this as a routing error!
	STC
	RET

;Here if entry not found in the routing table.  In this case we have to create
;  a new entry.  If we are looking for a local address, we create an
;  RTT_LPENDING entry, place the packet on its hold list, and set up an ARP
;  request packet to send in place of the current packet.  If it is a foreign
;  address, we create an RTT_DPENDING entry, place the packet on its hold list,
;  create or find an existing RTT_LPENDING entry which we link to the
;  RTT_FPENDING entry, and set up an ARP request packet to send in place of
;  the current packet.  In both cases, ARP response handling and retries are
;  handled at device fork level.

getanirt:
	CMPB	rtg_test[EBP], #0	;Just testing?
	JNE	getarerr		;Yes - fail
	XORL	EAX, pdb_ipaddr[EDI]	;No - is this a local address?
	TESTL	EAX, pdb_ipsubnetmask[EDI]
	JNE	getafor			;No
	MOVL	EAX, rtg_ipa[EBP]	;Yes - get IP address
	CALL	doarp			;Insert PENDING routing table entry
	JC	retlv2			;  and make an ARP packet
	MOVL	EDX, rtg_pkt[EBP]	;OK - get offset of original packet
	MOVL	rt_holdhead[ECX], EDX	;Initialize the packet hold list
	MOVL	rt_holdtail[ECX], EDX
	CLRL	EAX
	TESTL	EDX, EDX		;Do we have a packet?
	JE	10$			;No
	ORB	npb_sts[EDX], #NPS$USEDARP ;Yes - indicate used ARP for packet
	MOVL	npb_nextsnd[EDX], EAX
	MOVL	npb_holdrt[EDX], ECX
10$:	DECL	EAX			;This sets EAX to -1
	MOVL	EDX, EAX
retlv2:	LEAVE
ret002:
	JNC	13$
	TESTL	EAX, EAX
	JS	11$
	INT3

11$:	STC
13$:

	RET
.PAGE
;Here if have an address for a node on a different sub-net - first we create
;  an RTT_FOREIGN routing table entry

getafor:CMPL	pdb_ipdrthead[EDI], #0
	JE	17$
	MOVB	BL, #RTT_FOREIGN	;Get routing table entry type
	MOVB	BH, #1			;Request forced update
	MOVL	EAX, rtg_ipa[EBP]	;Get IP address
	CLRL	EDX			;No hardware address yet
	CLRL	ECX
	CALL	rtupdate		;Put entry in the routing table
	JC	retlv2			;If error
	MOVL	rtg_foreign[EBP], ECX	;OK - remember its offset
	MOVL	rt_t2lcnt[ECX], #RTT2L_FOREIGN ;Store time-out value
	MOVL	rt_timeout[ECX], #rttimeout ;Store offset of time-out routine

;Next we first try to find a routing table entry for any one of the default
;  routers - if we can't find any, we create a pending entry for each one in
;  turn

	MOVL	EDX, pdb_ipdrthead[EDI]
2$:	MOVL	EAX, dct_data[EDX]	;Get IP address for router
	PUSHL	EDX
	CALL	rtsearch		;Look for routing table entry
	POPL	EDX
	JE	18$			;If we found one
	MOVL	EDX, dct_lnknext[EDX]	;Not there (or not valid type for this)
	TESTL	EDX, EDX		;  - advance to next block
	JNE	2$

;Here if could not find a routing table entry for a default router - now we
;  must try to create one

6$:	MOVL	EDX, pdb_ipdrthead[EDI] ;Start with the first default router
	ORL	EDX, EDX		;Have one?
	JE	16$			;No
	MOVL	EAX, dct_data[EDX]	;Yes - get IP address
	PUSHL	EDX
	CALL	doarp
	POPL	EDX
	JC	14$			;If error
	MOVL	EAX, dct_ipnum[EDX]
	MOVL	EDX, rtg_foreign[EBP]	;Get offset of the new entry
	MOVB	rt_retry[EDX], AL	;Store state
	MOVB	rt_type[EDX], #RTT_DPENDING ;Change routing table entry type
	MOVL	rt_t2lcnt[EDX], #0	    ;  to RTT_DPENDING
	MOVL	rt_router[EDX], ECX	;Link the RTT_DPENDING entry to the
	MOVL	EAX, rt_rtntail[ECX]	;  RTT_LPENDING entry we just made
	TESTL	EAX, EAX
	JNE	8$
	MOVL	rt_rtnhead[ECX], EDX
	JMP	10$

8$:	MOVL	rt_rtnnext[EAX], EDX
10$:	MOVL	rt_rtntail[ECX], EDX
	MOVL	EAX, rtg_pkt[EBP]	;Get offset of original packet
	MOVL	rt_holdhead[EDX], EAX	;Initialize the packet hold list for
	MOVL	rt_holdtail[EDX], EAX	;  the RTT_DPENDING routing table entry
	TESTL	EAX, EAX		;Do we have a packet?
	JE	12$			;No
	MOVL	npb_nextsnd[EAX], #0
	MOVL	npb_holdrt[EAX], EDX
	ORB	npb_sts[EAX], #NPS$USEDARP ;Indicate used ARP for packet
12$:	MOVL	EAX, #-1
	MOVL	EDX, EAX
	LEAVE				;Return from rtgetaddr to send the first

	JNC	135$
	TESTL	EAX, EAX
	JS	13$
	INT3

13$:	STC
135$:

	RET				;  ARP packet

;Here if don't have an IP address for a default router

14$:	MOVL	EDX, dct_lnknext[EDX]	;Advance to next default
	JMP	6$			;Continue

;Here if can't find a router

16$:	MOVL	ECX, rtg_foreign[EBP]	;No
	MOVB	rt_type[ECX], #RTT_LOCAL
	MOVL	EAX, rtg_ipa[EBP]	;Give up the new entry
	MOVB	DL, #0
	CALL	xosipsRtRmvOne
17$:	MOVL	EBX, rtg_pkt[EBP]	;Restore offset of the packet
	MOVL	EAX, #ER_NRTNA		;Return the error
	LEAVE
	STC
	RET

;Here with routing table entry for a default router

18$:	CMPB	rt_type[ECX], #RTT_ROUTER ;Is this a router entry?
	JE	20$			;Yes - go on
	CMPB	rt_type[ECX], #RTT_LOCAL ;No - is this a local entry now?
	JNE	14$			;No - ignore it!
	MOVB	rt_type[ECX], #RTT_ROUTER ;Yes - change it to a router entry
20$:	MOVL	EAX, dct_ipnum[EDX]
	MOVL	EDX, rtg_foreign[EBP]	;Get offset of the new entry we made
	MOVB	rt_retry[EDX], AL	;Save state in case have RTT_LPENDING
					;  entry
	MOVL	rt_router[EDX], ECX	;Link it to the router entry
	MOVL	EAX, rt_rtntail[ECX]
	TESTL	EAX, EAX
	JNE	22$
	MOVL	rt_rtnhead[ECX], EDX
	JMP	24$

22$:	MOVL	rt_rtnnext[EAX], EDX
24$:	MOVL	rt_rtntail[ECX], EDX
	MOVL	rt_rtnprev[EDX], EAX
	CMPB	rt_type[ECX], #RTT_LPENDING ;Is this a pending entry?
	JNE	geta7			;No - return with original packet
	MOVL	ECX, EDX		;Yes
	MOVB	rt_type[EDX], #RTT_DPENDING
	MOVL	rt_t2lcnt[EDX], #0
	JMP	getapend
.PAGE
;Subroutine to setup ARP packet and pending routing table entry
;	c{EAX} = IP address
;	c{EDI} = Offset of PDB
;	CALL	doarp
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{ECX} = Offset of pending routing table entry
;	  c{EBX} = Offset of ARP packet to send

doarp:	CMPL	pdb_iparppdb[EDI], #0	;Do we have an ARP protocol?
	JE	4$			;No - fail
	PUSHL	EAX			;Yes - save IP address
	MOVB	BL, #RTT_LPENDING	;Routing table entry type
	MOVB	BH, #1			;Forced update
	CLRL	ECX
	CLRL	EDX
	CALL	rtupdate		;Put entry in the routing table
	JC	10$			;If can't put entry in routing table
	MOVL	rt_t2lcnt[ECX], #RTT2L_LPENDING ;Time-to-live value
	MOVB	rt_retry[ECX], #RTRETRY_PENDING ;Retry count value
	MOVL	rt_timeout[ECX], #pendtimeout ;Offset of timeout routine
	PUSHL	ECX			;Save offset of routing table entry
	PUSHL	EDI			;Yes
	MOVL	EDX, EDI
	MOVL	EAX, pdb_ipaddr[EDI]	;Get our IP address
	MOVL	ECX, 8[ESP]		;Get destination IP address
	MOVL	EDI, pdb_iparppdb[EDI]	;Point to the ARP PDB
	CALLI	pdb_arpgenreq[EDI]	;Yes - geneate the ARP request packet
	POPL	EDI
	JC	6$			;If error
	POPL	ECX			;Restore offset of routing table entry
	MOVL	rt_pkt[ECX], EBX	;Store offset of ARP packet buffer
	POPL	EAX			;Restore IP address
	RET				;Return to send the ARP packet

;Here if no ARP protocol is specified

4$:	MOVL	EAX, #ER_NNOPC
	STC
	RET

;Here if can't get a packet buffer or can't set up the packet after we have
;  created the pending routing table entry (if we had a packet, its already
;  been given up when we get here)

6$:	POPL	ECX			;Restore offset of routing table entry
	XCHGL	[ESP], EAX		;Save error code, get IP address
	MOVB	rt_type[ECX], #RTT_LOCAL ;Make routing table entry removeable
	MOVB	DL, #1
	CALL	xosipsRtRmvOne		;Remove the entry
	JC	12$			;This must work!
	POPL	EAX			;Restore error code

	TESTL	EAX, EAX
	JS	7$
	INT3
7$:

	STC				;Indicate error
8$:	RET

;Here if can't create a pending routing table entry

10$:	POPL	ECX			;Fix up the stack

	TESTL	EAX, EAX
	JS	11$
	INT3
11$:

	STC
	RET

12$:	CRASH	CFRT			;[Can't Find Routing Table entry]
.PAGE
;Subroutine called when pending local routing table entry times out
;	c{ECX} = Offset of routing table entry
;	c{ESI} = Offset of SDB
;	CALL	pendtimeout
;	c{ECX} = Offset of next routing table entry
;  ESI may be changed

pendtimeout:
	MOVL	EBX, rt_pkt[ECX]	;Get offset of packet buffer
	CMPL	npb_nextsnd[EBX], #-1	;Is the packet still being output?
	JNE	2$			;Yes - just time it out again!
	DECB	rt_retry[ECX]		;No - have we done this enough?
	JS	pendfail		;Yes
	MOVL	npb_outdone[EBX], #4$	;No - reset the output done dispatch
	MOVL	ESI, sdb_idb[ESI]
	PUSHL	ECX
	CALL	xosnetSendPkt##		;Output the packet again
	POPL	ECX
2$:	MOVL	rt_t2lcnt[ECX], #RTT2L_LPENDING ;Reset the time-out count
	MOVL	ECX, rt_next[ECX]	;Continue
4$:	RET
.PAGE
;Here with RTT_LPENDING entry whose retry count has run out.  We first call the
;  output done routine for each packet in the packet hold list with an error
;  of ER_NSNOD.  Then we give up the packet containing the ARP request.
;  Finally, we give up the routing table entry.
;	c{ECX} = Offset of routing table entry
;	c{EDI} = Offset of PDB
;	c{ESI} = Offset of SDB

pendfail:
	CALL	xosnetGiveBufr##	;Give up the ARP request packet
	CALL	discard			;Discard any held packets
pendlp:	CMPL	rt_rtnhead[ECX], #0	;Have any foreign or default entires
					;  linked to this one?
	JE	givefrc			;No - go on
	MOVL	EDX, rt_rtnhead[ECX]	;Yes - unlink one
	MOVL	EAX, rt_rtnnext[EDX]
	MOVL	rt_rtnhead[ECX], EAX
	TESTL	EAX, EAX
	JNE	4$
	MOVL	rt_rtntail[ECX], EAX
4$:	PUSHL	ECX
	MOVL	ECX, EDX
	CLRL	EAX
	MOVL	rt_rtnnext[ECX], EAX
	MOVL	rt_rtnprev[ECX], EAX
	CMPB	rt_type[ECX], #RTT_DPENDING ;Default entry?
	JNE	failfrgn		;No - go on
	MOVB	AL, rt_retry[ECX]	;Get the state
	MOVL	EDX, pdb_ipdrthead[EDI]
6$:	ORL	EDX, EDX
	JE	failfrgn
	CMPL	dct_ipnum[EDX], EAX
	JA	nextdft
	MOVL	EDX, dct_lnknext[EDX]
	JMP	6$
.PAGE
;Here with another default router address to try
;	c{ECX} = Offset of RTT_DPENDING entry

nextdft:MOVB	AL, dct_ipnum[EDX]	;Update state
	MOVB	rt_retry[ECX], AL
	MOVL	EAX, dct_data[EDX]	;Get IP address for router
	PUSHL	ECX
	CALL	rtsearch		;Look for routing table entry
	JE	6$			;Found one (must have snuck in!)
	CALL	doarp			;Create new pending entry and make an
	POPL	EDX			;  ARP packet
	JC	failfrgn		;Give up if error here
	MOVL	rt_router[EDX], ECX	;Link the new RTT_LPENDING entry to the
	MOVL	rt_rtnhead[ECX], EDX	;  old RTT_DPENDING entry
	MOVL	rt_rtntail[ECX], EDX
	MOVL	npb_outdone[EBX], #ret002
	MOVL	EAX, #-1
	MOVL	EDX, EAX
	CALL	xossnpSendPktHw##	;Send the ARP packet (ignore error - it
	POPL	ECX			;  will be retried normally in this
	JMP	pendlp			;  case)

;Here if found a routing table entry for a default router above - it must
;  have snuck in from someone else trying to access it - if its a LOCAL entry
;  we use it and finish with this entry - if its a PENDING entry we link to
;  it and let things finish up as they happen - if its anything else, someone
;  is confused and we make this attempt fail

6$:	POPL	EDX
	MOVB	AL, rt_type[EDX]
	CMPB	AL, #RTT_LOCAL		;Is this a local entry?
	JE	7$			;Yes
	CMPB	AL, #RTT_LPENDING	;No - is it a local pending entry?
	JE	8$			;Yes - go on
	JMP	failfrgn		;No - the sub-net mask must have
					;  changed - fail this time!

;Here with a local entry - we are finished now

7$:	PUSHL	ECX
	PUSHL	EDX
	MOVL	EAX, rt_hwaddr+0[ECX]
	MOVL	EDX, rt_hwaddr+4[ECX]
	CALL	unhold			;Handle any held packets
	POPL	ECX
	POPL	EDX
	MOVB	rt_type[ECX], #RTT_FOREIGN ;Update routing entry type
8$:	MOVL	rt_router[EDX], ECX	;Link it to the RTT_LOCAL or
	MOVL	EAX, rt_rtntail[ECX]	;  RTT_LPENDING entry
	TESTL	EAX, EAX
	JNE	10$
	MOVL	rt_rtnhead[ECX], EDX
	JMP	12$

10$:	MOVL	rt_rtnnext[EAX], EDX
12$:	MOVL	rt_rtntail[ECX], EDX
	POPL	ECX
	JMP	pendlp			;Continue
.PAGE
;Here to give up on the linked entry (either it was a simple foreign entry
;  or it was a default entry with no more default routers to try)
;	c{ECX} = Offset of RTT_DPENDINGor RTT_FOREIGN entry

failfrgn:
	CALL	discard			;Discard any held packets
	CALL	givefrc			;Remove the routing table entry
	POPL	ECX			;Restore offset of the RTT_LPENDING
					;  entry
	JMP	pendlp			;Continue

;Here with all held packets discarded and any linked foreign or default
;  entries handled

givefrc:MOVB	rt_type[ECX], #RTT_LOCAL ;Change entry type so can remove it
					;Fall into rttimeout to remove the
					;  routing table entry

;Subroutine called when normal routing table entry times out
;	c{ECX} = Offset of routing table entry
;	c{ESI} = Offset of SDB
;	CALL	rttimeout
;	c{ECX} = Offset of next routing table entry

rttimeout::
	MOVL	EAX, rt_ipaddr[ECX]	;Remove the entry
	CMPB	rt_type[ECX], #RTT_ROUTER ;Is this a router entry?
	JE	18$			;Yes
	MOVB	DL, #0			;No
	CALL	xosipsRtRmvOne		;Just remove it
	JC	20$			;It must be there!
	RET

;Here if have router entry - we don't remove it directly but try to ping it
;  first - the ping timeout routine will remove it if there is no reponse

18$:	MOVB	DL, #2			;Get time-out value
	MOVB	DH, #3			;Get number of retries
	PUSHL	rt_next[ECX]
	CALL	icmprouteping#
	POPL	ECX
	RET

20$:	CRASH	RTCO			;[Routing Table Corrupted]
.PAGE
	.SBTTL	discard - Subroutine to discard held packets for routing table entry

;Subroutine to discard held packets for a routing table entry
;	c{ECX} = Offset of routing table entry
;	CALL	discard

discard:MOVL	EBX, rt_holdhead[ECX]	;Get first packet to discard
	TESTL	EBX, EBX
	JE	10$.S			;If none
	PUSHL	ECX
	PUSHL	EDI
4$:	PUSHL	npb_nextsnd[EBX]	;Save next packet to discard
	ORB	npb_sts[EBX], #NPS$NANA ;Indicate ARP failure for packet
	MOVL	npb_nextsnd[EBX], #-1	;Indicate its not being output now
	MOVL	EDI, npb_dcb[EBX]
	MOVL	ECX, npb_outdone[EBX]
	MOVL	npb_outdone[EBX], #0
	MOVL	EAX, #ER_NSNOD		;Get error code
	TESTL	ECX, ECX
	JE	6$.S
	CALLI	ECX			;Call output done routine for packet
6$:	POPL	EBX			;Restore next packet to discard
	TESTL	EBX, EBX		;Have any more?
	JNE	4$.S			;Yes - continue
	POPL	EDI			;No
	POPL	ECX
	CLRL	EAX
	MOVL	rt_holdhead[ECX], EAX
	MOVL	rt_holdtail[ECX], EAX
10$:	RET
.PAGE
	.SBTTL	rtgood - Subroutine to mark route as good

;Subroutine to mark route as good.  This subroutine is called whenever a
;  known response is received from a node.  Its main purpose is to reset the
;  time-to-live timer for the routing table entry to its maximum value.
;	c{EAX} = IP address
;	c{EDI} = Offset of PDB
;	c{ESI} = Offset of SDB
;	CALL	rtgood

rtgood::CALL	rtsearch		;Find the routing table entry
	JNE	4$.S			;If not there
	ORB	rt_sts[ECX], #RTS$DATASEEN ;Remember have seen data
	CMPB	rt_type[ECX], #RTT_LPENDING ;Should we do anything else here?
	JAE	4$.S			;No
	MOVL	rt_t2lcnt[ECX], #RTT2L_LOCAL ;Yes - reset the timer
	CMPL	rt_router[ECX], #0	;Is it using a router?
	JE	4$.S			;No - finished
	MOVL	EAX, rt_router[ECX]	;Yes - update its time too
	MOVL	rt_t2lcnt[EAX], #RTT2L_LOCAL
4$:	RET
.PAGE
	.SBTTL	rtredirect - Subroutine to redirect a route

;Subroutine to redirect a route - this subroutine converts an RTT_FOREIGN,
;  RTT_FPENDING, or RTT_DPENDING entry to an RTT_FOREIGN or RTT_FPENDING entry
;  linked to a different RTT_LOCAL or RTT_LPENDING entry.
;	c{EAX} = Foreign IP address to redirect
;	c{ECX} = New router IP address (must be local)
;	c{EDI} = Offset of PDB
;	c{ESI} = Offset of ICMP TPDB
;	CALL	rtredirect

$$$=!0
FRM rtr_ipaddr  , 4t
FRM rtr_rtipaddr, 4t
FRM rtr_local   , 4t
rtr_SIZE=!$$$

rtredirect::
	ENTER	rtr_SIZE, 0
	MOVL	rtr_ipaddr[EBP], EAX	;Store IP address
	MOVL	rtr_rtipaddr[EBP], ECX	;Store new router address
	MOVL	EAX, ECX
	CLRL	EBX			;Indicate no packet
	CLRL	ECX
	CALL	geta2			;Get hardware address for the new router
	JC	16$.S			;If error
	TESTL	EBX, EBX		;Do we need to send a packet now?
	JE	2$.S			;No
	PUSHL	ECX			;Yes
	MOVL	npb_outdone[EBX], #ret002
	CALL	xossnpSendPktHw##	;Send the packet
	POPL	ECX
2$:	MOVB	AL, rt_type[ECX]
	CMPB	AL, #RTT_LPENDING 	;Is it a local address?
	JE	8$.S
	CMPB	AL, #RTT_ROUTER
	JE	8$.S
	CMPB	AL, #RTT_LOCAL
	JNE	16$.S			;No - forget it!
	MOVB	rt_type[ECX], #RTT_ROUTER ;OK
8$:	INCL	tpdb_icmpcntredirect[ESI] ;Count the redirected connection
	MOVL	rtr_local[EBP], ECX	;Save offset of local entry
	MOVL	EAX, rtr_ipaddr[EBP]	;Find entry for address to redirect
	CALL	rtsearch
	JE	10$.S			;If found it
	CLRL	EDX			;Note found - create one
	CLRL	ECX			;Don't worry about the hardware address
	MOVB	BL, #RTT_TEMP		;  since we don't really need it here!
	MOVB	BH, #1
	CALL	rtupdate
	JC	16$.S
10$:	CMPL	rt_router[ECX], #0	;Is it linked to a router now?
	JE	12$.S			;No
	CALL	rtunlkf			;Yes - unlink it
12$:	MOVL	EDX, rtr_local[EBP]	;Get offset of entry for router
	MOVB	AL, #RTT_FOREIGN	;Assume we have the router address now
	CMPB	rt_type[EDX], #RTT_ROUTER ;Right?
	JE	14$.S			;Yes
	MOVB	AL, #RTT_FPENDING	;No
14$:	MOVB	rt_type[ECX], AL
	MOVL	rt_router[ECX], EDX
	MOVL	EAX, rt_rtnhead[EDX]
	MOVL	rt_rtnnext[ECX], EAX
	MOVL	rt_rtnhead[EDX], ECX
	CLRL	EAX
	MOVL	rt_rtnprev[ECX], EAX
16$:	LEAVE				;Finished
	RET
.PAGE
	.SBTTL	Low level routing table manipulation routines

	.SBTTL	rtupdate - Subroutine to update IP routing table if necessary

;Subroutine to update IP routing table if necessary.  This subroutine handles
;  updating of existing entries, simple creation of routing table entries,
;  and conversion of RTT_LPENDING to RTT_LOCAL entries.  Generally, this
;  subroutine handles all routing table entry creations and conversions that do not
;  require generating an ARP request packet.  These conversions are handled by
;  the rtgetaddr and rtredir subroutines, which call this subroutine.  This
;  subroutine is also called by IPSCLS when an ARP packet is received.
;	c{EAX}     = IP address
;	c{EDX,ECX} = New hardware address
;	c(BL)      = Entry type
;	c(BH)      = Force update flag (0 = do not force, 1 = force)
;	c{EDI}     = Offset of PDB
;	c{ESI}     = Offset of SDB
;	CALL	rtupdate
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{ECX} = Offset of routing table entry (may be 0 if update not forced)

$$$=!0
FRM rtupd_ipaddr, 4t		;IP address
FRM rtupd_hwaddr, 8t		;Hardware address
FRM             , 2t
FRM rtupd_force , 1t		;1 to force update
FRM rtupd_type  , 1t		;Entry type
rtupd_SIZE=!$$$

rtupdate::
	ENTER	rtupd_SIZE, 0		;Set up our stack frame
	MOVL	rtupd_ipaddr[EBP], EAX	;Store IP address
	MOVL	rtupd_hwaddr+0[EBP], ECX ;Store hardware address
	MOVL	rtupd_hwaddr+4[EBP], EDX
	MOVL	rtupd_type[EBP], EBX	;Store type and force flag
	CALL	rtsearch		;Search the routing table
	JE	rtupfnd			;If found it
	MOVL	ECX, pdb_iprtinuse[EDI]	;Not there - have room for another
	CMPL	ECX, pdb_iprtmax[EDI]	;  routing table entry?
	JB	14$			;Yes
	CMPB	rtupd_force[EBP], #0	;No - is this update forced?
	JNE	6$			;Yes
	CLRL	ECX			;No - nothing to do here
rtupd3:	LEAVE
	RET

;Here if have a firm request to update the routing table and the routing table
;  is full.  In this case (which should not happen very often), we scan the
;  table and find the entry which was accessed least recently and remove it.

6$:	PUSHL	EDX			;Save routing table hash index for
					;  new entry
	MOVL	EBX, pdb_iprthead[EDI]	;Get first routing table entry
	MOVL	EDX, #-1		;Initialize oldest entry time
	CLRL	ECX			;Initialize oldest entry offset
8$:	CMPB	rt_type[EBX], #RTT_PERM ;Can we remove this one?
	JAE	10$			;No
	CMPL	rt_timestamp[EBX], EDX	;Yes - is this one older?
	JAE	10$			;No
	MOVL	EDX, rt_timestamp[EBX]	;Yes - remember it
	MOVL	ECX, EBX
10$:	MOVL	EBX, rt_next[EBX]	;Advance to next entry
	TESTL	EBX, EBX
	JNE	8$			;Continue if more to check
	JREGZ	ECX, 12$		;If we didn't find one we can remove
	MOVL	EAX, rt_ipaddr[ECX]	;Get IP address for oldest entry
	MOVB	DL, #1
	CALL	xosipsRtRmvOne		;Remove the entry
	JC	20$			;This should not fail!
12$:	POPL	EDX
14$:	PUSHL	ESI			;Get a QEL for a new routing table entry
	CALL	knlGetQel##
	MOVL	ECX, ESI
	POPL	ESI
	JC	rtupd3
	INCL	pdb_iprtinuse[EDI]
	MOVL	EAX, pdb_iprthash[EDI+EDX*4] ;Link into hash list
	MOVL	pdb_iprthash[EDI+EDX*4], ECX
	MOVL	[ECX], EAX
	MOVL	EAX, pdb_iprthead[EDI]	;Put at beginning of use list
	MOVL	pdb_iprthead[EDI], ECX
	MOVL	rt_next[ECX], EAX
	TESTL	EAX, EAX
	JE	16$
	MOVL	rt_prev[EAX], ECX
	CLRL	EAX
16$:	MOVL	rt_prev[ECX], EAX
	MOVL	rt_rtnhead[ECX], EAX
	MOVL	rt_rtntail[ECX], EAX
	MOVL	rt_pkt[ECX], EAX
	MOVB	rt_sts[ECX], AL
	MOVB	rt_type[ECX], #RTT_TEMP
	JMP	rtupd6

20$:	CRASH	RTCO			;[Routing Table COrrupted
.PAGE
;Here if a routing table entry was found for this IP address

rtupfnd:CMPB	rt_type[ECX], #RTT_LPENDING ;Is this a pending local entry?
	JNE	rtupd4.S		;No - go on
	CMPB	rtupd_type[EBP], #RTT_LPENDING ;Yes - trying to make a local
					       ;  pending entry?
	JE	rtupd3			;Yes - don't update anything
	MOVL	EBX, rt_pkt[ECX]	;No - have a request packet?
	TESTL	EBX, EBX
	JE	2$.S			;No
	CALL	xosnetGiveBufr##	;Yes - give it up
	CLRL	EBX
	MOVL	rt_pkt[ECX], EBX
2$:	MOVL	EAX, rtupd_hwaddr+0[EBP]
	MOVL	EDX, rtupd_hwaddr+4[EBP]
	CALL	unhold			;Handle any held packets
	CMPL	rt_rtnhead[ECX], #0	;Have any linked RTT_DPENDING or
					; RTT_FPENDING entries?
	JE	rtupd6.S		;No - go on
	PUSHL	ECX			;Yes
	MOVL	ECX, rt_rtnhead[ECX]
4$:	CMPB	rt_type[ECX], #RTT_DPENDING ;Is this an RTT_DPENDING entry?
	JE	5$.S			;Yes
	CMPB	rt_type[ECX], #RTT_FPENDING ;No - is it an RTT_FPENDING entry?
	JNE	6$.S			;No!
5$:	MOVB	rt_type[ECX], #RTT_FOREIGN ;Yes - change it to RTT_FOREIGN
	CALL	unhold			;Send its held packets
	MOVL	ECX, rt_rtnnext[ECX]
	TESTL	ECX, ECX
	JNE	4$.S
	POPL	ECX
	JMP	rtupd6.S		;Continue

6$:	CRASH	IRTE			;[Illegal Routing Table Entry]

;Here if we did not have a pending local routing table entry

rtupd4:	MOVB	AL, rt_type[ECX]	;Are we trying to change the type of
	CMPB	AL, rtupd_type[EBP]	;  the table entry?
	JE	rtupd6.S		;No - go on
	CMPB	AL, #RTT_ROUTER		;Yes - is this a router entry?
	JNE	10$.S			;No
	CMPB	rtupd_type[EBP], #RTT_LOCAL ;Yes - trying to change it to a
					      ;  local entry?
	JNE	10$.S			;No
	MOVB	rtupd_type[EBP], AL	;Yes - this is OK, but leave it a
	JMP	rtupd6.S		;  router entry

;Here if we are trying to change the type of a routing table entry.  This
;  should never happen but is possible if the user changes the IP address or
;  sub-net mask while the system is running.  (Note that if it is necessary
;  to change either the IP address of sub-net mask on a running system, the
;  routing cache should be flushed immediately afterwards!)  If we get here,
;  we delete the old entry and return an error!

10$:	CALL	rtrem4			;Remove the routing table entry
	LEAVE
	MOVL	EAX, #ER_NRTER
	STC
	RET				;And return
.PAGE
;Here with type OK

rtupd6:	MOVL	EAX, rtupd_ipaddr[EBP] ;Store IP address in entry
	MOVL	rt_ipaddr[ECX], EAX
	MOVL	EAX, rtupd_hwaddr+0[EBP] ;Store hardware address in entry
	MOVL	rt_hwaddr+0[ECX], EAX
	MOVL	EAX, rtupd_hwaddr+4[EBP]
	MOVL	rt_hwaddr+4[ECX], EAX
	CLRL	EAX
	MOVL	rt_router[ECX], EAX	;Clear router entry offset
	MOVL	rt_usecnt[ECX], EAX	;Clear use count
	MOVB	AL, rtupd_type[EBP]	;Get new entry type
	CMPB	AL, #RTT_LOCAL		;Changing type to RTT_LOCAL?
	JNE	4$.S			;No
	CMPB	rt_type[ECX], #RTT_ROUTER ;Yes - is this a router now?
	JE	6$.S			;Yes - leave the type along
	CMPL	rt_rtnhead[ECX], #0	;No - is this really a router?
	JE	4$.S			;No
	MOVB	AL, #RTT_ROUTER		;Yes
4$:	MOVB	rt_type[ECX], AL
6$:	MOVL	EAX, knlSchTtl##	;Store time-stamp value
	MOVL	rt_timestamp[ECX], EAX
	CLC
	LEAVE
	RET
.PAGE
	.SBTTL	unhold - Subroutine to handle held packets

;Subroutine to handle held packets
;	c{EDX,EAX} = Remote hardware address
;	c{ECX}     = Offset of routing table entry
;	CALL	unhold

unhold:	MOVL	EBX, rt_holdhead[ECX]	;No - get first packet to send
	TESTL	EBX, EBX
	JE	6$.S			;If none
	PUSHL	ECX
4$:	PUSHL	npb_nextsnd[EBX]	;Save next packet to send
	MOVL	npb_nextsnd[EBX], #-1	;Indicate not being output now
	MOVL	npb_holdrt[EBX], #0
	PUSHL	EAX
	PUSHL	EDX
	CALL	xossnpSendPktHw##	;Send the packet
	POPL	EDX
	POPL	EAX
	POPL	EBX			;Restore next packet to send
	TESTL	EBX, EBX		;Have any more?
	JNE	4$.S			;Yes - continue
	POPL	ECX
	CLRL	EAX			;No - finished here
	MOVL	rt_holdhead[ECX], EAX
	MOVL	rt_holdtail[ECX], EAX
6$:	RET
.PAGE
	.SBTTL	rtremove - Subroutine to remove entry from the IP routing table

;Subroutine to remove entry from the IP routing table.
;	c{EAX} = IP address to remove (0 to clear routing table)
;	c(DL)  = Purge flag (1 = purge (do not verify routers), 0 = remove)
;	c{EDI} = Offset of PDB
;	c{ESI} = Offset of SDB
;	CALL	rtremove
;	C:set = Not in routing table
;	C:clr = Normal
;	  c{EAX} = Number of entries removed
;	  c{ECX} = Offset of next entry

rtremove::
	REQUIREFORK
	TESTL	EAX, EAX		;Want to clear the routing table?
	JNE	xosipsRtRmvOne	;No
	CMPL	sdb_label[ESI], #'SDB*'	;Yes - make sure have an SDB
	JNE	14$
	PUSHL	#0			;Initialize number to return

;;;;;	MOVL	EBX, pdb_ipndb[EDI]

	MOVL	ECX, pdb_iprthead[EDI]
	JREGZ	ECX, 8$
2$:	CMPB	rt_type[ECX], #RTT_PERM ;Should we remove this one?
	JB	4$			;Yes
	CMPB	rt_type[ECX], #RTT_PING ;Maybe
	JAE	12$			;No
4$:	MOVL	EAX, rt_ipaddr[ECX]	;Yes - Get IP address
	PUSHL	ECX
	CALL	xosipsRtRmvOne		;Try to remove the entry
	JC	10$			;If error
	POPL	EDX			;OK - discard saved entry offset
	ADDL	[ESP], EAX		;Count it
6$:	TESTL	ECX, ECX		;More to do?
	JNE	2$			;Yes - continue
8$:	POPL	EAX			;No - get number removed
	RET				;Finished

;Here if have entry which cannot be removed here

10$:	POPL	ECX
12$:	MOVL	ECX, rt_next[ECX]	;Advance to next entry
	JMP	6$.S			;Continue

14$:	CRASH	NSDB			;[Not an SDB]
.PAGE
	.SBTTL	xosipsRtRmvOne - Subroutine to remove single routing table entry

;Subroutine to remove a single entry from the routing table (will also remove
;  linked foreign entires when router entry removed)
;	c{EAX} = IP address to remove (0 to clear routing table)
;	c(DL)  = Purge flag (1 = purge (do not verify routers), 0 = remove)
;	c{EDI} = Offset of PDB
;	c{ESI} = Offset of SDB
;	CALL	rtrmone
;	C:set = Not in routing table
;	C:clr = Normal
;	  c{EAX} = Number of entries removed
;	  c{ECX} = Offset of next entry

xosipsRtRmvOne::
	CMPL	sdb_label[ESI], #'SDB*'
	JNE	4$.S
	PUSHL	EDX			;Save the purge flag
	CALL	rtsearch		;Search for entry in the routing table
	POPL	EDX
	JNE	12$.S			;If not found
rtrem4:	MOVB	AL, rt_type[ECX]
	CMPB	AL, #RTT_LPENDING	;Can this entry be removed?
	JAE	6$.S			;Maybe not
	CMPB	AL, #RTT_FOREIGN	;Yes - is it a foreign entry
	JE	rtremfor.S		;Yes
2$:	CMPL	rt_rtnhead[ECX], #0	;No - does it have a node list?
	JNE	rtremrtr.S		;Yes - its a router entry
	JMP	rtremlcl.S		;No - its just a simple entry

4$:	CRASH	NSDB			;[Not an SDB]

;Here if have a pending entry

6$:	CMPL	rt_holdhead[ECX], #0	;Is the entry holding any packets?
	JNE	12$.S			;Yes - it cannot be removed!
	CMPB	AL, #RTT_LPENDING	;No - is it a local pending entry?
	JNE	rtremfor.S		;No - its a foreign entry
	JMP	2$.S			;Yes

;Here if entry not found or cannot be removed

12$:	STC
	RET

;Here to give up a foreign entry.  This is an entry which specifies a foreign
;  IP address and which is linked to a local entry which specifies the
;  corresponding router IP and hardware addresses.  It is linked to the node
;  list for the router so we must remove it from this list before giving up
;  the entry.  Also, since a request to remove a foreign entry may indicate a
;  problem with the associated router, after removing the entry we try to ping
;  the router.  If it does not respond, we also remove its routing table entry
;  (and all other associated foreign entries).  If a router fails, the first
;  indication we get is usually a failure of communication with some foreign
;  node which is using the router.  Often, this is the ONLY indication we get!
;  If we do not verify the validity of the router in this case, we may not be
;  able to route around it, especially if it is the current primary default
;  router!

rtremfor:
	PUSHL	EBX			;Save offset of previous entry
	CMPB	DL, #0			;Should we ping the router?
	JNE	16$.S			;No
	PUSHL	ECX			;Yes
	MOVL	ECX, rt_router[ECX]
	CMPB	rt_type[ECX], #RTT_LPENDING ;Is it a pending entry?
	JAE	14$.S			;Yes - don't bother pinging
	MOVL	EAX, rt_ipaddr[ECX]	;No - ping the router
	CALL	icmprouteping#
14$:	POPL	ECX
16$:	CALL	rtunlkf			;Unlink the foreign entry from its
					;  router entry
	POPL	EBX			;Restore offset of previous entry
					;Fall into rtremlclto give up this
					;  entry

;Here to give up a local routing table entry

rtremlcl:
	PUSHL	rt_next[ECX]		;Save offset of next entry
	CALL	rtunlnk2		;Unlink the block from the lists
	PUSHL	ESI
	MOVL	ESI, ECX		;Give it up
	CALL	knlGiveQel##
	POPL	ESI
	DECL	pdb_iprtinuse[EDI]
	POPL	ECX			;Restore offset of next entry
	CLRL	EAX			;Clear C, return 1
	INCL	EAX
	RET

;Here to give up the routing table entry for a router - we first give up all
;  of the entries in the router's node list

rtremrtr:
	PUSHL	#1
	PUSHL	EBX
20$:	PUSHL	ECX
	MOVL	ECX, rt_rtnhead[ECX]	;Get next node which uses the router
	MOVL	EAX, rt_ipaddr[ECX]
	MOVB	DL, #1
	CALL	xosipsRtRmvOne		;Remove its entry
	JC	22$.S			;It must be there!
	POPL	ECX
	INCL	4[ESP]			;Count it
	CMPL	rt_rtnhead[ECX], #0	;Have any more nodes?
	JNE	20$.S			;Yes - continue
	POPL	EBX			;No - almost finished
	CALL	rtremlcl		;Give up the router entry
	POPL	EAX			;Get value to return
	RET

22$:	CRASH	RTCO			;[Routing Table COrrupted
.PAGE
	.SBTTL	rtunlkf - Subroutine to unlink a foreign routing table entry

;Subroutine to unlink a foreign routing table entry from its router entry
;	c{ECX} = Offset of foreign entry
;	CALL	rtunlkf

rtunlkf:
	MOVL	EBX, rt_router[ECX]
	MOVL	EAX, rt_rtnprev[ECX]
	MOVL	EDX, rt_rtnnext[ECX]
	TESTL	EAX, EAX		;Have previous entry?
	JE	10$.S			;No
	MOVL	rt_rtnnext[EAX], EDX	;Yes
	JMP	12$.S

10$:	MOVL	rt_rtnhead[EBX], EDX
12$:	TESTL	EDX, EDX		;Have next entry?
	JE	14$.S
	MOVL	rt_rtnprev[EDX], EAX
	JMP	16$.S

14$:	MOVL	rt_rtntail[EBX], EAX
16$:	CLRL	EAX
	MOVL	rt_router[ECX], EAX
	RET
.PAGE
	.SBTTL	rtsearch - Subroutine to search for routing table entry

;Subroutine to search for routing table entry
;	c{EAX} = IP address to search for
;	c{EDI} = Offset of PDB
;	c{ESI} = Offset of SDB
;	CALL	rtsearch
;	c{EDX} = Routing table hash index for entry
;	Z:set = Entry found
;	  c{ECX} = Offset of routing table entry
;	  c{EBX} = Offset of previous entry in hash list
;	Z:clr = Not found

rtsearch:
	PUSHL	EAX			;Save actual address
	PUSHL	EAX
	MOVZBL	EDX, AL			;Calculate hash index
	XORB	DL, AH
	SHRL	EAX, #16t
	XORB	DL, AL
	XORB	DL, AH
	POPL	EAX
	LEAL	EBX, pdb_iprthash[EDI+EDX*4]
4$:	MOVL	ECX, [EBX]		;Get next block
	JREGZ	ECX, 6$			;If no more
	CMPL	rt_ipaddr[ECX], EAX	;Is it this one?
	JE	8$.S			;Yes - return with Z set
	MOVL	EBX, ECX		;No - advance to next
	JMP	4$.S			;Continue

6$:	INCL	ECX			;Not found - clear Z
8$:	POPL	EAX			;Restore actual address
	RET
.PAGE
	.SBTTL	rtunlink - Subroutine to remove routing table entry

;Subroutine to remove routing table entry (does NOT give up the memory block)
;	c{ECX} = Offset of entry
;	c{EDI} = Offset of PDB
;	CALL	rtunlink

rtunlink:
	MOVL	EAX, rt_ipaddr[ECX]	;Calculate its hash index
	MOVZBL	EDX, AL
	XORB	DL, AH
	SHRL	EAX, #16t
	XORB	DL, AL
	XORB	DL, AH
	LEAL	EBX, pdb_iprthash[EDI+EDX*4]
4$:	CMPL	[EBX], ECX		;Find previous block
	JE	rtunlnk2.S		;OK - go unlink everything
	MOVL	EBX, [EBX]		;Advance to next
	TESTL	EBX, EBX
	JNE	4$
	CRASH	BRTH			;[Bad Routing Table Hash list]

	.SBTTL	rtunlnk2 - Subroutine to unlink block from routing table lists

;Subroutine to unlink block from the routing table lists
;	c{ECX} = Offset of block
;	c{EBX} = Offset of previous block in hash list (points to hash table
;		   entry if this is the first block in the hash list)
;	c{EDI} = Offset of PDB
;	CALL	rtunlnk2

rtunlnk2:
	MOVL	EDX, [ECX]		;Remove block from its hash list
	MOVL	[EBX], EDX
	MOVL	EBX, rt_next[ECX]	;Remove block from the routing table
	MOVL	EAX, rt_prev[ECX]	;  list
	TESTL	EAX, EAX
	JE	8$
	MOVL	rt_next[EAX], EBX
	JMP	10$

8$:	MOVL	pdb_iprthead[EDI], EBX
10$:	TESTL	EBX, EBX
	JE	12$
	MOVL	rt_prev[EBX], EAX
12$:	RET
.PAGE
	.SBTTL	routeoas - Once-a-second routine for routing

;Once-a-second routine for routing

routeoas::
	MOVL	EDI, xosipsPdbHead#	;Get offset of first IPS PDB in system
	TESTL	EDI, EDI
	JE	14$			;If none
4$:	TOFORK
	MOVL	ECX, pdb_iprthead[EDI]	;Get offset of first routing table entry
	JREGZ	ECX, 12$		;If none
6$:	CMPL	rt_t2lcnt[ECX], #0	;Are we timing this one?
	JE	8$.S			;No
	DECL	rt_t2lcnt[ECX]		;Yes - count it down
	JE	20$			;If it timed-out
8$:	MOVL	ECX, rt_next[ECX]	;Advance to next routing table entry
10$:	TESTL	ECX, ECX		;More?
	JNE	6$			;Yes - continue
12$:	FROMFORK
	MOVL	EDI, pdb_nextd[EDI]	;No - advance to next IPS PDB
	TESTL	EDI, EDI		;More?
	JNE	4$			;Yes - continue
14$:	JMP	dnsoas#			;No - all done here - now do the DNS
					;  once-a-second stuff

;Here with routing table entry which has timed-out

20$:	PUSHL	EDI
	MOVL	ESI, pdb_sdb[EDI]
	CALLI	rt_timeout[ECX]		;Call the time-out routine for the entry
	POPL	EDI
	JMP	10$			;Continue

	LKEEND
