	.TITLE	fdkadev - Floppy disk device driver type A

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.PROC	80486
	.INCLUD	XOSINC:\xmac\xos.par
	.INCLUD	XOSINC:\xmac\xosx.par
	.INCLUD	XOSINC:\xmac\xoserr.par
	.INCLUD	XOSINC:\xmac\pcat.par
	.INCLUD	XOSINC:\xmac\xosdisk.par
	.INCLUD	XOSINC:\xmac\xosxdisk.par
	.INCLUD	XOSINC:\xmac\xostime.par
	.INCLUD	XOSINC:\xmac\xoslke.par
	.INCLUD	XOSINC:\xmac\xosxlke.par

;This is the device driver for the PC/AT floppy disk controller.  It supports
;  up to 4 drives on each controller and any number of controllers.  Each
;  controller must have a unique IO register address but all controllers must
;  use IRQ 6 and DMA 2.  The use of the IRQ and DMA lines is treated as a
;  shared resource and only one controller can actually be transfering data
;  at a time.

MAJV   =!1t
MINV   =!1t
EDITNUM=!3t

;v1.0.0  19-JuL-94
;	Initial version
;v1.1.0  28-Aug-94
;	Fixed problem with counting some device errors.
;v1.1.1  29-Dec-94
;	Added code to check status register before reading first status byte
;	after interrupt.  Some Dell machines don't have the byte ready
;	immediately and trying to read it too soon really messes up the
;	controller!
;v1.1.2  23-Jan-94
;	Fixed problem with hardware errors with no corresponding counters,
;	such errors incremented UCB link pointer and name!
;v1.1.3  20-Nov-99
;	Removed the BOOT load-time characteristic.  This is no longer needed
;	since DISKINST is now used to set up boot-time devices.

	LKEHEAD	FDKADRV, MAJV, MINV, EDITNUM, LKETYPE_DEVICE

;Following table describes the supported drives types:
;    Size     TrackDen DataDen Mod  RotSpeed   Freq
;    8"DD     48tpi    Single  FM    360rpm   500KHz
;    8"DD     48tpi    Double  MFM   360rpm   500KHz
;    5.25"DD  48tpi    Single  FM    300rpm   250KHz
;    5.25"DD  48tpi    Double  MFM   300rpm   250KHz
;    5.25"HD  48tpi    Single  FM    360rpm   300KHz
;    5.25"HD  48/96tpi Double  MFM   360rpm   300KHz
;    5.25"HD  96tpi    High    MFM   360rpm   500KHz
;    3.5"DD   135tpi   Double  MFM   300rpm   250KHz
;    3.5"HD   135tpi   Double  MFM   300rpm   250KHz
;    3.5"HD   135tpi   High    MFM   300rpm   500KHz

;Following table gives the default initial parameters for each supported drive
;  type:
;Type TrackDen DataDen Mod   Freq  MS Rmv SecSz Sect Hd Trk SRT HUT HLT MST
; DD8  48tpi   Double  MFM  500KHz  N  Y   512    8   2  77  8  240  1  1000
; DD5  48tpi   Double  MFM  300KHz  N  Y   512    9   2  40  6  240  1  1000
; HD5  96tpi   High    MFM  500KHz  Y  Y   512   15   2  80  3  240  1  1000
; DD3  135tpi  Double  MFM  250KHz  Y  Y   512    9   2  80  6  240  1  1000
; HD3  135tpi  High    MFM  500KHz  Y  Y   512   18   2  80  6  240  1  1000

;When attempting to mount a disk, the sector size is first determined by
;  reading a header on track 0, head 0.  The initial parameters are then first
;  used to attempt to read sector 1, track 0, head 0 if the disk is not
;  mounted.  If it is mounted, the current parameter values are used first,
;  followed by the use of the initial parameters.  If this fails, the next
;  lowest density is used (the initial density is assumed to be the highest
;  density the drive supports), except that single density is never used on
;  5.25" drives.  If this sector is a valid boot block for a supported file
;  system, the disk is mounted and the current parameters are set from data in
;  the boot block.  If the disk does not contain a valid boot block, it is
;  mounted as a non-file structured disk with the initial parameters for the
;  drive.

;Some floppy controllers reverse the sense of the media change bit in the
;  digital input register.  To handle this, we do not use this bit until we
;  have done a succesful access of a floppy on other than track 0 (which should
;  insure that the media change bit has been reset).  We then check the media
;  change bit and assume that the sense it has at that time means that a disk
;  is present.

;Define offsets in the device dependent part of the KCB for floppy disks

$$$=!kcb_devdep
BLK kcb_fdmoto  , 1t	;Motor on bits for controller
BLK kcb_fdtrkd  , 1t	;Current track density (value for P_DC1FDCTL)
BLK kcb_fdselect, 1t	;Current drive select value
BLK kcb_fdtype  , 1t	;Floppy controller type
BLK kcb_fdspcfy , 2t	;Current specify command value
BLK kcb_fdmssen , 1t	;Media sensor sense
BLK             , 1
kcb_fdSIZE=!$$$

;Define offsets in the stack frame used by all routines which access the floppy
;  controller

$$$=!0
FRM fdk_retry1, 1t		;Retry counter for restore during data transfer
FRM fdk_retry2, 1t		;Retry counter for restore during seek
FRM fdk_retry3, 1t		;Retry counter for seek operation
FRM fdk_retry4, 1t		;Retry counter for data transfer operation
				;Following three items must be in this order!
FRM           , 2t		;Reserved (must not be used)
FRM fdk_mode  , 1t		;Mode bits for transfer
FRM fdk_func  , 1t		;Function value for interface
FRM fdk_daddr , 4t		;Disk address
FRM fdk_buffer, 4t		;Address of buffer for transfer
FRM fdk_count , 4t		;Amount to transfer
FRM fdk_block , 4t		;Disk block number
FRM fdk_amount, 4t		;Total amount transfered
fdk_SIZE=!$$$

;Define values for fdk_mode

FM$LIST=!04h			;Transfering buffer list
FM$READ=!02h			;Reading data

	CODE

;Floppy disk device driver dispatch table

fdkdsp:	.LONG	fdkinit		;kf_init     =  0. - Initialize controller
	.LONG	0		;kf_getparms =  4. - Get drive parameters
	.LONG	0		;kf_getaparms=  8. - Get alternate drive
				;		       parameters
	.LONG	0		;kf_setparms = 12. - Set drive parameters
	.LONG	fdkreadraw	;kf_readraw  = 16. - Read raw block
	.LONG	fdkreaddata	;kf_readdata = 20. - Read block
	.LONG	fdkreadlist	;kf_readlist = 24. - Read buffer list
	.LONG	fdkreadid	;kf_readid   = 28. - Read ID field
	.LONG	fdkwritraw	;kf_writraw  = 32. - Write raw block
	.LONG	fdkwritdata	;kf_writdata = 36. - Write block
	.LONG	fdkwritlist	;kf_writlist = 40. - Write buffer list
	.LONG	fdkformat	;kf_format   = 44. - Format track
	.LONG	fdkchkwpf	;kf_chkwp    = 48. - Check write protect status
				;		       at fork level
	.LONG	fdkchkchgf	;kf_chkchg   = 52. - Check for possible disk
				;		       change at fork level
	.LONG	0		;kf_sessions = 56. - Get CD-ROM session list
	.LONG	0		;kf_tracks   = 60. - Get CD-ROM track list
	.LONG	fdkmedia	;ks_media    = 64. - Determine media type
	.LONG	fdkchkwp	;ks_chkwp    = 68. - Check write protect status
	.LONG	fdkchkchg	;ks_chkchg   = 72. - Check for possible disk
				;		       change
	.LONG	fdkchkchg	;ks_senchg   = 76. - Sense disk change
	.LONG	fdkaddunit	;ks_addunit  = 80. - Add disk unit
	.LONG	fdkremovebgn	;ks_removebgn = 84. - Begin removing disk
	.LONG	fdkremovefin	;ks_removefin = 88. - Finish removing disk

;Floppy disk device characteristics table

	.MOD	4
fdkdchartbl:
 CHARBGN  1, knlDCharValCom##
 CHARENT  CLASS   , TEXT, ,   8, knlDcMsgClass##    , knlDcGetClass##  , knlDcSetClass##   , 0
 CHARENT  TYPE    , TEXT, ,   4, knlDcMsgType##     , knlDcGet4Byte##  , 0                 , dcb_typename
 CHARENT  CONDESP , TEXT, ,   4, xosdskMsgConDsp##  , getcondesp       , 0                 , 0
 CHARENT  IOREG   , HEXV, ,   2, knlDcMsgIoReg##    , xosdskGtU2Byte## , 0                 , ucb_ioreg1
 CHARENT  INDEX   , DECV, ,   1, knlDcMsgIndex##    , xosdskGtU1Byte## , 0                 , ucb_index
 CHARENT  INT     , DECV, ,   1, knlDcMsgInt##      , xosdskGtK1Byte## , 0                 , kcb_intlvl
 CHARENT  UNITTYPE, TEXT, ,   4, knlDcMsgUnitType## , xosdskUnitType## , setunittype       , 0
 CHARENT  MSENSOR , TEXT, ,   4, xosdskMsgMSensor## , xosdskGetBits##  , xosdskSetBits##   , UB$MEDIAC
 CHARENT  REMOVE  , TEXT, ,   4, xosdskMsgRemove##  , xosdskGetRemove##, 0                 , 0
 CHARENT  VOLNAME , TEXT, ,  16, xosdskMsgVolName## , xosdskGtU16Byte##, xosdskStU8Byte##  , ucb_volname
 CHARENT  DOSNAME , TEXT, ,  16, xosdskMsgDosName## , xosdskGtU16Byte##, xosdskStU8Byte##  , ucb_dosname
 CHARENT  PARTN   , HEXV, ,   1, xosdskMsgPartn##   , xosdskGtU1Byte## , 0                 , ucb_partnx
 CHARENT  PARTOFF , DECV, ,   4, xosdskMsgPartOff## , xosdskGtU4Byte## , 0                 , ucb_partnoff
 CHARENT  TRKDEN  , DECV, ,   1, xosdskMsgTrkDen##  , gettrkden        , settrkden         , 0
 CHARENT  DATADEN , TEXT, ,   8, xosdskMsgDataDen## , getdataden       , setdataden        , 0
 CHARENT  CBLKSZ  , DECV, ,   2, xosdskMsgCBlkSz##  , xosdskGtU4Byte## , setcblksz         , ucb_csecsz
 CHARENT  CHEADS  , DECV, ,   1, xosdskMsgCHeads##  , xosdskGtU1Byte## , setsizecparm      , ucb_cheads
 CHARENT  CSECTS  , DECV, ,   4, xosdskMsgCSects##  , xosdskGtU4Byte## , setsizecparm      , ucb_csects
 CHARENT  CCYLNS  , DECV, ,   4, xosdskMsgCCylns##  , xosdskGtU4Byte## , setsizecparm      , ucb_ccylns
 CHARENT  CBLOCKS , DECV, ,   4, xosdskMsgCBlocks## , xosdskGtU4Byte## , 0                 , ucb_cblocks
 CHARENT  IBLKSZ  , DECV, ,   2, xosdskMsgIBlkSz##  , xosdskGtU4Byte## , setiblksz         , ucb_isecsz
 CHARENT  IHEADS  , DECV, ,   1, xosdskMsgIHeads##  , xosdskGtU1Byte## , setsizeiparm      , ucb_iheads
 CHARENT  ISECTS  , DECV, ,   4, xosdskMsgISects##  , xosdskGtU4Byte## , setsizeiparm      , ucb_isects
 CHARENT  ICYLNS  , DECV, ,   4, xosdskMsgICylns##  , xosdskGtU4Byte## , setsizeiparm      , ucb_icylns
 CHARENT  IBLOCKS , DECV, ,   4, xosdskMsgIBlocks## , xosdskGtU4Byte## , 0                 , ucb_iblocks
 CHARENT  XGAPLEN , DECV, ,   2, msgxgpl            , xosdskGtU2Byte## , xosdskStU2Byte##  , ucb_fdxgpl
 CHARENT  SRTIME  , DECV, ,   1, msgsrt             , xosdskGtU1Byte## , setsrt            , ucb_fdsrt
 CHARENT  HUTIME  , DECV, ,   2, msghut             , xosdskGtU2Byte## , sethut            , ucb_fdhut
 CHARENT  HLTIME  , DECV, ,   2, msghlt             , xosdskGtU2Byte## , sethlt            , ucb_fdhlt
 CHARENT  MSTIME  , DECV, ,   4, msgmst             , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_fdmst
 CHARENT  MOTIME  , DECV, ,   1, msgmot             , xosdskGtU1Byte## , setmot            , ucb_fdmot
 CHARENT  WTMAX   , DECV, ,   4, knlDcMsgWTMax##    , xosdskGtU4Byte## , xosdskSetWTMax##  , ucb_wtmax
 CHARENT  RAMAX   , DECV, ,   4, knlDcMsgRAMax##    , xosdskGtU4Byte## , xosdskSetRAMax##  , ucb_ramax
 CHARENT  DTHLIMIT, DECV, ,   4, knlDcMsgDTHLimit## , xosdskGtU4Byte## , 0                 , ucb_dthlimit
 CHARENT  BLOCKIN , DECV, ,   4, xosdskMsgBlockIn## , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_blockin
 CHARENT  BLOCKOUT, DECV, ,   4, xosdskMsgBlockOut##, xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_blockout
 CHARENT  BYTEIN  , DECV, ,   4, knlDcMsgByteIn##   , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_bytein
 CHARENT  BYTEOUT , DECV, ,   4, knlDcMsgByteOut##  , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_byteout
 CHARENT  TDEVERR , DECV, ,   4, knlDcMsgTDevErr##  , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_tdeverr
 CHARENT  HDEVERR , DECV, ,   4, knlDcMsgHDevErr##  , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_hdeverr
 CHARENT  TDATAERR, DECV, ,   4, knlDcMsgTDataErr## , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_tdataerr
 CHARENT  HDATAERR, DECV, ,   4, knlDcMsgHDataErr## , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_hdataerr
 CHARENT  TSEEKERR, DECV, ,   4, xosdskMsgTSeekErr##, xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_tseekerr
 CHARENT  HSEEKERR, DECV, ,   4, xosdskMsgHSeekErr##, xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_hseekerr
 CHARENT  TIDFERR , DECV, ,   4, xosdskMsgTIdFErr## , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_tidferr
 CHARENT  HIDFERR , DECV, ,   4, xosdskMsgHIdFErr## , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_hidferr
 CHARENT  TRNFERR , DECV, ,   4, xosdskMsgTRNFErr## , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_trnferr
 CHARENT  HRNFERR , DECV, ,   4, xosdskMsgHRNFErr## , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_hrnferr
 CHARENT  TOVRNERR, DECV, ,   4, knlDcMsgTOvrnErr## , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_tovrnerr
 CHARENT  HOVRNERR, DECV, ,   4, knlDcMsgHOvrnErr## , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_hovrnerr
 CHARENT  HUNGERR , DECV, ,   4, knlDcMsgHungErr##  , xosdskGtU4Byte## , xosdskStU4Byte##  , ucb_hungerr
 CHARENT  DISKID  , HEXV, ,   4, xosdskMsgDiskID##  , xosdskGtU4Byte## , 0                 , ucb_diskid
 CHARENT  PRTNTYPE, DECV, ,   1, xosdskMsgPrtnType##, xosdskGtU1Byte## , 0                 , ucb_prtntype
 CHARENT  FSTYPE  , TEXT, ,   8, xosdskMsgFSType##  , xosdskGtU8Byte## , 0                 , ucb_fsname
 CHARENT  CLSSZ   , DECV, ,   4, xosdskMsgClsSz##   , xosdskGtU4Byte## , 0                 , ucb_clussize
 CHARENT  CLUSTERS, DECV, ,   4, xosdskMsgClsters## , xosdskGtU4Byte## , 0                 , ucb_total
 CHARENT  AVAIL   , DECV, ,   4, xosdskMsgAvail##   , xosdskGtU4Byte## , 0                 , ucb_avail
 CHARENT  FATMODE , HEXV, ,   1, xosdskMsgFatMode## , xosdskGtU1Byte## , 0                 , ucb_fatmode
 CHARENT  DUPFAT  , TEXT, ,   4, xosdskMsgDupFat##  , xosdskGetBits##  , xosdskSetBits##   , UB$DUPFAT
 CHARENT  DEFER   , TEXT, ,   4, xosdskMsgDupFat##  , xosdskGetBits##  , xosdskSetBits##   , UB$DEFER
 CHARENT  FATSIZE , DECV, ,   4, xosdskMsgFatSize## , xosdskGtU4Byte## , 0                 , ucb_satsize
 CHARENT  NUMFATS , DECV, ,   4, xosdskMsgNumFats## , xosdskGtU4Byte## , 0                 , ucb_numsat
 CHARENT  ROOTBLK , DECV, ,   4, xosdskMsgRootBlk## , xosdskGtU4Byte## , 0                 , ucb_rootblk
 CHARENT  ROOTSIZE, DECV, ,   4, xosdskMsgRootSize##, xosdskGtU4Byte## , 0                 , ucb_rootsize
 CHAREND

msgxgpl:CHARINFO  {Gap length for transfers}
msgsrt: CHARINFO  {Step rate timing in milliseconds}
msghut: CHARINFO  {Head unload time in milliseconds}
msghlt: CHARINFO  {Head load time in milliseconds}
msgmst: CHARINFO  {Motor start time in milliseconds}
msgmot: CHARINFO  {Motor off time in seconds}
.PAGE
	.SBTTL	Initialization

	INITSUB	initfdka

	ONCE

initfdka::
	MOVL	EBX, #'FDKA'		;Get disk driver name
	MOVL	EDX, #fdkdsp		;Get offset of dispatch table
	CALL	xosdskNewDriver##	;Register this disk driver
	JC	6$			;If error
	MOVL	EBX, lkei_pctop-4[ESP]	;OK
	MOVL	[EBX], #codetop
	CLRL	EAX
6$:	RET	lkei_ADJ

	CODE
.PAGE
	.SBTTL	fdkaddunit - Subroutine to add disk unit

;Subroutine to add disk unit
;	c{EAX} = Unit number
;	c{EBX} = Address of KCB for controller (0 if none)
;	c{ECX} = Index on controller
;	c{EDX} = Address of characteristics list
;	c{ESI} = Base IO register
;	CALL	fdkaddunit
;	c{EAX} = Returned value

$$$=!0
FRM fau_char , 8	;Address of characteristics list
FRM fau_ioreg, 4	;Base IO register number
FRM fau_index, 4	;Index on controller
FRM fau_bits , 1	;Value for ubc_bits
FRM fau_type , 1
FRM fau_unit , 1	;Unit number
FRM fau_info , 1	;Information about drive
FRM fau_cdsp , 1	;Controller description
FRM fau_srt  , 1	;Step rate time value
FRM fau_xgpl , 1	;Transfer gap length
FRM          , 1
FRM fau_head , 4	;Heads per track (1 or 2)
FRM fau_ssize, 4	;Default sector size (bytes)
FRM fau_tsize, 4	;Default track size (sectors)
FRM fau_cyln , 4	;Default number of cylinders
FRM fau_kcb  , 4	;Offset of KCB
fau_SIZE=!$$$

;Define bits for fau_info

INFO$NEWOAS=!0x02	;New once-a-second routine set up
INFO$NEWKCB=!0x01	;New KCB created

	.MOD	4
fdkaublk:				;Characteristics description block
 CHARBGN  3, knlDCharValCom##		;  for addunit
 CHARENT  TYPE    , TEXT, , 8, 0, 0, knlRtnZero##, 0
 CHARENT  UNIT    , DECV, , 4, 0, 0, knlRtnZero##, 0
 CHARENT  CONDESP , TEXT, , 8, 0, 0, fdkaucdsp   , 0
 CHARENT  IOREG   , HEXV, , 4, 0, 0, knlRtnZero##, 0
 CHARENT  INDEX   , DECV, , 4, 0, 0, fdkauindex  , 0
 CHARENT  UNITTYPE, TEXT, , 8, 0, 0, fdkauutyp   , 0
 CHAREND

;Values for the CONDESP parameter

cdspval:.LONG	'PCAT', 0, FCT_PCAT
	.LONG	'CMPT', 0, FCT_CMPT
CDSPVALSZ=!{$-cdspval}/12t

;Values for the UNITTYPE parameter

utypval:.LONG	'HD3', 0, {DT_HD3<8}|DD_HIGH|UB$MEDIAC
	.LONG	'DD3', 0, {DT_DD3<8}|DD_DOUBLE|UB$MEDIAC
	.LONG	'HD5', 0, {DT_HD5<8}|DD_HIGH|UB$MEDIAC
	.LONG	'DD5', 0, {DT_DD5<8}|DD_DOUBLE|UB$48TPI
	.LONG	'DD8', 0, {DT_DD8<8}|DD_DOUBLE
UTYPVALSZ=!{$-utypval}/12t

;Values for the DDEN parameter

ddenval:.LONG	'S'   , 0   , DD_SINGLE
	.LONG	'SING', 'LE', DD_SINGLE
	.LONG	'D'   , 0   , DD_DOUBLE
	.LONG	'DOUB', 'LE', DD_DOUBLE
	.LONG	'H'   , 0   , DD_HIGH
	.LONG	'HIGH', 0   , DD_HIGH
	.LONG	0
DDENVALSZ=!{$-ddenval}/12t

;Generate table which gives default values as a function of drive type

	.MACRO	XX  srt, nmcyl, xgpl, fgpl, nsec, nhds, secsz
	.BYTE	srt, nmcyl't, xgpl'h, fgpl'h, nsec't, nhds
	.WORD	secsz't
	.ENDM

;	   SRT NmCyl XGpl FGpl #Sec#Hds SecSz
dvaltbl:XX  6,  80,  1B,  60,   9,  2,  512 ;DT_DD3 = 1 - 3.5" double density
	XX  6,  80,  1B,  6C,  18,  2,  512 ;DT_HD3 = 2 - 3.5" high density
	XX  6,  40,  2A,  50,  15,  2,  512 ;DT_DD5 = 3 - 5.25" double density
	XX  3,  80,  2A,  54,   9,  2,  512 ;DT_HD5 = 4 - 5.25" high density
	XX  8,  77,  0F,  1B,  26,  1,  128 ;DT_DD8 = 5 - 8" double density

	CODE

2$:	LEAVE
	RET

fdkaddunit:
	ENTER	fau_SIZE, 0		;Allocate our stack frame
	MOVB	fau_unit[EBP], AL	;Store unit number
	MOVL	fau_kcb[EBP], EBX	;Store possible KCB offset
	MOVL	fau_ioreg[EBP], ESI	;Store base IO register
	MOVL	fau_char+0[EBP], EDX	;Store address of characteristis list
	MOVL	fau_char+4[EBP], FS
	MOVB	fau_info[EBP], #0
	MOVL	fau_index[EBP], #-1
	MOVB	fau_cdsp[EBP], #FCT_PCAT
	MOVL	EAX, #{DT_HD5<8}|DD_HIGH|UB$MEDIAC
	CALL	fdkauutyp2		;Set up default drive type
	MOVL	EBX, #fdkaublk		;Point to our characteristics table
	IFFAULT	knlQAdrErr0Lv##
	MOVL	EDX, fau_char[EBP]	;Point to caller's characteristics list
	MOVB	AL, #0			;Fail on bad names
	CALL	knlAddUnitChar##	;Process characteristics
	JC	10$			;If error
	CMPL	fau_index[EBP], #-1
	JE	14$
	MOVB	AL, #'F'		;OK - see if this unit is defined now
	MOVB	AH, fau_unit[EBP]
	CALL	xosdskSrchUnit##
	JE	15$			;Defined - fail
	CALL	knlGetXRes##		;Get the exec memory resource
	MOVL	EBX, fau_kcb[EBP]	;Is there a KCB now?
	TESTL	EBX, EBX
	JNE	28$			;Yes - go on
	MOVB	CL, #{ID_FDISK-ID_HBASE}/8 ;No - see if our vector is available
	CALL	knlChkIRqAvl##
	JC	8$.S
	MOVL	EDX, fau_ioreg[EBP]	;See if our IO registers are available
	LEAL	ECX, 5[EDX]
	CALL	knlChkIORegs##
	JC	8$.S
	MOVL	EDX, fau_ioreg[EBP]
	ADDL	EDX, #7
	MOVL	ECX, EDX
	CALL	knlChkIORegs##
	JC	8$
	CMPB	fau_cdsp[EBP], #FCT_CMPT ;Is this a ComptiCard?
	JNE	6$.S			;No
	MOVL	EDX, fau_ioreg[EBP]	;Yes - register its extra register
	ADDL	EDX, #P_DCFDCR2
	MOVL	ECX, EDX
	CALL	knlChkIORegs##
	JC	8$
6$:	MOVL	EDX, fau_ioreg[EBP]
	A2IOP	P_DCFDDOR		;Set reset
	MOVB	AL, #00h
	OUTB	[DX]
	IOPAUSE
	MOVL	ECX, #10t
	CALL	knlSDelay##
	MOVB	AL, #04h		;Remove reset
	OUTB	[DX]
	IOPAUSE
	MOVL	ECX, #50t		;Wait for a while
	CALL	knlSDelay##
	A2IOP	P_DCFDSTS-P_DCFDDOR	;Try to read status reg
	INB	[DX]
	IOPAUSE
	TESTB	AL, #7Fh		;Is it right?
	JE	22$.S			;Yes
	MOVL	EAX, #ER_PDNAV		;No - fail
8$:	CALL	knlGiveXRes##
10$:	LEAVE
	CLRL	ECX
	MOVL	EBX, #QSTS$DONE
	TOFORK
	RET

;;;;;;;;;;;;;; FIX THIS ^^^


14$:	MOVL	EAX, #ER_CHARM
	JMP	10$.S

15$:	MOVL	EAX, #ER_DUADF
	JMP	10$.S

;Here if error after once-a-second routine is set up

16$:	TESTB	fau_info[EBP], #INFO$NEWOAS
	JE	18$.S
	MOVL	EDX, #fdkoas
	CALL	knlUnlinkOAS##

;Here if error after KCB is set up

18$:	TESTB	fau_info[EBP], #INFO$NEWKCB
	JE	22$
20$:	PUSHL	EDI
	PUSHL	#fdkakcb
	CALL	xosdskUnlinkKcb##
	PUSHL	EDI
	CALL	sysMemGiveXmb##
	JMP	8$

;Here if controller exists

22$:	PUSHL	#0			;Make a KCB
	MOVL	EAX, ESP
	PUSHL	#KB$AHEAD+KB$DEFER
	PUSHL	#kcb_fdSIZE
	PUSHL	#fdkdsp
	PUSHL	#'FDKA'
	PUSHL	#fdkakcb
	PUSHL	EAX
	CALL	xosdskMakeKcb##
	POPL	EDI
	TESTL	EAX, EAX
	JS	8$
	MOVL	fau_kcb[EBP], EDI	;Remember where our KCB is
	ORB	fau_info[EBP], #INFO$NEWKCB
	MOVL	kcb_fdisp[EDI], #fdkfork
	MOVL	EAX, fau_ioreg[EBP]	;Store base IO register number in the
	MOVL	kcb_ioreg1[EDI], EAX	;  KCB
	MOVB	AL, fau_cdsp[EBP]	;Store floppy controller type in the
	MOVB	kcb_fdtype[EDI], AL	;  KCB
	CMPL	fdkakcb, #0		;Is this the first floppy KCB?
	JNE	24$.S			;No
	MOVL	EBX, #fdkoas		;Yes - setup our once-a-second routine
	PUSHL	EDI
	CALL	knlSetupOAS##
	POPL	EDI
	JC	20$.S			;If error
	ORB	fau_info[EBP], #INFO$NEWOAS
24$:	MOVL	EBX, EDI
	MOVB	kcb_fdtrkd[EBX], #0xFF	;Force data rate to be set first time
	MOVB	kcb_fdmoto[EBX], #0x0C
	TESTB	knlMachType##+3, #0x02	;Is this a PS/2?
	JE	26$			;No
	MOVB	kcb_fdmoto[EBX], #0x04	;Yes - use correct value
26$:	MOVL	kcb_dmachnl[EBX], #2	;Store DMA channel
	PUSHL	EBX
	MOVL	EBX, #hwname+8		;Construct the hardware name
	MOVL	EAX, fau_ioreg[EBP]
	CALL	knlPutHex4Nmbr##
	PUSHL	#hwname			;Set up our vector
	PUSHL	#{ID_FDISK-ID_HBASE}/8
	PUSHL	#DT_TRAP
	PUSHL	#fdkint
	PUSHL	#0
	CALL	sysIoSetInt##
	TESTL	EAX, EAX
	JS	275$
	PUSHL	#hwname
	MOVL	EDX, fau_ioreg[EBP]
	PUSHL	EDX
	ADDL	EDX, #5
	PUSHL	EDX
	CALL	sysIoRegPorts##
	TESTL	EAX, EAX
	JS	275$
	PUSHL	#hwname
	MOVL	EDX, fau_ioreg[EBP]
	ADDL	EDX, #7
	PUSHL	EDX
	PUSHL	EDX
	CALL	sysIoRegPorts##
	TESTL	EAX, EAX
	JS	275$
	CMPB	fau_cdsp[EBP], #FCT_CMPT ;Is this a ComptiCard?
	JNE	273$			;No
	PUSHL	#hwname
	MOVL	EDX, fau_ioreg[EBP]	;Yes - register its extra register
	ADDL	EDX, #P_DCFDCR2
	PUSHL	EDX
	PUSHL	EDX
273$:	CALL	sysIoRegPorts##



275$:	POPL	EBX
	JC	8$

;;;;;;;;;;;;;; FIX THIS ^^^

;Here with KCB setup or if already have a KCB

28$:	PUSHL	#'F'			;First letter for the device name
	MOVZBL	EDX, fau_unit[EBP]	;Unit number
	PUSHL	EDX
	PUSHL	EBX			;Address of the KCB
	PUSHL	#ucb_fdSIZE		;Size of the UCB
	PUSHL	EAX
	CALL	xosdskMakeUcb##		;Make a UCB
	POPL	ESI
	TESTL	EAX, EAX
	JS	16$			;If error

	PUSHL	ESI
	PUSHL	#DHTS_256		;Hash table size index
	CALL	xosdskMakeHashTbl##	;Create our minimum hash table
	TESTL	EAX, EAX
	JS	40$
	CALL	knlGiveXRes##		;Give up the exec memory resource
	ORL	ucb_dsp[ESI], #DS$FIXED	;This is a fixed controller
	MOVL	ucb_devchar[ESI], #fdkdchartbl ;Store offset of our device
					       ;  characteristics table
	MOVL	ucb_ramax[ESI], #16t	;Store initial read-ahead limit (note
					;  that there is no hardware limit for
					;  floppies)
	MOVL	ucb_wtmax[ESI], #16t	;Also store the initial write transfer
	MOVB	AL, fau_bits[EBP]	;  maximum
	ORB	AL, #UB$DUPFAT
	MOVB	ucb_bits[ESI], AL	;Store value for ucb_bits
	ORL	ucb_dsp[ESI], #DS$REMOVE ;All floppies are removeable
	MOVB	AL, fau_type[EBP]
	MOVB	ucb_unittype[ESI], AL
	CALL	setmdfq			;Set the mode and frequency values
	MOVB	AL, fau_head[EBP]	;Store number of heads
	MOVB	ucb_cheads[ESI], AL
	MOVB	ucb_iheads[ESI], AL
	MOVL	EAX, fau_tsize[EBP]	;Store initial number of sectors/track
	MOVL	ucb_csects[ESI], EAX
	MOVL	ucb_isects[ESI], EAX
	MOVL	EAX, fau_ssize[EBP]	;Store sector size
	MOVL	ucb_csecsz[ESI], EAX
	MOVL	ucb_isecsz[ESI], EAX
	BSFL	EBX, EAX		;Get power of 2
	SUBL	EBX, #7		;Minus 7 gives length code
	CMPB	BL, #6			;Make sure legal value
	JBE	32$.S
	MOVB	BL, #0
32$:	MOVB	ucb_fdsecs[ESI], BL	;Store for controller
	CALL	setnumsec		;Store active number of sector/track
	MOVL	EAX, fau_cyln[EBP]	;Store number of cylinders
	MOVL	ucb_ccylns[ESI], EAX
	MOVL	ucb_icylns[ESI], EAX
	IMULL	EAX, ucb_isects[ESI]	;Store total number of blocks
	IMULL	EAX, ucb_iheads[ESI]
	MOVL	ucb_cblocks[ESI], EAX
	MOVL	ucb_iblocks[ESI], EAX
	MOVZBL	EAX, fau_xgpl[EBP]	;Store transfer gap length value
	MOVW	ucb_fdxgpl[ESI], AX
	MOVB	AL, fau_srt[EBP]	;Store step rate time value
	MOVB	ucb_fdsrt[ESI], AL
	MOVW	ucb_fdhut[ESI], #240t	;Store head unload timer value
	MOVW	ucb_fdhlt[ESI], #1	;Store head load time value
	MOVB	ucb_fdmot[ESI], #4	;Store motor off time value
	MOVL	ucb_fdmst[ESI], #1000t	;Store motor start timer value
	CALL	setspcfy		;Update specify command value
	MOVL	EAX, fau_index[EBP]	;Store unit index and select values
	MOVB	ucb_index[ESI], AL
	DECL	EAX
	MOVB	ucb_select[ESI], AL
	MOVB	AL, motoron[EAX]	;Store motor on bit
	MOVB	ucb_fdmoto[ESI], AL
	MOVL	EAX, fau_ioreg[EBP]	;Store base IO register number in the
	MOVL	ucb_ioreg1[ESI], EAX	;  UCB
	MOVB	ucb_sts1[ESI], #U1$RECAL ;Indicate should recalibrate first
	TESTB	fau_info[EBP], #INFO$NEWKCB ;Did we just make a KCB?
	JNE	initcon			;Yes - go on
34$:	TOFORK				;No - finished now
initfin:LEAVE
	CLRL	EAX
	CLRL	ECX
	INCL	ECX
	MOVL	EBX, #QSTS$DONE
	RET

;Here if could not create our hash table

40$:	CRASH	CMHT
.PAGE
;Here if have just created a KCB - now we must initialize the controller

initcon:PUSHL	#0			;Get a disk DCB
	PUSHL	#0
	PUSHL	#'INIT'
	PUSHL	#'FDKA'
	MOVL	EDX, ESP
	PUSHL	#0
	MOVL	EAX, ESP
	PUSHL	EDX
	PUSHL	ESI
	PUSHL	EAX
	CALL	xosdskGetDcb##
	POPL	EDI
	ADDL	ESP, #16t
	TESTL	EAX, EAX
	JS	6$			;If can't get a DCB
	MOVL	EAX, knlTda+tdaAddr##
	MOVL	dcb_outtda[EDI], EAX	;OK - store TDA address
	MOVL	knlTda+tdaDcb##, EDI
	MOVL	EBX, ucb_kcb[ESI]
	MOVB	kcb_itimer[EBX], #3t	;Start interrupt timer
	CLRL	EAX			;Just to be safe, clear some vectors
	MOVL	dcb_sdisp[EDI], EAX
	PUSHL	#kf_init		;Do initialization
	PUSHL	#0
	PUSHL	#0
	CALL	xosdskTransfer##
	TESTL	EAX, EAX
	JNS	4$			;If OK
	PUSHL	EAX			;If error
	CALL	xosdskClearDcb##
	INB	P_INC1P1		;Disable our interrupt
	IOPAUSE
	ORB	AL, #0x40
	OUTB	P_INC1P1
	IOPAUSE
	MOVL	EBX, ucb_kcb[ESI]
	MOVB	AL, kcb_fdmoto[EBX]
	MOVL	EDX, kcb_ioreg1[EBX]
	A2IOP	P_DCFDDOR
	OUTB	[DX]
	IOPAUSE
	ORB	kcb_bits[EBX], #KB$ERROR ;Indicate controller not usable
	CLRL	EAX			;Clear UCB pointer in the KCB to make
	MOVL	kcb_fucb[EBX], EAX	;  floppies unusable!
	MOVL	knlTda+tdaDcb##, EAX
	POPL	EAX			;Restore error code
	JMP	6$

4$:	CALL	xosdskClearDcb##	;Give up the DCB we were using
	CLRL	EAX
	MOVL	knlTda+tdaDcb##, EAX
	INCL	EAX
	RET

;Here if error creating a disk DCB

6$:	ADDL	ESP, #16t
	STC
	RET
.PAGE
;Subroutine called by adduparms for the INDEX parameter

fdkauindex:
	TESTL	EAX, EAX		;Valid index on controller?
	JE	knlBadParmV##		;No
	CMPL	EAX, #4			;Maybe
	JA	knlBadParmV##		;No
	MOVL	fau_index[EBP], EAX	;Yes - store index
	CLC
	RET

;Subroutine called by adduparms for the "CONDESP" parameter

fdkaucdsp:
	MOVL	EBX, #cdspval
	MOVL	ECX, #CDSPVALSZ
	CALL	knlGetDcVal2##
	JC	4$
	MOVB	fau_cdsp[EBP], AL
4$:	RET

;Subroutine called by adduparms for the "UNITTYPE" parameter

fdkauutyp:
	MOVL	EBX, #utypval
	MOVL	ECX, #UTYPVALSZ
	CALL	knlGetDcVal2#
	JC	4$.S
fdkauutyp2:
	MOVB	fau_bits[EBP], AL
	MOVB	fau_type[EBP], AH
	MOVZBL	EAX, AH
	MOVZBL	EDX, dvaltbl-8+0[EAX*8]	;Get SRT value
	MOVB	fau_srt[EBP], DL
	MOVB	DL, dvaltbl-8+1[EAX*8]	;Get number of cylinders value
	MOVL	fau_cyln[EBP], EDX
	MOVB	DL, dvaltbl-8+2[EAX*8]	;Get transfer gap length value
	MOVB	fau_xgpl[EBP], DL
	MOVB	DL, dvaltbl-8+4[EAX*8]	;Get number of sectors value
	MOVL	fau_tsize[EBP], EDX
	MOVB	DL, dvaltbl-8+5[EAX*8]	;Get number of heads value
	MOVL	fau_head[EBP], EDX
	MOVW	DX, dvaltbl-8+6[EAX*8]	;Get sector size value
	MOVL	fau_ssize[EBP], EDX
	RET
.PAGE
	.SBTTL	Subroutines for QFNC_DEVCHAR functions

;Here to get the value of the CONDESP characteristic

getcondesp:
	MOVL	ESI, dcb_dkucb[EDI]
	MOVL	EBX, ucb_kcb[ESI]
	MOVL	EAX, #'PCAT'		;Assume standard floppy controller
	CMPB	kcb_fdtype[EBX], #FCT_CMPT ;Is this a ComptiCard?
	JNE	2$.S			;No
	MOVL	EAX, #'CMPT'
2$:	CLRL	EDX
	RET

;Here to get the value of the TRKDEN characteristic - for 3.5" disks, track
;  density is always 135tpi - for 5.25" disks, track density can be either
;  48tpi or 96tpi - for 8" disks, track density is always 48tpi

gettrkden:
	MOVL	ESI, dcb_dkucb[EDI]
	CLRL	EDX
	MOVB	CL, ucb_unittype[ESI]	;Get disk type
	MOVL	EAX, #48t		;Assume 48tpi disk
	CMPB	CL, #DT_DD8		;Is it an 8" disk?
	JE	6$.S			;Yes - its 48tpi
	CMPB	CL, #DT_DD5		;Is it a DD 5.25" disk?
	JE	6$.S			;Yes - this is also 48tpi
	JB	8$.S			;If 3.5" disk, its 135tpi
	TESTB	ucb_bits[ESI], #UB$48TPI ;Must be HD 5.25" disk - is it at
					 ;  48tpi now?
	JNE	6$.S			;Yes
4$:	MOVB	AL, #96t		;No - its 96tpi
6$:	RET

;Here if have 3.5" disk - its 135tpi

8$:	ADDL	EAX, #135t-48t
ret004:	RET

;Here to get the value of the DATADEN characteristic

getdataden:
	MOVL	ESI, dcb_dkucb[EDI]
	MOVB	DL, ucb_bits[ESI]	;Get density value
	ANDL	EDX, #UB$DENSITY
	MOVL	EAX, denname+0[EDX]
	MOVL	EDX, denname+4[EDX]
	RET

	.MOD	4
denname:.LONG	'SING','LE'	;DT_SINGLE = 0t<3 - Single density
	.LONG	'DOUB','LE'	;DT_DOUBLE = 1t<3 - Double density
	.LONG	'HIGH',''	;DT_HIGH   = 2t<3 - High density
.PAGE
;Here to set the value of the UNITTYPE characteristic

setunittype:
	MOVL	EBX, #utypval
	MOVL	ECX, #UTYPVALSZ
	CALL	knlGetDcVal2##
	JC	ret004.S
	MOVL	ESI, dcb_dkucb[EDI]
	ORB	ucb_bits[ESI], AL
	MOVB	ucb_unittype[ESI], AH
	MOVZBL	EAX, AH
	MOVZBL	EDX, dvaltbl-8+0[EAX*8]	;Get SRT value
	MOVZBL	EDX, dvaltbl-8+0[EAX*8]	;Get SRT value
	MOVB	ucb_fdsrt[ESI], DL
	MOVB	DL, dvaltbl-8+1[EAX*8]	;Get number of cylinders value
	MOVL	ucb_ccylns[ESI], EDX
	MOVL	ucb_icylns[ESI], EDX
	MOVB	DL, dvaltbl-8+2[EAX*8]	;Get transfer gap length value
	MOVB	ucb_fdxgpl[ESI], DL
	MOVB	DL, dvaltbl-8+4[EAX*8]	;Get number of sectors value
	MOVL	ucb_csecsz[ESI], EDX
	MOVL	ucb_isecsz[ESI], EDX
	MOVB	DL, dvaltbl-8+5[EAX*8]	;Get number of heads value
	MOVL	ucb_cheads[ESI], EDX
	MOVL	ucb_iheads[ESI], EDX
	IMULL	EDX, ucb_csects[ESI]	;Get total number of blocks value
	IMULL	EDX, ucb_ccylns[ESI]
	MOVL	ucb_cblocks[ESI], EAX
	MOVL	ucb_iblocks[ESI], EAX
	MOVW	DX, dvaltbl-8+6[EAX*8]	;Get sector size value
	MOVL	ucb_csecsz[ESI], EDX
	MOVL	ucb_isecsz[ESI], EDX
	MOVL	ucb_fdmst[ESI], #1000t	;Get motor start timer value
	MOVB	ucb_fdmot[ESI], #4	;Get motor off timer value
	MOVL	ucb_fdhut[ESI], #240t	;Get head unload timer value
	MOVL	ucb_fdhlt[ESI], #1	;Get head load timer value
	CALL	setmdfq			;This should never fail here since we
	CLC				;  are using standard values!
	RET

;Here to set the value of the TRKDEN characteristic

settrkden:
	MOVL	ESI, dcb_dkucb[EDI]
	CMPL	EAX, #48t		;Want 48tpi?
	JA	6$.S			;No
	ORB	ucb_bits[EBP], #UB$48TPI ;Yes
	RET

6$:	ANDB	ucb_bits[EBP], #~UB$48TPI
	RET

;Here to set the value of the DATADEN characteristic

setdataden:
	MOVL	EBX, #ddenval
	MOVL	ECX, #DDENVALSZ
	CALL	knlGetDcVal2##
	JC	10$.S
	MOVL	ESI, dcb_dkucb[EDI]
	CMPB	AL, #DD_HIGH		;Want high density?
	JNE	8$.S			;No
	TESTB	ucb_unittype[ESI], #01h ;Yes - have high density drive?
	JNE	14$.S			;No - fail
8$:	ANDB	ucb_bits[ESI], #~UB$DENSITY ;OK - store new density value
	ORB	ucb_bits[ESI], AL
10$:	RET

;Here to set the value of the CBLKSZ characteristic

setcblksz:
	BSFL	ECX, EAX		;Find power of 2
12$:	JE	knlBadParmV##
	SUBL	ECX, #7
	CMPB	CL, #6
	JA	14$.S
	MOVL	EDX, #128t
	SHLL	EDX, CL
	CMPL	EDX, EAX		;Is it a power of 2?
14$:	JNE	knlBadParmV##		;No - fail
	MOVL	ESI, dcb_dkucb[EDI]	;Yes
	MOVL	ucb_csecsz[ESI], EAX	;Store it
	MOVB	ucb_fdsecs[ESI], CL	;Also store index
	CALL	setnumsec		;Store active number of sector/track
	CLC
	RET

;Here to set the value of the DBLKSZ characteristic

setiblksz:
	BSFL	ECX, EAX		;Find power of 2
	JE	12$.S
	SUBL	ECX, #7
	JL	14$.S
	MOVL	EDX, #128t
	SHLL	EDX, CL
	CMPL	EDX, EAX		;Is it a power of 2?
	JNE	14$.S			;No - fail
	MOVL	ESI, dcb_dkucb[EDI]	;Yes
	MOVL	ucb_isecsz[ESI], EAX	;Store it
	RET

;Here to set value of the ISECTS, IHEADS, or ICYLNS characteristic

setsizeiparm:
	MOVL	ESI, dcb_dkucb[EDI]
	MOVL	[ESI+EBX], EAX		;Update value
	MOVL	EAX, ucb_iheads[ESI]	;Update total blocks value
	IMULL	EAX, ucb_isects[ESI]
	IMULL	EAX, ucb_icylns[ESI]
	MOVL	ucb_iblocks[ESI], EAX
	CLC
	RET

;Here to set value of the CSECTS, CHEADS, or CCYLNS characteristic

setsizecparm:
	MOVL	ESI, dcb_dkucb[EDI]
	MOVL	[ESI+EBX], EAX		;Update value
	MOVL	EAX, ucb_cheads[ESI]	;Update total blocks value
	IMULL	EAX, ucb_csects[ESI]
	IMULL	EAX, ucb_ccylns[ESI]
	MOVL	ucb_cblocks[ESI], EAX
	CLC
	RET

;Here to set the value of the SRTIME characteristic

setsrt:	CMPL	EAX, #16t
	JB	16$.S
	MOVB	AL, #16t
16$:	TESTL	EAX, EAX
	JNE	18$.S
	INCL	EAX
18$:	MOVL	ESI, dcb_dkucb[EDI]
	MOVB	ucb_fdsrt[ESI], AL
	RET

;Here to set the value of the HUTIME characteristic

sethut:	CMPL	EAX, #240t
	JB	20$.S
	MOVL	EAX, #240t
20$:	ADDL	EAX, #15t
	SHRL	EAX, #4
	JNE	22$.S
	INCL	EAX
22$:	MOVL	ESI, dcb_dkucb[EDI]
	MOVB	ucb_fdhut[ESI], AL
	CLC
	RET

;Here to set the value of the HLTIME characteristic

sethlt:	CMPL	EAX, #254t
	JB	24$.S
	MOVL	EAX, #254t
24$:	INCL	EAX
	SHRL	EAX, #1
	JNE	26$.S
	INCL	EAX
26$:	MOVL	ESI, dcb_dkucb[EDI]
	MOVB	ucb_fdhlt[ESI], AL
	CLC
	RET

;Here to set the value of the MOTIME characteristic

setmot:	CMPL	EAX, #255t		;Too big?
	JB	32$.S			;No
	MOVL	EAX, #255t		;Yes - just use maximum
32$:	TESTL	EAX, EAX		;Make sure not 0
	JNE	34$.S
	INCL	EAX
34$:	MOVL	ESI, dcb_dkucb[EDI]
	MOVB	ucb_fdmot[ESI], AL
	RET
.PAGE
	.SBTTL	setmdfq - Subroutine to set mode and frequency when density changed

;Subroutine to set modulation mode and frequency when data denity is changed
;	c{ESI} = Offset of UCB
;	CALL	setmdfq
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal

setmdfq:MOVB	BL, ucb_bits[ESI]
	ANDL	EBX, #UB$DENSITY
	ORB	BL, ucb_unittype[ESI]
	MOVB	AL, freqtbl[EBX]	;Get frequency value
	CMPB	AL, #0			;Is this combination legal?
	JS	knlBadParmV##		;No
	MOVB	ucb_fdtrkd[ESI], AL	;Yes - store in UCB
	MOVB	AL, #0			;Assume single density (FM)
	ANDB	BL, #UB$DENSITY		;Get just density value
	CMPB	BL, #DD_SINGLE		;Single density?
	JE	4$.S			;Yes
	MOVB	AL, #40h		;No - its double or high density (MFM)
4$:	MOVB	ucb_fdmode[ESI], AL	;Store in UCB
	ANDB	ucb_sts1[ESI], #~U1$HFTRK ;Assume not half track mode
	TESTB	ucb_bits[ESI], #UB$48TPI ;Want 48tpi?
	JE	6$.S			;No
	CMPB	ucb_unittype[ESI], #DT_HD5 ;Yes - is this a HD5 drive?
	JNE	6$.S			;No
	ORB	ucb_sts1[ESI], #U1$HFTRK ;Yes - indicate half track mode
6$:	CLC
	RET

X=!0FFh

;		   DD3 HD3 DD5 HD5 DD8
freqtbl:.BYTE	X,  2,  2,  2,  1,  0,  X,  X	;DD_SINGLE
	.BYTE	X,  2,  2,  2,  1,  0,  X,  X	;DD_DOUBLE
	.BYTE	X,  X,  0,  X,  0,  X,  X,  X	;DD_HIGH
	.BYTE	X,  X,  X,  X,  X,  X,  X,  X
.PAGE
	.SBTTL	setnumsec - Subroutine to determine number of sectors

;Subroutine to determine number of sectors per track, given the drive type
;  and the data density

setnumsec:
	MOVZBL	EAX, ucb_unittype[ESI]	;Get drive type
	MOVB	DL, ucb_bits[ESI]	;Get density
	ANDL	EDX, #UB$DENSITY
	DECL	EAX			;Calculate table index
	SHRL	EAX, #1
	IMULL	EAX, #3
	SHRL	EDX, #3
	ADDL	EDX, EAX
	MOVB	AL, ucb_fdsecs[ESI]	;Get sector size code
	MOVB	AL, numsectbl[EDX+EAX*8] ;Get number of sectors per track
	MOVL	ucb_csects[ESI], EAX	;Store it
	RET

numsectbl:
;		SD3  DD3  HD3  SD5  DD5  HD5  SD8  DD8	 Sector size
	.BYTE	16t, 30t, 60t, 16t, 30t, 56t, 26t, 48t	;128 bytes
	.BYTE	 9t, 16t, 32t,  9t, 16t, 30t, 15t, 26t	;256 bytes
	.BYTE	 4t,  9t, 18t,  4t,  9t, 15t,  8t, 15t	;512 bytes
	.BYTE	 2t,  4t,  8t,  2t,  4t,  7t,  4t,  8t	;1024 bytes
	.BYTE	 1t,  2t,  4t,  1t,  2t,  4t,  2t,  4t	;2048 bytes
	.BYTE	 0 ,  1t,  2t,  0 ,  1t,  2t,  1t,  2t	;4096 bytes
	.BYTE	 0 ,  0 ,  1t,  0 ,  0 ,  1t,  0 ,  1t	;8192 bytes
.PAGE
	.SBTTL	setgaplen - Subroutine to set gap lengths for current parameters

;Subroutine to set gap lengths for current parameters
;	CALL	setgaplen

setgaplen:
	RET

	.SBTTL	setspcfy - Subroutine to set values for specify command

;Subroutine to set values for the specify command
;	CALL	setspcfy

setspcfy:
	MOVZWL	EAX, ucb_fdhut[ESI]	;Get head unload time
	CALL	spcadj			;Adjust it for transfer rate
	ADDL	EAX, #31t		;Round up
	SHRL	EAX, #5t
	CMPL	EAX, #15t		;Too big?
	JB	2$.S			;No
	MOVB	AL, #15t		;Yes
2$:	MOVB	ucb_fdspcfy+0[ESI], AL	;Store in UCB
	MOVZBL	EAX, ucb_fdsrt[ESI]	;Get step rate time
	CALL	spcadj			;Adjust it for transfer rate
	INCL	EAX			;Round up
	SHRL	EAX, #1t
	JNE	4$.S			;Make sure not 0
	INCL	EAX
4$:	CMPL	EAX, #16t		;Make sure not too big
	JB	6$.S
	MOVB	AL, #16t
6$:	NEGB	AL			;1ms = 0Fh, 2ms = 0Eh, etc.
	ADDB	AL, #16t
	SHLB	AL, #4
	ORB	ucb_fdspcfy+0[ESI], AL	;Store in UCB
	MOVZWL	EAX, ucb_fdhlt[ESI]	;Get head load time
	CALL	spcadj			;Adjust it for transfer rate
	ADDL	EAX, #3			;Round up
	SHRL	EAX, #2
	JNE	8$.S			;Make sure not 0
	INCL	EAX
8$:	CMPL	EAX, #127t		;Make sure not too big
	JB	10$.S
	MOVB	AL, #127t
10$:	ADDL	EAX, EAX
	MOVB	ucb_fdspcfy+1[ESI], AL	;Store in UCB
	RET

;Subroutine to adjust value based on data rate
;	c{EAX} = Value to adjust
;	CALL	spcadj
;	c{EAX} = Adjusted value

spcadj:	CMPB	ucb_fdtrkd[ESI], #1	;Check density
	JA	12$.S			;If 250KHz (no adjustment needed)
	JE	14$.S			;If 300KHz
	ADDL	EAX, EAX		;500KHz, double value
12$:	RET

;Here if 300KHz

14$:	MULL	lit6			;Multiply by 6/5
	DIVL	lit5
	RET
.PAGE
	.SBTTL	vinbflpy - INB instruction for floppy disk from virtual DOS mode

vinbflpy::
	UNIMOP	0E4h, 00h, 00h, 00h

	.SBTTL	voutbflpy - OUTB instruction for floppy disk from virtual DOS mode

voutbflpy::
	UNIMOP	0E6h, 00h, 00h, 00h
.PAGE
	.SBTTL	ks_media - Subroutine to determine media type

;Subroutine to determine media type - must be called in extended fork context
;	c{EDI} = Address of DCB
;	CALL	fdkmedia
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  Z:set = Disk may be file structured
;	  Z:clr = Disk cannot be file structured

fdkmedia:
	PUSHL	#kf_chkwp		;Check write protect status
	PUSHL	#0
	PUSHL	#0
	CALL	xosdskTransfer##
	TESTL	EAX, EAX
	JS	2$			;If error
	TESTB	ucb_unittype[ESI], #01h ;OK - is this a high density drive?
	JNE	8$			;No - just use the current density
	TESTB	ucb_sts1[ESI], #U1$MOUNT ;Is the disk mounted now?
	JNE	4$			;Yes - use current density first
	ANDB	ucb_bits[ESI], #~{UB$DENSITY|UB$48TPI}
	ORB	ucb_bits[ESI], #DD_HIGH ;No - force high density
	CALL	setmdfq
2$:	JC	14$
	CALL	setnumsec
	CALL	setgaplen
	CALL	setspcfy
4$:	MOVL	dcb_dkdskblk+0[EDI], #1	;Read an ID field on track 0
	MOVL	dcb_dkdskblk+4[EDI], #0
	PUSHL	#kf_readid
	PUSHL	#0
	PUSHL	#0
	CALL	xosdskTransfer##
	TESTL	EAX, EAX
	JNS	10$			;If this worked
	CMPL	EAX, #ER_NTRDY		;Error - is it "not ready"?
	JE	12$			;Yes - report it (NEED TO CHANGE DEN)
	CMPL	EAX, #ER_ABORT		;Or abort?
	JE	12$			;Yes - report this too
	XORB	ucb_bits[ESI], #UB$DENSITY ;No - try other density
	ANDB	ucb_bits[ESI], #~UB$48TPI
	TESTB	ucb_bits[ESI], #DD_HIGH ;High density now?
	JNE	6$			;Yes
	CMPB	ucb_unittype[ESI], #DT_HD5 ;No - is this a 5 1/4" drive?
	JNE	6$			;No
	ORB	ucb_bits[ESI], #UB$48TPI ;Yes - indicate 48 tracks
6$:	CALL	setmdfq
	JC	14$
	CALL	setnumsec
	CALL	setgaplen
	CALL	setspcfy
8$:	MOVL	dcb_dkdskblk+0[EDI], #1	;Read an ID field on track 0
	MOVL	dcb_dkdskblk+4[EDI], #0
	PUSHL	#kf_readid
	PUSHL	#0
	PUSHL	#0
	CALL	xosdskTransfer##
	TESTL	EAX, EAX
	JS	14$			;If error
10$:	PUSHL	#0
	MOVL	EAX, ESP
	PUSHL	#-XMBX_512
	PUSHL	EAX
	CALL	sysMemGetXmb##		;Get an XMB
	POPL	EBX
	TESTL	EAX, EAX
	JNS	16$			;If OK
12$:	STC
14$:	RET

;Here with a buffer

16$:	CLRL	EAX
	MOVL	dcb_dkdskblk+0[EDI], EAX	;Try to read block 0
	MOVL	dcb_dkdskblk+4[EDI], EAX
	MOVL	bfr_nextxfr[EBX], EAX
	PUSHL	#kf_readlist		;Read a block
	PUSHL	EBX
	PUSHL	#512t			;Get amount to read
	CALL	xosdskTransfer##
	PUSHL	EAX
	PUSHL	EBX
	CALL	sysMemGiveXmb##
	POPL	EAX
	TESTL	EAX, EAX
	JS	20$			;If error
	ORB	ucb_sts1[ESI], #U1$VALID ;Indicate disk contains valid data
	CLRL	EAX			;Set Z, clear C
	RET				;Thats all

;Here if error reading block from disk

20$:

;CODE HERE TO SET DISK TO BE PREFERRED TYPE (HD)

	STC
	RET
.PAGE
	.SBTTL	ks_chkwp - Check write protect status at main program level

;Subroutine to check write protect status - must be called at main program level
;	c{EDI} = Address of DCB
;	CALL	fdkchkwp

fdkchkwp:
	PUSHL	#kf_chkwp
	PUSHL	#0
	PUSHL	#0
	CALL	xosdskTransfer##
	RET
.PAGE
;Function to reset drive parameters when disk is unmounted (called through
;  the ks_removebgn dispatch)
;	void fdkremovebgn
;	    UCB *ucb,
;	    long rmv);

fdrb_ucb=!8
fdrb_rmv=!4

fdkremovebgn:
	MOVL	EDX, fdrb_ucb[ESP]
	MOVL	EDX, ucb_kcb[EDX]
	MOVW	kcb_fdspcfy[EDX], #0	;Force specify command next time
	MOVB	kcb_fdtrkd[EDX], #0xFF	;Force setting density next time
	RET	8			;Thats all

;Function finish removing a disk (called through the ks_removefin dispatch)
;	void fdkremovefin
;	    UCB *ucb,
;	    long rmv);

fdrf_ucb=!8
fdrf_rmv=!4

fdkremovefin:
	RET	8			;Thats all
.PAGE
	.SBTTL	ks_chkchg - Subroutine to check for possible disk change

;Function to check for possible disk change (ks_chkchg dispatch).  If the
;  drive has a media sensor, it is used.  If it does not have a media sensor
;  and the motor is not running, a possible change is reported, if the motor
;  is running, not changed is reported.
;	long usbdskachkchg(void);
;  Value returned is:
;	<0 - Error, value is XOS error code
;	 0 - Disk not changed
;	 1 - Disk might be changed (no media sensor)

fdkchkchg:
	PUSHL	EDI
	PUSHL	ESI
	MOVL	EDI, knlTda+tdaDcb##
	MOVL	ESI, dcb_dkucb[EDI]

	TESTB	ucb_sts1[ESI], #U1$MOTON ;Is the motor on?
	JNE	2$			;Yes - say not changed
	TESTB	ucb_bits[ESI], #UB$MEDIAC ;No - does drive have a media
					  ;  sensor?
	JE	10$			;No
	MOVL	EDX, ucb_kcb[ESI]	;Yes
	TESTB	kcb_sts2[EDX], #K2$MSENSOR ;Are we using it now?
	JE	10$			;No
	PUSHL	#kf_chkchg		;Yes - must get the controller to check
	PUSHL	#0			;  the sensor state
	PUSHL	#0
	CALL	xosdskTransfer##
	TESTL	EAX, EAX
	JS	chgdone			;If error
2$:	CLRL	EAX			;OK - return 0 to say not changed
chgdone:POPL	ESI
	POPL	EDI
	RET

;Here to report that disk may have been changed if motor is off and do not
;  have a media sensor

10$:	MOVL	EAX, #1
	JMP	chgdone
.PAGE
	.SBTTL	kf_init - Initialize controller

;Here for the initialize controller function at fork level
;	c{ESI} = Offset of UCB
;	c{EDI} = Offset of DCB
;	CALL	fdkinit

fdkinit:ENTER	fdk_SIZE, 0
	MOVL	EBX, ucb_kcb[ESI]
	ORB	kcb_sts1[EBX], #K1$INT	;Indicate expecting interrupt now
	MOVB	dcb_dmachnl[EDI], #2	;Store DMA channel number
	MOVL	EDX, ucb_ioreg1[ESI]	;Point to the digital output register
	A2IOP	P_DCFDDOR
	MOVB	AL, #0			;Reset the floppy controller
	OUTB	[DX]
	IOPAUSE
	MOVL	EBX, ucb_kcb[ESI]	;Get the DMA channel (must get it here
	CALL	getcont			;  even though we don't use it since
	JC	8$.S			;  the controller enables DMA and
	MOVL	EDX, kcb_ioreg1[EBX]	;  interrupts together)
	A2IOP	P_DCFDDOR		;Make sure interrupts are enabled
	MOVB	AL, #0Ch		;  (getcont will not enable interrupts
	OUTB	[DX]			;  if running on a PS/2)
	IOPAUSE
	INB	P_INC1P1
	IOPAUSE
	ANDB	AL, #~40h
	OUTB	P_INC1P1
	IOPAUSE
	MOVL	EAX, #-1
	MOVL	EDX, EAX
	CALL	knlXfWait##
	JC	8$.S
	MOVL	ESI, dcb_dkucb[EDI]	;Get UCB offset
	CALL	floprslt
	JC	8$.S
	DECL	EBX			;Did we get at least one result byte?
	JS	8$.S			;No
	CMPB	ucb_fdrslt[ESI], #0C0	;Yes - is ST0 correct?
	JNE	8$.S			;No
	CLRL	EAX
	JMP	10$.S			;If normal

;Here if error

8$:	INCL	ucb_tdeverr[ESI]	;Count the error
	INCL	ucb_hdeverr[ESI]
	MOVL	EAX, #ER_DEVER
10$:	CLRL	ECX
	JMP	fdkdone
.PAGE
	.SBTTL	kf_readid - Read ID field

;Here for the read ID field function at fork level
;	c{EDI} = Offset of DCB
;	CALL	fdkreadid

fdkreadid:
	ENTER	fdk_SIZE, 0
	MOVB	fdk_mode[EBP], #0
	MOVL	EBX, ucb_kcb[ESI]
	CALL	getcont
	JC	1$
	MOVL	fdk_daddr[EBP], #1
	CALL	doseek			;Seek to right track
1$:	JC	fdkdone
	MOVB	fdk_retry1[EBP], #3	;Initialize retry count
2$:	MOVL	EBX, ucb_kcb[ESI]
	ORB	kcb_sts1[EBX], #K1$INT|K1$RESULT ;Indicate result phase next
	MOVB	AH, #0Ah		;Get read ID function
	ORB	AH, ucb_fdmode[ESI]
	CALL	flopcmdi
	JC	6$
	MOVB	AH, ucb_select[ESI]	;Head and unit select
	CALL	flopcmda
	JC	6$
	MOVL	EBX, ucb_kcb[ESI]
	MOVB	kcb_itimer[EBX], #3t	;Start interrupt timer
	MOVL	EAX, #-1
	MOVL	EDX, EAX
	CALL	knlXfWait##
	JC	6$
	CALL	knlDmaDisable##		;Disable our DMA channel
	ANDB	kcb_sts1[EBX], #~K1$DMAINUSE
	CALL	floprslt2		;Get remaining result bytes
6$:	JC	fdkdone
	CMPB	BL, #6			;Did we get 6 bytes?
	JNE	xfdverr			;No - fail
	MOVB	BL, ucb_fdrslt+0[ESI]	;Check ST0 result value
	CMPB	BL, ucb_select[ESI]
	JNE	10$.S			;If error
	MOVB	CL, ucb_fdrslt+6[ESI]	;OK - get sector size code
	CMPB	CL, #6			;Is it legal?
	JBE	8$			;Yes
	MOVB	CL, #0			;No - assume 128 bytes!
8$:	MOVB	ucb_fdsecs[ESI], CL	;Store it sector size code
	MOVL	EAX, #128t		;Convert to number of bytes
	SHLL	EAX, CL
	MOVL	ucb_csecsz[ESI], EAX	;And store that too
	CALL	setnumsec		;Update number of sectors per track
	CLRL	EAX			;  to match
	JMP	fdkdone

;Here if error

10$:	MOVL	EBX, ucb_kcb[ESI]	;Is this the read-ahead DCB?
12$:	MOVW	AX, ucb_fdrslt+1[ESI]	;Get error status bits
	CALL	xlaterror		;Translate the error indication
	DECB	fdk_retry1[EBP]		;No - have we done this enough?
	JNS	2$			;No - go try again
	JMP	xferr			;Yes - go fail
.PAGE
	.SBTTL	kf_format - Format track

;Here for the format track function
;	c{EBX} = Address of buffer
;	c{ECX} = Amount to transfer
;	c{ESI} = Offset of UCB
;	c{EDI} = Offset of DCB
;	CALL	fdkformat
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EAX} = 0

fdkformat:
	MOVB	AH, #0
	MOVL	EDX, dcb_dkdskblk+0[EDI]
	MOVB	AL, #0x0D		;Get format command
	JMP	fdkxfer			;Continue

	.SBTTL	kf_readlist - Read buffer list

;Here for the read buffer list function at fork level
;	c{EBX} = Address of buffer
;	c{ECX} = Amount to transfer
;	c{ESI} = Offset of UCB
;	c{EDI} = Offset of DCB
;	CALL	fdkreadlist
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EAX} = 0

fdkreadlist:
	MOVB	AH, #FM$LIST|FM$READ
	MOVL	ECX, #512t
	JMP	2$

	.SBTTL	kf_readdata - Read block

;Here for the read raw block function at fork level
;	c{EBX} = Address of buffer
;	c{ECX} = Amount to transfer
;	c{ESI} = Offset of UCB
;	c{EDI} = Offset of DCB
;	CALL	fdkreadraw
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EAX} = 0

fdkreadraw:
	MOVB	AH, #FM$READ
	MOVL	EDX, dcb_dkdskblk+0[EDI]
	JMP	4$

;Here for the read block function at fork level
;	c{EBX} = Address of buffer
;	c{ECX} = Amount to transfer
;	c{ESI} = Offset of UCB
;	c{EDI} = Offset of DCB
;	CALL	fdkreaddata
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EAX} = 0

fdkreaddata:
	MOVB	AH, #FM$READ
2$:	CALL	fdkbk2phy
4$:	MOVB	AL, #0x26		;Get read command
	JMP	fdkxfer			;Continue

	.SBTTL	kf_writlist - Write buffer list

;Here for the write buffer list function
;	c{EBX} = Address of buffer
;	c{ECX} = Amount to transfer
;	c{ESI} = Offset of UCB
;	c{EDI} = Offset of DCB
;	CALL	fdkwritlist
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EAX} = 0

fdkwritlist:
	MOVB	AH, #FM$LIST
	MOVL	ECX, #512t
	JMP	6$

	.SBTTL	kf_writraw - Write raw block

;Here for the write raw block function
;	c{EBX} = Address of buffer
;	c{ECX} = Amount to transfer
;	c{ESI} = Offset of UCB
;	c{EDI} = Offset of DCB
;	CALL	fdkwritraw
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EAX} = 0

fdkwritraw:
	MOVB	AH, #0
	MOVL	EDX, dcb_dkdskblk+0[EDI]
	JMP	8$

	.SBTTL	kf_writdata - Write block

;Here for the write block function
;	c{EBX} = Address of buffer
;	c{ECX} = Amount to transfer
;	c{ESI} = Offset of UCB
;	c{EDI} = Offset of DCB
;	CALL	fdkwritdata
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal
;	  c{EAX} = 0

fdkwritdata:
	MOVB	AH, #0
6$:	CALL	fdkbk2phy
8$:	MOVB	AL, #0x05		;Get write command
.PAGE
;Here when ready to start transfer
;	c(AL)  = Function for controller
;	c{EBX} = Address of buffer
;	c{ECX} = Amount to transfer
;	c{EDX} = Physical disk address
;	c{ESI} = Offset of UCB
;	c{EDI} = Offset of DCB

fdkxfer:ENTER	fdk_SIZE
	MOVL	fdk_func[EBP], EAX	;This also stores fdk_mode
	MOVL	fdk_buffer[EBP], EBX	;Store buffer address
	MOVL	fdk_count[EBP], ECX	;Store count
	MOVL	EAX, dcb_dkdskblk+0[EDI]	;Store disk block number
	MOVL	fdk_block[EBP], EAX
	CLRL	EAX			;Clear amount transfered
	MOVL	fdk_amount[EBP], EAX
	MOVL	fdk_daddr[EBP], EDX	;Store physical disk address
	MOVL	EBX, ucb_kcb[ESI]	;Get offset of the KCB
	TESTB	ucb_sts1[ESI], #U1$MOUNT ;Is the disk mounted now?
	JE	10$			;No - don't check for media change
	TESTB	ucb_sts1[ESI], #U1$MOTON ;Yes - is the motor on?
	JNE	10$			;Yes - assume not changed
	TESTB	ucb_bits[ESI], #UB$MEDIAC ;No - does drive have a media
					  ;  sensor?
	JE	2$			;No
	PUSHL	EBX			;Yes
	MOVL	EBX, ucb_kcb[ESI]
	TESTB	kcb_sts2[EBX], #K2$MSENSOR ;Are we using it now?
	JNE	4$			;Yes
2$:	BTL	dcb_dsp[EDI], #DS%FILE	;No - is this a physical disk?
	JNC	6$			;No - treat this as if its media sensor
					;  just went off!
	JMP	12$			;Yes - say not changed

;Here if we have a media sensor

4$:	CALL	fdkchkchgf		;See if the media sensor is set
	JNC	12$			;If its not set
	CMPL	EAX, #ER_MDCHG		;Error - was it "media changed"?
	JNE	8$			;No - fail
6$:	TESTB	dcb_sts2[EDI], #D2$CHKCHG ;Yes - are we checking for a changed
					  ;  disk now?
	JNE	12$			;Yes - don't be recusive!
	ORB	dcb_sts2[EDI], #D2$CHKCHG ;No - but we are now
	MOVL	EAX, dcb_sdisp[EDI]	;See if it is really changed
	CALLI	sd_vfychg[EAX]
	TESTL	EAX, EAX
	JE	10$			;If not changed
	ANDB	dcb_sts2[EDI], #~D2$CHKCHG
8$:	LEAVE				;Media changed - return the error
	STC
	RET

;Here if media was not changed

10$:	ANDB	dcb_sts2[EDI], #~D2$CHKCHG
12$:	CALL	getcont			;Get use of the controller
doxfer0:MOVB	fdk_retry3[EBP], #2	;Initialize restore retry count
doxfer2:MOVL	EBX, ucb_kcb[ESI]
	CALL	doseek			;Seek to right track
14$:	JC	fdkdone			;If error
	MOVB	fdk_retry1[EBP], #3	;Initialize transfer retry count
doxfer4:MOVL	ECX, fdk_count[EBP]	;Get transfer count

	CMPB	fdk_mode[EBP], #FM$LIST ;Transfering buffer list?
	JE	16$			;No
	MOVL	EBX, fdk_buffer[EBP]	;Yes
	MOVL	EBX, bfr_paddr[EBX]
	CALL	knlDmaSetPhy##
	JMP	18$

16$:	MOVL	EBX, fdk_buffer[EBP]	;Get virtual address for transfer
	MOVW	FS, xffPda##
	CALL	knlDmaSetVir##		;Set up DMA controller
18$:	MOVL	EBX, ucb_kcb[ESI]
	JC	14$			;If error
	TESTB	fdk_mode[EBP], #FM$READ ;Read operation?
	JNE	20$			;Yes
	INCL	ucb_blockout[ESI]	;No
	ADDL	ucb_byteout[ESI], ECX
	CALL	knlDmaOutput##		;start write
	JMP	22$

20$:	INCL	ucb_blockin[ESI]
	ADDL	ucb_bytein[ESI], ECX
	CALL	knlDmaInput##		;Start input
22$:	ORB	kcb_sts1[EBX], #K1$DMAINUSE
	MOVL	EBX, ucb_kcb[ESI]	;Restore offset of our KCB
	ORB	kcb_sts1[EBX], #K1$INT|K1$RESULT ;Indicate result phase next
	MOVB	AH, fdk_func[EBP]	;Get read, write, or format function
	ORB	AH, ucb_fdmode[ESI]
	CALL	flopcmdi
24$:	JC	14$
	MOVB	AH, ucb_select[ESI]	;Head and unit select
	CALL	flopcmda
	JC	24$
	CMPB	fdk_func[EBP], #0Dh	;Is this a format operation?
	JE	dofmt			;Yes
	MOVB	AH, fdk_daddr+2[EBP]	;No - set cylinder number
	CALL	flopcmda
	JC	24$
	MOVB	AH, fdk_daddr+1[EBP]	;Set head number
	CALL	flopcmda
	JC	24$
	MOVB	AH, fdk_daddr+0[EBP]	;Set sector number
	CALL	flopcmda
	JC	24$
	MOVB	AH, ucb_fdsecs[ESI]	;Set sector size
	CALL	flopcmda
	JC	24$
	MOVB	AH, ucb_csects[ESI]	;Get end of track value (assume want
					;  multi-sector operation)
	BTL	knlTda+tdaCmd##, #XO%NORDAH ;Want single sector transfer?
	JNC	26$			;No
	MOVB	AH, fdk_daddr+0[EBP]	;Yes - get right value
26$:	CALL	flopcmda
	JC	30$
	MOVB	AH, ucb_fdxgpl[ESI]	;GPL value
	CALL	flopcmda
	JC	30$
	MOVB	AH, #0xFF		;DTL value
28$:	CALL	flopcmda
	JC	30$
	MOVL	EBX, ucb_kcb[ESI]
	MOVB	kcb_itimer[EBX], #3t	;Start interrupt timer
	MOVL	EAX, #-1
	MOVL	EDX, EAX
	CALL	knlXfWait##
	JNC	xferdone
	JMP	xferder

;Here if doing format operation

dofmt:	MOVB	AH, ucb_fdsecs[ESI]	;Sector size
	CALL	flopcmda
30$:	JC	fdkdone
	MOVB	AH, 0			;GPL value for format
	CALL	flopcmda
	JC	30$
	MOVB	AH, #0xF6		;Data fill value
	JMP	28$			;Continue
.PAGE
;Here when transfer is complete

xferdone:
	CALL	knlDmaDisable##		;Disable our DMA channel
	ANDB	kcb_sts1[EBX], #~K1$DMAINUSE
	CALL	floprslt2		;Get remaining result bytes
xferder:JC	fdkdone
	CMPB	BL, #6			;Did we get 6 bytes?
	JNE	xfdverr			;No - fail
	MOVB	BL, ucb_fdrslt+0[ESI]	;Check ST0 result value
	CMPB	BL, ucb_select[ESI]
	JE	xfgood			;If no error
	CMPW	ucb_fdrslt+1[ESI], #0080h ;Is the error end of cylinder?
	JE	xfgood			;Yes - this is OK
	MOVW	AX, ucb_fdrslt+1[ESI]	;No - get error status bits
	CALL	xlaterror		;Translate the error indication
	CMPL	EAX, #ER_WPRER		;Write protect error?
	JE	xferr			;Yes - don't bother retrying that
	MOVL	EBX, ucb_kcb[ESI]	;No - is this the read-ahead DCB?
;;;;?????
	DECB	fdk_retry1[EBP]		;No - have we done this enough?
	JNS	doxfer4			;No - go try again
	ORB	ucb_sts1[ESI], #U1$RECAL ;Yes - recalibrate next time
	DECB	fdk_retry3[EBP]		;Should we try a restore?
	JS	xferr			;No - its time to give up!
	JMP	doxfer2			;Go try again

;Here if fatal error
;	c{EAX} = Error code
;	c{EDX} = Offset of error counter pair

xferr:	TESTL	EDX, EDX		;Do we have an error counter?
	JE	fdkdone			;No
	INCL	4[EDX]			;Yes - bump the hard error counter
	JMP	fdkdone

;Here with good transfer complete

xfgood:	CALL	knlDmaGetCnt##		;Get the transfer count
	MOVL	EBX, ucb_kcb[ESI]
	SUBL	EAX, fdk_count[EBP]	;Calculate amount actually transfered
	NEGL	EAX
	ADDL	fdk_amount[EBP], EAX	;Add in to total amount transfered
	TESTB	ucb_bits[ESI], #UB$MEDIAC ;Does drive have a media sensor?
	JE	4$			;No
	TESTB	kcb_sts2[EBX], #K2$MSENSOR ;Yes - are we using it now?
	JNE	4$			;Yes
	CMPW	fdk_daddr+2[EBP], #0	;No - on track 0 now?
	JE	4$			;Yes
	MOVL	EDX, ucb_ioreg1[ESI]	;No - get current media change bit
	A2IOP	P_DCFDDIR		;  state
	INB	[DX]
	IOPAUSE
	ANDB	AL, #0x80
	MOVB	kcb_fdmssen[EBX], AL
	ORB	kcb_sts2[EBX], #K2$MSENSOR
4$:	TESTB	fdk_mode[EBP], #FM$LIST ;Do we have a buffer list?
	JE	6$			;No - all done
	MOVL	EAX, fdk_buffer[EBP]	;Yes
	ORB	bfr_status[EAX], #BS$FULL ;Indicate this buffer is full
	MOVL	EAX, bfr_nextxfr[EAX]	;Get next buffer
	TESTL	EAX, EAX
	JE	fdkdone			;Done if no more
	MOVL	fdk_buffer[EBP], EAX	;More - advance buffers
	INCL	fdk_block[EBP]		;Bump disk block number
	MOVL	EDX, fdk_block[EBP]
	CALL	fdkbk2phy2		;Convert to physical disk address
	MOVL	fdk_daddr[EBP], EDX
	JMP	doxfer0			;Go transfer next buffer

;Here when finished with a good transfer

6$:	CLRL	EAX			;Indicate no error
fdkdone:MOVL	EBX, ucb_kcb[ESI]
	TESTB	kcb_sts1[EBX], #K1$DMAALLOC ;Do we have the DMA channel?
	JE	10$			;No
	PUSHL	EAX			;Yes
	TESTB	kcb_sts1[EBX], #K1$DMAINUSE ;Is it in use?
	JE	8$			;No
	CALL	knlDmaDisable##		;Yes - disable it
8$:	MOVL	ECX, #fdkclnup
	PUSHL	EDI
	MOVL	EDI, EBX
	CALL	knlDmaGiveChn##		;Give up DMA channel
	ANDB	kcb_sts1[EBX], #~{K1$DMAALLOC|K1$DMAINUSE}
	POPL	EDI
	POPL	EAX
10$:	TESTB	ucb_sts1[ESI], #U1$MOTON ;Is our motor running now?
	JE	12$			;No
	MOVB	CL, ucb_fdmot[ESI]	;Yes - start motor off timer
	MOVB	ucb_fdmoc[ESI], CL
12$:	TESTL	EAX, EAX
	JNS	16$			;If no error
	ORB	ucb_sts1[ESI], #U1$RECAL ;Error - recalibrate next time
	MOVL	ECX, fdk_amount[EBP]
	STC				;Indicate error
16$:	LEAVE
	RET

;Here to report hard device error

xfdverr:INCL	ucb_tdeverr[ESI]	;Count the error
	INCL	ucb_hdeverr[ESI]
	MOVL	EAX, #ER_DEVER		;Get error code
	JMP	fdkdone
.PAGE
;Subroutine to translate error indication from the drive and to increment the
;  total error counter
;	c[AX] = Error bits from the drive
;	CALL	xlaterror
;	c{EAX} = knl error code
;	c{EDX} = Offset of error counter pair (total error count followed by
;		   hard error count)

xlaterror:
	MOVL	EDX, #errtable		;Point to error table
	MOVL	ECX, #6
4$:	TESTW	[EDX], AX		;Check for this error
	JNE	6$			;Found it
	ADDL	EDX, #6t
	LOOP	ECX, 4$
6$:	MOVXWL	EAX, 2t[EDX]		;Get error code
	MOVZWL	EDX, 4t[EDX]		;Get relative offset of error counter
	TESTL	EDX, EDX		;Do we have one?
	JE	8$			;No
	ADDL	EDX, ESI		;Yes - get its actual offset
	INCL	[EDX]			;Increment the soft error counter
8$:	RET
.PAGE
	.SBTTL	kf_chkwp - Check write protect status

;Here for the check write protect status function at fork level
;	c{EDI} = Offset of DCB
;	CALL	fdkchkwp

fdkchkwpf:
	ENTER	fdk_SIZE, 0
	MOVB	fdk_mode[EBP], #0
	MOVL	EBX, ucb_kcb[ESI]
	MOVB	AL, kcb_fdmoto[EBX]	;Get current motor on bits
	ORB	AL, ucb_fdmoto[ESI]	;Plus our motor on bit
	ORB	AL, ucb_select[ESI]	;Include our select value
	MOVL	EDX, ucb_ioreg1[ESI]	;Give it to the controller
	A2IOP	P_DCFDDOR
	OUTB	[DX]
	IOPAUSE
	ANDB	AL, #3
	MOVB	kcb_fdselect[EBX], AL
	MOVB	AH, #04h		;Do a sense drive status command
	CALL	flopcmdi
	JC	10$
	MOVB	AH, ucb_select[ESI]	;Head and unit select
	CALL	flopcmda
	JC	10$
	CALL	floprslt
	JC	10$
	DECL	EBX
	JNE	8$
	MOVL	EBX, ucb_kcb[ESI]
	MOVB	AL, kcb_fdmoto[EBX]	;Restore correct motor states
	ORB	AL, ucb_select[ESI]	;Include our select value
	MOVL	EDX, ucb_ioreg1[ESI]
	A2IOP	P_DCFDDOR
	OUTB	[DX]
	IOPAUSE
	ANDB	ucb_sts1[ESI], #{~U1$WPROT}&0FFh ;Assume not write protected
	TESTB	ucb_fdrslt[ESI], #40h	;Is the drive write protected?
	JE	6$			;No
	ORB	ucb_sts1[ESI], #U1$WPROT ;Yes - remember that
6$:	CLRL	EAX
	JMP	fdkdone

;Here if error while have drive selected

8$:	INCL	ucb_tdeverr[ESI]	;Count the error
	INCL	ucb_hdeverr[ESI]
	MOVL	EAX, #ER_DEVER
10$:	PUSHL	EAX			;Save error code
	MOVL	EBX, ucb_kcb[ESI]
	MOVB	AL, kcb_fdmoto[EBX]	;Restore correct motor states
	ORB	AL, ucb_select[ESI]	;Include our select value
	MOVL	EDX, ucb_ioreg1[ESI]
	A2IOP	P_DCFDDOR
	OUTB	[DX]
	IOPAUSE
	POPL	EAX			;Restore error code
	JMP	fdkdone
.PAGE
	.SBTTL	kf_chkchg - Check for possible disk change at fork level

;Here for the check for possible disk change function at fork level
;	c{EDI} = Offset of DCB
;	CALL	fdkchkchf

fdkchkchgf:
	ENTER	fdk_SIZE, 0
	MOVB	fdk_mode[EBP], #0
	MOVL	EBX, ucb_kcb[ESI]
	CALL	chgbit			;Is the disk changed bit set?
	JNC	8$			;No - nothing to do here!
	CALL	getcont			;Yes - disk may have been changed
	MOVB	fdk_retry3[EBP], #8t	;Initialize retry count
2$:	MOVL	fdk_daddr[EBP], #1	;Assume should seek to track 0
	CMPB	ucb_fdctrk[ESI], #0	;On track 0 now?
	JNE	4$			;No
	INCB	fdk_daddr+2[EBP]	;Yes - seek to track 1
4$:	CALL	doseek			;Do seek to try and clear disk change
					;  indication
	JC	8$			;If error
	CALL	chgbit			;Did disk change indication go away?
	JNC	6$			;Yes
	DECB	fdk_retry3[EBP]		;No - have we done this enough?
	JNE	2$			;No - go try again
	JMP	8$			;Yes - go report the error

;Here if the disk changed indication was cleared - indicate disk changed

6$:	MOVL	EAX, #ER_MDCHG
8$:	JMP	fdkdone
.PAGE
	.SBTTL	chgbit - Subroutine to check disk change bit

;Subroutine to check disk change bit
;	CALL	chgbit
;	C:set = Error (bit set, reported as not ready)
;	  c{EAX} = Error code
;	C:clr = Normal (bit clear)
;	  c{EAX}  = 0

chgbit:	MOVB	AL, kcb_fdmoto[EBX]	;Get current motor on bits
	ORB	AL, ucb_fdmoto[ESI]	;Plus our motor on bit
	ORB	AL, ucb_select[ESI]	;Include our select value
	MOVL	EDX, ucb_ioreg1[ESI]	;Give it to the controller
	A2IOP	P_DCFDDOR
	OUTB	[DX]
	IOPAUSE
	ANDB	AL, #3
	MOVB	kcb_fdselect[EBX], AL
	A2IOP	P_DCFDDIR-P_DCFDDOR
	INB	[DX]			;Get the disk change bit
	IOPAUSE
	MOVB	AH, AL
	MOVB	AL, kcb_fdmoto[EBX]	;Restore correct motor states
	ORB	AL, ucb_select[ESI]	;Include our select value
	A2IOP	P_DCFDDOR-P_DCFDDIR
	OUTB	[DX]
	IOPAUSE
	XORB	AH, kcb_fdmssen[EBX]
	JS	6$			;If set
	CLRL	EAX			;Not set
	RET

6$:	MOVL	EAX, #ER_NTRDY		;No - say may have been changed
	STC
	RET
.PAGE
	.SBTTL	doseek - Device fork level subroutine to do seek

;Device fork level subroutine to do seek
;	CALL	doseek
;	C:set = Error
;	  c{EAX} = Error code (do not have DMA channel)
;	C:clr = Normal (DMA channel allocated)

doseek:	MOVB	ucb_fdmoc[ESI], #0	;Stop motor off timer
	MOVW	AX, ucb_fdspcfy[ESI]	;Do we need to do a specify command?
	CMPW	kcb_fdspcfy[EBX], AX
	JE	4$			;No
	MOVW	kcb_fdspcfy[EBX], AX	;Yes - remember new value
	MOVB	AH, #3			;Yes - issue specify command
	CALL	flopcmdi
	JC	2$
	MOVB	AH, kcb_fdspcfy+0[EBX]	;Step rate time and head unload time
	CALL	flopcmda
	JC	2$
	MOVB	AH, kcb_fdspcfy+1[EBX]	;Head load time and DMA mode
	CALL	flopcmda
	JNC	4$			;Go on if OK
2$:	MOVW	kcb_fdspcfy[EBX], #0	;Error - force specify command next time
	JMP	done4

4$:	MOVB	AL, ucb_fdtrkd[ESI]	;Get our track density
	CMPB	kcb_fdtrkd[EBX], AL	;Do we need to change it?
	JE	10$			;No
	MOVB	kcb_fdtrkd[EBX], AL	;Yes - remember new value
	MOVL	EDX, ucb_ioreg1[ESI]
	CMPB	kcb_fdtype[EBX], #FCT_CMPT ;Is this a ComptiCard?
	JNE	6$			;No
	A2IOP	P_DCFDCR2		;Yes
	MOVB	AL, cmptcr2[EAX]	;Get right value
	JMP	8$			;Continue

;Here if have normal AT floppy controller

6$:	A2IOP	P_DCFDCTL		;Give it to the controller
8$:	OUTB	[DX]
	IOPAUSE
10$:	MOVB	AL, ucb_fdmoto[ESI]	;Get our motor on bit
	ORB	kcb_fdmoto[EBX], AL	;Set in in the KCB
	MOVB	AL, kcb_fdmoto[EBX]	;Get all motor on bits
	ORB	AL, ucb_select[ESI]	;Put in our select value
	MOVL	EDX, ucb_ioreg1[ESI]	;Give it to the controller (this
	A2IOP	P_DCFDDOR		;  selects drive and turns its motor
	OUTB	[DX]			;  on if it was off)
	IOPAUSE
	ANDB	AL, #3
	MOVB	kcb_fdselect[EBX], AL	;Save just the select bits
	TESTB	ucb_sts1[ESI], #U1$MOTON ;Is the motor on now?
	JNE	motoro4			;Yes
	MOVL	EAX, ucb_fdmst[ESI]
	CLRL	EDX
	CALL	knlXfTimeWait##
	JNC	12$
	MOVB	ucb_fdmoc[ESI], #1	;Make sure we turn the motor off soon
	JMP	done4

12$:	ORB	ucb_sts1[ESI], #U1$MOTON ;Indicate motor is on now
motoro4:MOVB	AL, ucb_fdmot[ESI]
	MOVB	ucb_fdmoc[ESI], AL
	MOVL	EBX, ucb_kcb[ESI]
	TESTB	ucb_sts1[ESI], #U1$RECAL ;Need to recalibrate now?
	JE	rstrdn2			;No
	ANDB	ucb_sts1[ESI], #~U1$RECAL ;Yes
	MOVB	fdk_retry2[EBP], #3	;Set retry count for seek
again2:	MOVB	fdk_retry1[EBP], #3	;Set retry count for restore
again4:	MOVB	ucb_fdctrk[ESI], #0	;Going to track 0
	ORB	kcb_sts1[EBX], #K1$INT	;Indicate expecting interrupt now
	MOVB	AH, #0x07			;Start a restore operation
	CALL	flopcmdi
	JC	done2			;If error
	MOVB	AH, ucb_select[ESI]	;Unit select
	ANDB	AH, #3
	CALL	flopcmda
	JC	done2			;If error
	MOVL	EBX, ucb_kcb[ESI]	;OK
	MOVB	kcb_itimer[EBX], #3t	;Start interrupt timer
	MOVL	EAX, #-1
	MOVL	EDX, EAX
	CALL	knlXfWait##
	JNC	rstrdone
	JMP	done2
.PAGE
;Here if error on seek (from below)

2$:	INCL	ucb_tseekerr[ESI]	;Count the seek error
	DECB	fdk_retry1[EBP]		;Should we try the restore again?
	JG	again4			;Yes
	INCL	ucb_hseekerr[ESI]	;No - count it as a hard error
done2:	JMP	done8			;Fail!

;Here at device fork level after restore is complete

rstrdone:
	CALL	chkseek			;No - check for seek error
	MOVL	EBX, ucb_kcb[ESI]	;Restore KCB offset
	JC	2$			;If error
rstrdn2:ORB	kcb_sts1[EBX], #K1$INT	;Indicate expecting interrupt now
	CMPB	fdk_daddr+1[EBP], #0	;Which side?
	JNE	6$			;Side 1
	ANDB	ucb_select[ESI], #~4	;Side 0 - clear head select bit
	JMP	8$

6$:	ORB	ucb_select[ESI], #4	;Set head select bit
8$:	MOVB	AL, fdk_daddr+2[EBP]	;Are we on the right track now?
	CMPB	ucb_fdctrk[ESI], AL
	JE	done4			;Yes - finished now
	MOVB	AH, #0x0F		;No - start a seek operation
	CALL	flopcmdi
	JC	done8
	MOVB	AH, ucb_select[ESI]	;Head and unit select
	CALL	flopcmda
	JC	done8
	MOVB	AH, fdk_daddr+2[EBP]	;Get requested track
	TESTB	ucb_sts1[ESI], #U1$HFTRK ;Is this a 48 tpi disk in a 96 tpi
					 ;  drive?
	JE	10$			;No
	ADDB	AH, AH			;Yes - adjust the track number
10$:	CALL	flopcmda		;Give final command byte to the
	JC	done8			;  controller
	MOVL	EBX, ucb_kcb[ESI]
	MOVB	kcb_itimer[EBX], #3t	;Start interrupt timer
	MOVL	EAX, #-1		;Wait for the seek to finish
	MOVL	EDX, EAX
	CALL	knlXfWait##
	JC	done4			;If error
	CALL	chkseek			;Check for seek error
	MOVL	EBX, ucb_kcb[ESI]	;Restore KCB offset
	JC	14$			;If error
	MOVB	AL, fdk_daddr+2[EBP]	;OK - store new current track
	MOVB	ucb_fdctrk[ESI], AL
done4:	MOVL	EBX, ucb_kcb[ESI]
	RET

;Here if error on the seek

14$:	INCL	ucb_tseekerr[ESI]	;Count the seek error
	DECB	fdk_retry2[EBP]		;Should we try again?
	JG	again2			;Yes - go restore and then seek
	INCL	ucb_hseekerr[ESI]	;No - count as hard seek error
done8:	STC
	JMP	done4
.PAGE
	.SBTTL	flopcmdi - Subroutine to send initial command byte to floppy controller

;Subroutine to send initial command byte to the floppy controller
;	c(AH) = Command byte value
;	CALL	flopcmdi
;	C:clr = Normal
;	C:set = Error
;	  c{EAX} = Error code
;  EBX is respected

flopcmdi:
	MOVL	EDX, ucb_ioreg1[ESI]	;Get IO port
	A2IOP	P_DCFDSTS
	INB	[DX]			;Get controller status
	IOPAUSE
	ANDB	AL, #0xC0		;Just high 2 bits
	CMPB	AL, #0x80		;Is it ready for data?
	JE	8$			;Yes
	PUSHL	EAX			;No - wait for a short time
	MOVL	EAX, #50t*ST_MILLISEC
	CLRL	EDX
	CALL	knlXfTimeWait##
	POPL	EAX
	INB	[DX]			;Is it ready now?
	IOPAUSE
	ANDB	AL, #0xC0
	CMPB	AL, #0x80
	JE	8$			;Yes
	PUSHL	EBX			;No
	CALL	floprslt		;Assume need to get some result bytes!
	POPL	EBX
	JC	6$			;If error
					;OK - fall into flopcmda to try again

	.SBTTL	flopcmda - Subroutine to send additional command byte to floppy controller

;Subroutine to send additional command byte to the floppy controller
;	c(AH) = Command byte value
;	CALL	flopcmda
;	C:clr = Normal
;	C:set = Error
;	  c{EAX} = Error code

flopcmda:
	MOVL	EDX, ucb_ioreg1[ESI]	;Get IO port
	A2IOP	P_DCFDSTS
	MOVL	ECX, #0x30000		;Initialize retry counter
4$:	INB	[DX]			;Get controller status
	IOPAUSE
	ANDB	AL, #0xC0		;Just high 2 bits
	CMPB	AL, #0x80		;Is it ready for data?
	JE	8$			;Yes
	LOOP	ECX, 4$			;No - wait some more!
	INCL	ucb_tdeverr[ESI]	;Count the error
	INCL	ucb_hdeverr[ESI]
	MOVL	EAX, #ER_DEVER		;Indicate device error
	STC
6$:	RET

;Here when controller is ready for data

8$:	MOVB	AL, AH			;Get value
	INCL	EDX			;Point to data port
	OUTB	[DX]			;Output the byte
	IOPAUSE
	RET				;Finished (C is clear)
.PAGE
	.SBTTL	chkseek - Subroutine to check for error after seek

;Subroutine to check for error after seek is complete
;	c{EBX} = Offset of KCB
;	c{ESI} = Offset of UCB
;	CALL	chkseek
;	C:set = Error
;	  c{EAX} = Error code
;	C:clr = Normal

chkseek:CALL	floprslt		;Get result bytes
	JC	2$
	DECL	EBX
	JS	2$
	MOVB	AL, ucb_fdrslt+0[ESI]	;Check ST0 value
	ANDB	AL, #0xF8
	CMPB	AL, #0x20
	JE	4$			;OK
2$:	MOVL	EAX, #ER_SEKER		;Indicate seek error
	STC
4$:	RET
.PAGE
	.SBTTL	floprslt - Subroutine to get result bytes from floppy controller

;Subroutine to get result bytes from the floppy controller
;	CALL	floprslt
;	C:set = Error
;	  c{EAX} = Error code
;	  c{EBX} = Number of result bytes obtained
;	C:clr = Normal

floprslt:
	MOVL	EDX, ucb_ioreg1[ESI]
	PUSHL	ESI			;Save UCB offset
	ADDL	ESI, #ucb_fdrslt	;Point to buffer for the result bytes
	JMP	2$
					;Enter here if already have first
floprslt2:				;  result byte
	MOVL	EDX, ucb_ioreg1[ESI]
	PUSHL	ESI			;Save UCB offset
	ADDL	ESI, #ucb_fdrslt+1	;Point to buffer for the result bytes
2$:
	MOVL	ECX, #10
	CALL	knlSDelay##

	CLRL	EBX
	A2IOP	P_DCFDSTS
4$:	MOVL	ECX, #0x30000		;Get loop count
6$:	INB	[DX]			;Get controller status
	IOPAUSE
	ANDB	AL, #0xC0		;Only high 2 bits
	CMPB	AL, #0xC0		;Is a result byte ready?
	JE	8$.S			;Yes
	LOOP	ECX, 6$			;No - wait some more
	JMP	12$			;Go report device error

;Here with result byte ready

8$:	INCL	EDX			;Point to the data port
	INCL	EBX
	INB	[DX]			;Get the result byte
	IOPAUSE
	MOVB	[ESI], AL		;Store the byte
	INCL	ESI			;Bump pointer
	MOVL	ECX, #3t
	CALL	knlSDelay##		;Wait for at least 12us
	DECL	EDX			;Point to the status port
	INB	[DX]			;Get status
	IOPAUSE
	TESTB	AL, #0x10		;More result bytes available?
	JE	14$			;No - all finished here
	CMPB	BL, #8			;Yes - is this too many?
	JB	4$			;No - go get another
12$:	POPL	ESI			;Yes
	INCL	ucb_tdeverr[ESI]	;Report the hard device error
	INCL	ucb_hdeverr[ESI]
	MOVL	EAX, #ER_DEVER
	STC
	RET

;Here if OK (C is clear)

14$:	POPL	ESI
	RET
.PAGE
	.SBTTL	getcont - Subroutine to fully allocate floppy controller

;Subroutine to fully allocate floppy controller
;	CALL	getcont

getcont:MOVB	dcb_dmachnl[EDI], #2	;Make sure have DMA channel number
	PUSHL	EDI
	MOVL	EDI, EBX
	CALL	knlXfDmaGetChn##	;Get the DMA channel
	POPL	EDI
	JC	4$			;If error
	ORB	kcb_sts1[EBX], #K1$DMAALLOC ;Indicate DMA channel allocated
	MOVL	curkcb, EBX		;This is now the current KCB
	TESTB	knlMachType##+3, #02h	;Is this a PS/2?
	JNE	4$			;Yes
	ORB	kcb_fdmoto[EBX], #08h	;No - enable interrupts and DMA
	MOVB	AL, kcb_fdmoto[EBX]
	MOVL	EDX, ucb_ioreg1[ESI]
	A2IOP	P_DCFDDOR
	OUTB	[DX]
	IOPAUSE
	INB	P_INC1P1		;Enable our interrupt
	IOPAUSE
	ANDB	AL, #~40h
	OUTB	P_INC1P1
	IOPAUSE
	CLC
4$:	RET
.PAGE
	.SBTTL	fdkclnup - Subroutine to do clean-up after using DMA channel

;Subroutine to do clean-up after using DMA channel - this subroutine is
;  called after we have given up the DMA channel but before it is given to
;  anyone else - the only valid register is EDI, which points to our DCB -
;  all registers except EAX must be preserved
;	c{EDI} = Offset of KCB
;	CALL	fdkclnup

fdkclnup:
	PUSHL	EDX
	INB	P_INC1P1		;Disable our interrupt
	IOPAUSE
	ORB	AL, #40h
	OUTB	P_INC1P1
	IOPAUSE
	ANDB	kcb_fdmoto[EDI], #~08	;Disable interrupts and DMA at
	MOVB	AL, kcb_fdmoto[EDI]	;  interface
	MOVL	EDX, kcb_ioreg1[EDI]
	A2IOP	P_DCFDDOR
	OUTB	[DX]
	IOPAUSE
	POPL	EDX
	RET				;Thats all
.PAGE
	.SBTTL	fdkbk2phy - Subroutine to convert block number to physical disk address

;Subroutine to convert block number to physical disk address.  A file structured
;  double density floppy disk has the following format:
;    Tracks:        40 (0-39) or 80 (0-79)
;    Heads/Track:   2
;    Sectors/Track: 9
;    Byte/sector:   512
;  A file structured high density floppy disk has the following format:
;    Tracks:        80 (0-79)
;    Heads/Track:   2
;    Sectors/Track: 15
;    Byte/sector:   512
;	c<dcb_dkdskblk{EDI}> = Block number on disk
;	c{EDI}             = Offset of DCB
;	CALL	fdkbk2phy
;	c{EDX} = Sector number (8) Head number (8) Track number (16)
;  All registers except EDX are preserved

fdkbk2phy:
	MOVL	EDX, dcb_dkdskblk+0[EDI] ;Get block number on disk
fdkbk2phy2:
	PUSHL	EAX
	PUSHL	ECX
	MOVL	EAX, EDX
	MOVL	ECX, ucb_csects[ESI]	;Get sectors per track
	TESTL	ECX, ECX		;Make sure not 0!
	JNE	2$
	INCL	ECX
2$:	CLRL	EDX
	DIVL	ECX			;Get sector number
	INCB	DL
	PUSHL	EDX			;Save sector number
	MOVB	DL, #0
	CMPB	ucb_cheads[ESI], #1	;Double sided disk?
	JE	4$			;No
	SHRL	EAX, #1			;Yes - get head and track numbers
	RCLB	DL, #1
4$:	MOVB	1[ESP], DL		;Store head number
	MOVW	2[ESP], AX		;Store track number
	POPL	EDX			;Get in right register
	POPL	ECX
	POPL	EAX
	RET
.PAGE
	.SBTTL	fdkoas - Floppy disk once-a-second routine

;Here once-a-second.  If we detect a hung controller, we reset the controller
;  and restart the timer.  It should interrupt immediately, which we will
;  detect as a hung controller interrupt because the K1$RESET bit will be set.
;  If it times out again, we fake the interrupt directly from here and hope
;  for the best.  In this case, the controller is probably dead!

fdkoas:	MOVL	EBX, fdkakcb		;Get our first KCB
2$:	TOFORK
	CMPB	kcb_itimer[EBX], #0	;Are we timing an interrupt now?
	JE	6$			;No
	DECB	kcb_itimer[EBX]		;Yes - reduce count
	JNE	6$			;If not out yet
	MOVL	EDI, kcb_curdcb[EBX]	;Timed-out - get DCB
	TESTL	EDI, EDI
	JE	6$
	MOVL	ESI, dcb_dkucb[EDI]	;And get UCB
	CALL	knlDmaDisable##		;Disable our DMA channel
	CALL	knlDmaGetCnt##		;Get the transfer count
	MOVL	EDX, ucb_ioreg1[ESI]
	A2IOP	P_DCFDSTS
	INB	[DX]
	IOPAUSE
	TESTB	kcb_sts1[EBX], #K1$RESET ;Already doing reset?
	JE	4$			;No - go on
	PUSHL	EBX			;Yes
	CALL	fdkfrk2			;Do timeout stuff now
	POPL	EBX
	JMP	6$			;Continue

;Here if not already resetting timed out controller

4$:	ORB	kcb_sts1[EBX], #K1$RESET ;Indicate doing reset
	ANDB	kcb_sts1[EBX], #~K1$RESULT
	MOVL	EDX, ucb_ioreg1[ESI]	;Reset the floppy controller
	A2IOP	P_DCFDDOR
	MOVB	AL, #8
	OUTB	[DX]
	IOPAUSE
	MOVB	ucb_fdmoc[ESI], #0	;Clear motor off timer
	ANDB	ucb_sts1[ESI], #~U1$MOTON
	MOVB	AL, ucb_fdmoto[ESI]	;Clear our motor on bit
	NOTB	AL
	ANDB	kcb_fdmoto[EBX], AL
	MOVB	AL, kcb_fdmoto[EBX]
	ORB	AL, ucb_select[ESI]	;Remove the reset state (this should
	ORB	AL, #0x0C		;  cause an interrupt)
	OUTB	[DX]
	IOPAUSE
	MOVB	kcb_itimer[EBX], #2t	;Restart the timer
6$:	FROMFORK
	MOVL	ESI, kcb_fucb[EBX]	;Get first UCB
	TESTL	ESI, ESI
	JE	16$			;If none
10$:	TOFORK
	CMPB	ucb_fdmoc[ESI], #0	;Are we timing the motor?
	JE	12$			;No
	DECB	ucb_fdmoc[ESI]		;Yes - count it down
	JNE	12$			;Time to turn it off?
	MOVB	AL, ucb_fdmoto[ESI]	;Yes - get the motor bit
	NOTB	AL
	ANDB	kcb_fdmoto[EBX], AL	;Clear the bit in the KCB
	MOVB	AL, kcb_fdmoto[EBX]	;And turn off the motor
	ORB	AL, kcb_fdselect[EBX]	;Preserve current select value
	MOVL	EDX, ucb_ioreg1[ESI]
	A2IOP	P_DCFDDOR
	OUTB	[DX]
	IOPAUSE
	ANDB	ucb_sts1[ESI], #~U1$MOTON
12$:	FROMFORK
	MOVL	ESI, ucb_next[ESI]	;Advance to next UCB
	TESTL	ESI, ESI
	JNE	10$			;Continue if another
16$:	MOVL	EBX, kcb_nexts[EBX]	;Advance to next KCB
	TESTL	EBX, EBX
	JNE	2$			;Continue if another
	RET
.PAGE
	.SBTTL	fdkint - Floppy disk interrupt routine

fdkint:	PUSHAL				;Save all registers
	PUSHL	DS
	PUSHL	ES
	PUSHL	FS
	PUSHL	GS
	PUSHL	#GS_XDATA
	POPL	DS
	MOVL	EDI, curkcb		;Get our current KCB
	MOVL	EDX, kcb_ioreg1[EDI]	;Point to data register
	A2IOP	P_DCFDDATA
	TESTB	kcb_sts1[EDI], #K1$INT	;Expecting interrupt now?
	JE	18$			;No
	TESTB	kcb_sts1[EDI], #K1$RESULT ;Yes - in result phase now?
	JNE	2$			;Yes
	MOVB	AL, #0x08		;No - do sense interrupt command
	OUTB	[DX]
	IOPAUSE
	JMP	8$

;Here if in result phase now

2$:	MOVL	ECX, #0x1000
	DECL	EDX
4$:	INB	[DX]
	ANDB	AL, #0xC0
	CMPB	AL, #0xC0
	LOOPNE	ECX, 4$
	INCL	EDX
	INB	[DX]			;Get first result byte
	IOPAUSE
	MOVL	EDX, kcb_curdcb[EDI]	;Get current DCB
	MOVL	ESI, dcb_dkucb[EDX]
	MOVB	ucb_fdrslt+0[ESI], AL	;Store first result byte
8$:	CALL	knlReqFork##		;Yes - request fork
	TESTB	knlMachType##+3, #0x02	;Is this a PS/2?
	JE	14$			;No - don't wait for interrupt to go
	MOVL	EDX, kcb_ioreg1[EDI]	;  away
	MOVL	ECX, #0x1000		;Yes - must wait for interrupt request
12$:	INB	[DX]			;  to go away before dismissing the
	IOPAUSE
	TESTB	AL, #0x80		;  interrupt!
	LOOPNE	ECX, 12$
	JNE	20$
14$:	JMP	knlIntLowDone##		;Dismiss interrupt

;Here if not expecting interrupt.  If not running on a PS/2, we just dismiss it
;  and hope everything will continue to work (this is likely to happen when we
;  first enable a controller).  If running on a PS/2, we crash, since we can't
;  ignore a level triggered interrupt!

18$:	TESTB	knlMachType##+3, #0x02	;Is this a PS/2?
	JE	14$			;No - just dismiss the interrupt
	CRASH	SPRI			;[SPuRious Interrupt]

;Here if can't clear the interrupt request on a PS/2.  There is nothing we can
;  do in this case, since we can't dismiss a PS/2 interrupt while the request
;  is still active!

20$:	CRASH	HNGI			;[HuNG Interrupt request]
.PAGE
;Device fork level routine for floppys

fdkfork:MOVL	EBX, EDI
fdkfrk2:MOVL	EDI, kcb_curdcb[EBX]
	MOVL	ESI, dcb_dkucb[EDI]	;Get UCB offset
	TESTL	ESI, ESI
	JE	8$
	MOVB	kcb_itimer[EBX], #0	;Clear interrupt timer
	TESTB	kcb_sts1[EBX], #K1$RESET ;Doing reset now?
	JNE	4$			;Yes - this is a timeout
	ANDB	kcb_sts1[EBX], #{~{K1$INT|K1$RESULT}}&0FFh ;No
	CLRL	EAX			;Everything is normal
2$:	JMP	knlResumeOutput##

;Here if doing reset - this means we had a time-out

4$:	ANDB	kcb_sts1[EBX], #{~{K1$RESET|K1$INT|K1$RESULT}}&0FFh
	CALL	floprslt		;Get result bytes
	MOVL	EBX, ucb_kcb[ESI]
	ORB	ucb_sts1[ESI], #U1$RECAL ;Recalculate next time
	MOVL	EAX, #ER_NTRDY		;Assume should report as not ready
	TESTB	ucb_bits[ESI], #UB$MEDIAC ;Does it have a media sensor?
	JE	2$			;No
	TESTB	kcb_sts2[EBX], #K2$MSENSOR ;Yes - are we using it now?
	JE	2$			;No
	INCL	ucb_hungerr[ESI]	;Yes
8$:	MOVL	EAX, #ER_NORSP		;Report this as a timeout
	JMP	2$
.PAGE
	.SBTTL	Data

;Table which gives correspondense between result byte values and error codes

	.MOD	2
errtable:
	.WORD	0020h, ER_DATER, ucb_tdataerr
	.WORD	0101h, ER_IDFER, ucb_tidferr
	.WORD	0004h, ER_RNFER, ucb_trnferr
	.WORD	1200h, ER_SEKER, ucb_tseekerr
	.WORD	0002h, ER_WPRER, 0
	.WORD	0010h, ER_LSTER, ucb_tovrnerr
	.WORD	0000h, ER_DEVER, ucb_tdeverr

;Table which gives motor on bits as a function of drive on controller

motoron:.BYTE	10h		;Drive 1
	.BYTE	20h		;Drive 2
	.BYTE	40h		;Drive 3
	.BYTE	80h		;Drive 4

;Table giving values for the ComptiCard control register 2

cmptcr2:.BYTE	0Fh	; 0 - 500kb
	.BYTE	1	; 1 - 300kb
	.BYTE	0	; 2 - 250kb
	.BYTE	0	; 3 - Illegal

	DATA

	.MOD	4
fdkakcb:.LONG	0		;Offset of first floppy KCB
curkcb: .LONG	0		;Offset of current floppy KCB
lit5:	.LONG	5t
lit6:	.LONG	6t
hwname: .LONG	'DISK', 'fdka', '03F0', 0

	LKEEND
