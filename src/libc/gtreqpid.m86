	.TITLE	getreqpid - Function to process requestor PID argument for symbiont
	.SBTTL	Run-time library for XC for XOS

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.INCLUD XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\CTYPE.PAR
	.INCLUD	XOSINC:\XMAC\STDIO.PAR

	.STDSEGS

	.PSECT	_TEXT_p

;Subroutine to process requestor PID argument for symbiont
;	char *getreqpid(
;	    char *args,		// Address of base of argument msect
;	    long *reqpid);	// address of long to receive requestor PID
;  Value returned is address of character after PID value in the command tail
;    if normal or 0 if error

gpid_args=!8
gpid_pid =!12t

	.ENTRY	getreqpid
getreqpid:
	PUSHL	ESI
	MOVL	ESI, gpid_args[ESP]	;Get address of argument msect
	MOVL	ESI, arg_cmdofs[ESI]	;Get address of argument string
	CLRL	EAX
2$:	MOVZBL	EAX, [ESI]		;Find first non-whitespace character
	INCL	ESI
	TESTB	_chrtbl#[EAX], #CT$SPACE
	JNE	2$
	CMPB	AL, #0
	JE	14$
4$:	MOVZBL	EAX, [ESI]		;Now find first whitespace character
	INCL	ESI			;  (this skips the program name)
	CMPB	AL, #0
	JE	14$
	TESTB	_chrtbl#[EAX], #CT$SPACE
	JE	4$
6$:	MOVZBL	[ESI]			 ;Find first non-whitespace character
	INCL	ESI			 ;  (this gets us to the start of the
	TESTB	_chrtbl#[EAX], #CT$SPACE ;  process ID argument)
	JNE	6$
	CLRL	EDX
8$:	TESTB	_chrtbl#[EAX], #CT$HEXDG|CT$DIGIT ;Is this a hex digit?
	JE	12$			;No
	CMPB	AL, #'A'
	JB	10$
	ADDB	AL, #9
10$:	ANDB	AL, #0F
	SHLL	EDX, #4t
	ORL	EDX, EAX
	MOVZBL	EAX, [ESI]
	INCL	ESI
	JMP	8$

;Here with character which is not a hex digit

12$:	TESTB	_chrtbl#[EAX], #CT$SPACE ;Is this character a whitespace?
	JNE	18$			;Yes - go on
	CMPB	AL, #0			;No - end of command?
	JE	16$			;Yes
14$:	CLRL	EAX			;No - return 0 to indicate error
	POPL	ESI
	RET

;Here with valid PID value

18$:	DECL	ESI
16$:	MOVL	EAX, gpid_pid[ESP]	;Get offset of long for value
	MOVL	[EAX], EDX		;Store value
	MOVL	EAX, ESI		;Get argument offset to return
	POPL	ESI
	RET

	.END
