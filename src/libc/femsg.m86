	.TITLE	femsg - Function to display fatal error message
	.SBTTL	Run-time library for XC for XOS

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD	XOSINC:\XMAC\XOSERR.PAR
	.INCLUD	XOSINC:\XMAC\STDIO.PAR

	.STDSEGS

	.PSECT	_DATA_p

fmt1:	.ASCIZ	{LF}"? %s: %s"
fmt2:	.ASCIZ	"; %s"

	.PSECT	_TEXT_p

;Function to display fatal error message
;	const char *progname;
;	long  code;
;	const char *msg;
;	femsg(progname, code, msg);
;  NEVER RETURNS

$$$=!0
FRM fem_bufr1, 80t
FRM fem_bufr2, 92t
fem_SIZE=!$$$

	.ENTRY	femsg
femsg:	ENTER	fem_SIZE, 0
	PUSHL	12t[EBP]
	PUSHL	#3
	LEAL	EBX, fem_bufr1[EBP]
	PUSHL	EBX
	CALL	svcSysErrMsg#
	PUSHL	EBX			;buffer
	PUSHL	8[EBP]			;prog
	PUSHL	#fmt1
	LEAL	EAX, fem_bufr2[EBP]
	PUSHL	EAX
	CALL	sprintf#
	PUSHL	#DH_STDERR
	LEAL	EDX, fem_bufr2[EBP]
	PUSHL	EDX
	PUSHL	EAX
	CALL	svcIoOutBlock##
	CMPL	16t[EBP], #0
	JE	4$
	PUSHL	16t[EBP]
	PUSHL	#fmt2
	LEAL	EAX, fem_bufr2[EBP]
	PUSHL	EAX
	CALL	sprintf#
	ADDL	ESP, #16t
	PUSHL	#DH_STDERR
	LEAL	EDX, fem_bufr2[EBP]
	PUSHL	EDX
	PUSHL	EAX
	CALL	svcIoOutBlock##
4$:	PUSHL	#DH_STDERR
	PUSHL	#LF
	CALL	svcIoOutSingle##
	PUSHL	#1
	CALL	exit#

	.END
