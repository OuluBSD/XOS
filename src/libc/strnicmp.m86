	.TITLE	strnicmp - Function to compare two strings ignoring case
	.SBTTL	Run-time library for XC for XOS

;++++
; This software is in the public domain.  It may be freely copied and used
; for whatever purpose you see fit, including commerical uses.  Anyone
; modifying this software may claim ownership of the modifications, but not
; the complete derived code.  It would be appreciated if the authors were
; told what this software is being used for, but this is not a requirement.

;   THIS SOFTWARE IS PROVIDED BY THE AUTHORS "AS IS" AND ANY EXPRESS OR
;   IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
;   OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
;   IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY DIRECT, INDIRECT,
;   INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
;   BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
;   OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
;   ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
;   TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
;   USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;----

	.INCLUD	XOSINC:\XMAC\XOS.PAR
	.INCLUD XOSINC:\XMAC\XOSERR.PAR
	.INCLUD	XOSINC:\XMAC\STDIO.PAR
	.INCLUD	XOSINC:\XMAC\CTYPE.PAR

	.STDSEGS

	.PSECT	_TEXT_p

	.SBTTL	strnicmp - Function to compare two strings ignoring case

;Function to compare two strings ignoring case
;	const char *stra;	/* Pointer to first string */
;	const char *strb;	/* Pointer to second string */
;	int   max;		/* Maximum number of character to compare */
;	(int)val = strnicmp(stra, strb)
;  Value returned is the difference of first not-equal character (0 if
;    strings are the same)

arg_stra=!12t
arg_strb=!16t
arg_max =!20t

	.ENTRY	strnicmp
strnicmp:
	PUSHL	EBX
	PUSHL	ESI
	MOVL	ESI, arg_stra[ESP]	;Get offset of first string
	MOVL	EBX, arg_strb[ESP]	;Get offset of second string
	MOVL	ECX, arg_max[ESP]	;Get maximum amount to compare
	CLRL	EAX
	CLRL	EDX
2$:	MOVZBL	EAX, [ESI]		;Get character from first string
	INCL	ESI
	TESTB	_IsTable#+1[EAX], #_LOWER
	JE	4$
	SUBL	EAX, #0x20
4$:	MOVZBL	EDX, [EBX]
	INCL	EBX
	TESTB	_IsTable#+1[EDX], #_LOWER
	JE	6$
	SUBL	EDX, #0x20
6$:	SUBL	EAX, EDX
	JNE	10$			;If different
	CMPL	EDX, #0			;Same - end of string?
	LOOPNE	ECX, 2$			;Continue if not end and more to check
	CLRL	EAX			;No more to check or end - return equal
10$:	POPL	ESI
	POPL	EBX
	RET

	.END
